{{
  "language": "Solidity",
  "sources": {
    "solidity/contracts/BeaconRewards.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity ^0.5.17;\n\nimport \"./Rewards.sol\";\nimport \"./KeepRandomBeaconOperator.sol\";\nimport \"./TokenStaking.sol\";\n\n/// @title KEEP Random Beacon Signer Subsidy Rewards\n/// @notice Contract distributing KEEP rewards to Random Beacon signers based\n/// on the defined reward schedule.\n///\n/// The amount of KEEP to be distributed is determined by funding the contract,\n/// and additional KEEP can be added at any time.\n/// \n/// When an interval is over, it will be allocated a percentage of the remaining\n/// unallocated rewards based on its weight, and adjusted by the number of groups\n/// created in the interval if the quota is not met.\n///\n/// The adjustment for not meeting the group quota is a percentage that equals\n/// the percentage of the quota that was met; if the number of groups created is\n/// 80% of the quota then 80% of the base reward will be allocated for the\n/// interval.\n///\n/// Any unallocated rewards will stay in the unallocated rewards pool,\n/// to be allocated for future intervals. Intervals past the initially defined\n/// schedule have a weight of 100%, meaning that all remaining unallocated\n/// rewards will be allocated to the interval.\n///\n/// Groups can receive rewards once the interval they were created in is over,\n/// and the group has been marked as stale.\n/// There is no time limit to receiving rewards, nor is there need to wait for\n/// all groups from the interval to be marked as stale.\n/// Calling `receiveReward` automatically allocates the rewards for the interval\n/// the specified group was created in and all previous intervals.\n///\n/// If a group is terminated, that fact can be reported to the reward contract.\n/// Reporting a terminated group returns its allocated reward to the pool of\n/// unallocated rewards.\ncontract BeaconRewards is Rewards {\n\n\n    // Weights of the 24 reward intervals assigned over\n    // 24 * beaconTermLength days.\n    uint256[] internal beaconIntervalWeights = [\n        4, 8, 10, 12, 15, 15,\n        15, 15, 15, 15, 15, 15,\n        15, 15, 15, 15, 15, 15,\n        15, 15, 15, 15, 15, 15\n    ];\n\n    // Beacon genesis date, 2020-09-24, is the first interval start.\n    // https://etherscan.io/tx/0xe2e8ab5631473a3d7d8122ce4853c38f5cc7d3dcbfab3607f6b27a7ef3b86da2\n    uint256 internal constant beaconFirstIntervalStart = 1600905600;\n\n    // Each interval is 30 days long.\n    uint256 internal constant beaconTermLength = 30 days;\n\n    // There has to be at least 2 groups per interval to meet the group quota\n    // and distribute the full reward for the given interval.\n    uint256 internal constant minimumBeaconGroupsPerInterval = 2;\n\n\n    KeepRandomBeaconOperator operatorContract;\n    TokenStaking tokenStaking;\n\n    constructor (\n        address _token,\n        address _operatorContract,\n        address _stakingContract\n    ) public Rewards(\n        _token,\n        beaconFirstIntervalStart,\n        beaconIntervalWeights,\n        beaconTermLength,\n        minimumBeaconGroupsPerInterval\n    ) {\n        operatorContract = KeepRandomBeaconOperator(_operatorContract);\n        tokenStaking = TokenStaking(_stakingContract);\n    }\n\n    /// @notice Sends the reward for a group to the group member beneficiaries.\n    /// @param groupIndex Index of the group to receive a reward.\n    function receiveReward(uint256 groupIndex) public {\n        receiveReward(bytes32(groupIndex));\n    }\n\n    /// @notice Stakers can receive KEEP rewards from multiple groups of their choice\n    /// in one transaction to reduce total cost comparing to single calls for rewards.\n    /// It is a caller responsibility to determine the cost and consumed gas when\n    /// receiving rewards from multiple groups.\n    /// @param groupIndices An array of group indices.\n    function receiveRewards(uint256[] memory groupIndices) public {\n        uint256 len = groupIndices.length;\n        bytes32[] memory bytes32identifiers = new bytes32[](len);\n        for (uint256 i = 0; i < groupIndices.length; i++) {\n            bytes32identifiers[i] = bytes32(groupIndices[i]);\n        }\n        receiveRewards(bytes32identifiers);\n    }\n\n    /// @notice Checks if the group is eligible to receive a reward.\n    /// Group is eligible to receive a reward if it has been marked as stale\n    /// and rewards has not been claimed yet.\n    /// @param groupIndex Index of the group to check.\n    function eligibleForReward(uint256 groupIndex) public view returns (bool) {\n        return eligibleForReward(bytes32(groupIndex));\n    }\n\n    /// @notice Report that the group was terminated, and return its allocated\n    /// rewards to the unallocated pool.\n    /// @param groupIndex Index of the terminated group.\n    function reportTermination(uint256 groupIndex) public {\n        reportTermination(bytes32(groupIndex));\n    }\n\n    /// @notice Report about the terminated groups in batch. All the allocated\n    /// rewards in these groups will be returned to the unallocated pool.\n    /// @param groupIndices An array of group indices.\n    function reportTerminations(uint256[] memory groupIndices) public {\n        uint256 len = groupIndices.length;\n        bytes32[] memory bytes32identifiers = new bytes32[](len);\n        for (uint256 i = 0; i < groupIndices.length; i++) {\n            bytes32identifiers[i] = bytes32(groupIndices[i]);\n        }\n        reportTerminations(bytes32identifiers);\n    }\n\n    /// @notice Checks if the group is terminated and thus its rewards can be\n    /// returned to the unallocated pool by calling `reportTermination`.\n    /// @param groupIndex Index of the potentially terminated group.\n    function isTerminated(uint256 groupIndex) public view returns (bool) {\n        return eligibleButTerminated(bytes32(groupIndex));\n    }\n\n    function _getKeepCount() internal view returns (uint256) {\n        return operatorContract.getNumberOfCreatedGroups();\n    }\n\n    function _getKeepAtIndex(uint256 i) internal view returns (bytes32) {\n        return bytes32(i);\n    }\n\n    function _getCreationTime(bytes32 groupIndexBytes) internal view returns (uint256) {\n        return operatorContract.getGroupRegistrationTime(uint256(groupIndexBytes));\n    }\n\n    function _isClosed(bytes32 groupIndexBytes) internal view returns (bool) {\n        if (_isTerminated(groupIndexBytes)) { return false; }\n        bytes memory groupPubkey = operatorContract.getGroupPublicKey(\n            uint256(groupIndexBytes)\n        );\n        return operatorContract.isStaleGroup(groupPubkey);\n    }\n\n    function _isTerminated(bytes32 groupIndexBytes) internal view returns (bool) {\n        return operatorContract.isGroupTerminated(uint256(groupIndexBytes));\n    }\n\n    function _recognizedByFactory(bytes32 groupIndexBytes) internal view returns (bool) {\n        return _getKeepCount() > uint256(groupIndexBytes);\n    }\n\n    function _distributeReward(bytes32 groupIndexBytes, uint256 _value) internal {\n        bytes memory groupPubkey = operatorContract.getGroupPublicKey(\n            uint256(groupIndexBytes)\n        );\n        address[] memory members = operatorContract.getGroupMembers(groupPubkey);\n\n        uint256 memberCount = members.length;\n        uint256 dividend = _value.div(memberCount);\n\n        // Only pay other members if dividend is nonzero.\n        if(dividend > 0) {\n            for (uint256 i = 0; i < memberCount - 1; i++) {\n                token.safeTransfer(\n                    tokenStaking.beneficiaryOf(members[i]),\n                    dividend\n                );\n            }\n        }\n\n        // Transfer of dividend for the last member. Remainder might be equal to\n        // zero in case of even distribution or some small number.\n        uint256 remainder = _value.mod(memberCount);\n        token.safeTransfer(\n            tokenStaking.beneficiaryOf(members[memberCount - 1]),\n            dividend.add(remainder)\n        );\n    }\n}\n"
    },
    "solidity/contracts/Rewards.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity ^0.5.17;\n\nimport \"openzeppelin-solidity/contracts/ownership/Ownable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/IERC20.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"openzeppelin-solidity/contracts/math/Math.sol\";\n\nimport \"./KeepToken.sol\";\n\n/// @title KEEP Signer Subsidy Rewards\n/// @notice A contract for distributing KEEP token rewards to keeps.\n/// When a reward contract is created, the creator defines a reward schedule\n/// consisting of one or more reward intervals and their interval weights,\n/// the length of reward intervals, and the quota of how many keeps must be\n/// created in an interval for the full reward for that interval to be paid out.\n///\n/// The amount of KEEP to be distributed is determined by funding the contract,\n/// and additional KEEP can be added at any time.\n/// The reward contract is funded with `approveAndCall` with no extra data,\n/// but it also collects any KEEP mistakenly sent to it in any other way.\n///\n/// An interval is defined by the timestamps [startOf, endOf);\n/// a keep created at the time `startOf(i)` belongs to interval `i`\n/// and one created at `endOf(i)` belongs to `i+1`.\n///\n/// When an interval is over, it will be allocated a percentage of the remaining\n/// unallocated rewards based on its weight, and adjusted by the number of keeps\n/// created in the interval if the quota is not met.\n///\n/// The adjustment for not meeting the keep quota is a percentage that equals\n/// the percentage of the quota that was met; if the number of keeps created is\n/// 80% of the quota then 80% of the base reward will be allocated for the\n/// interval.\n///\n/// Any unallocated rewards will stay in the unallocated rewards pool,\n/// to be allocated for future intervals. Intervals past the initially defined\n/// schedule have a weight of 100%, meaning that all remaining unallocated\n/// rewards will be allocated to the interval.\n///\n/// Keeps of the appropriate type can receive rewards once the interval they\n/// were created in is over, and the keep has closed happily.\n/// There is no time limit to receiving rewards, nor is there need to wait for\n/// all keeps from the interval to close.\n/// Calling `receiveReward` automatically allocates the rewards for the interval\n/// the specified keep was created in and all previous intervals.\n///\n/// If a keep is terminated, that fact can be reported to the reward contract.\n/// Reporting a terminated keep returns its allocated reward to the pool of\n/// unallocated rewards.\n///\n/// @dev A concrete implementation of the abstract rewards contract must specify\n/// functions for accessing information about keeps and paying out rewards.\n/// For the purpose of rewards, Random Beacon signing groups count as \"keeps\"\n/// and the beacon operator contract acts as the \"factory\".\ncontract Rewards is Ownable {\n    using SafeMath for uint256;\n    using SafeERC20 for KeepToken;\n\n    KeepToken public token;\n\n    // Array representing the percentage of unallocated rewards\n    // available for each reward interval.\n    uint256[] public intervalWeights; // percent array\n    // Length of one interval in seconds (timestamp diff).\n    uint256 public termLength;\n    // The number of keeps required in an interval\n    // for the full reward to be allocated to the interval.\n    uint256 public minimumKeepsPerInterval;\n\n    // Total number of KEEP tokens to distribute by this contract.\n    // Includes those already dispensed.\n    uint256 public totalRewards;\n    // Rewards that haven't been allocated to finished intervals.\n    uint256 public unallocatedRewards;\n    // Rewards that have been dispensed from this contract as signer rewards.\n    // `token.balanceOf(address(this))` should always equal\n    // `totalRewards.sub(dispensedRewards)`\n    uint256 public dispensedRewards;\n    // The following invariant should always hold:\n    // token.balanceOf(address(this)) >= totalRewards.sub(dispensedRewards)\n\n    // Timestamp of first interval beginning.\n    // Interval 0 covers everything before `firstIntervalStart`\n    // and the first `termLength` after `firstIntervalStart`.\n    uint256 public firstIntervalStart;\n    // Mapping of interval number to tokens allocated for the interval.\n    uint256[] internal intervalAllocations;\n\n    // mapping of keeps to booleans.\n    // True if the keep has been used to claim a reward.\n    mapping(bytes32 => bool) internal claimed;\n    // Mapping of interval to number of keeps created in/before the interval\n    mapping(uint256 => uint256) internal keepsByInterval;\n    // Mapping of interval to number of keeps whose rewards have been paid out,\n    // or reallocated because the keep closed unhappily\n    mapping(uint256 => uint256) public intervalKeepsProcessed;\n\n    // Indicates whether the contract has been properly funded. Rewards can not\n    // be allocated before the first funding and the owner of the\n    // contract is responsible for marking it as already funded. Further funding\n    // of the contract is possible with no owner's intervention.\n    bool public funded = false;\n\n    // Owner of the contract may initiate an upgrade to a new rewards contract\n    // but the pending and past intervals must have their rewards allocated\n    // before any KEEP tokens are transferred out from this contract.\n    uint256 public upgradeInitiatedTimestamp;\n    uint256 public upgradeFinalizedTimestamp;\n    address public newRewardsContract;\n\n    event RewardReceived(bytes32 keep, uint256 amount);\n    event UpgradeInitiated(address newRewardsContract);\n    event UpgradeFinalized(uint256 amountTransferred);\n\n    constructor (\n        address _token,\n        uint256 _firstIntervalStart,\n        uint256[] memory _intervalWeights,\n        uint256 _termLength,\n        uint256 _minimumKeepsPerInterval\n    ) public {\n        token = KeepToken(_token);\n        firstIntervalStart = _firstIntervalStart;\n        intervalWeights = _intervalWeights;\n        termLength = _termLength;\n        minimumKeepsPerInterval = _minimumKeepsPerInterval;\n    }\n\n    /// @notice Funds the rewards contract.\n    /// @dev Adds the received amount of tokens to `totalRewards` and\n    /// `unallocatedRewards`. May be called at any time, even after allocating\n    /// some intervals.\n    /// If the contract has been upgraded,\n    /// the funding will be transferred to the new contract instead.\n    /// Changes to `unallocatedRewards` will take effect on subsequent interval\n    /// allocations. Intended to be used with `approveAndCall`.\n    /// If the reward contract has received tokens outside `approveAndCall`,\n    /// this collects them as well.\n    /// The following invariant should hold right after calling this function:\n    /// token.balanceOf(address(this)) == totalRewards.sub(dispensedRewards).\n    /// @param _from The original sender of the tokens.\n    /// Must have approved at least `_value` tokens for the rewards contract.\n    /// @param _value The amount of tokens to fund.\n    /// @param _token The token to fund the rewards in.\n    /// Must match the one specified in the rewards contract.\n    function receiveApproval(\n        address _from,\n        uint256 _value,\n        address _token,\n        bytes memory\n    ) public {\n        require(IERC20(_token) == token, \"Unsupported token\");\n\n        token.safeTransferFrom(_from, address(this), _value);\n\n        uint256 currentBalance = token.balanceOf(address(this));\n        uint256 beforeBalance = totalRewards.sub(dispensedRewards);\n        require(\n            currentBalance >= beforeBalance,\n            \"Reward contract has lost tokens\"\n        );\n\n        uint256 addedBalance = currentBalance.sub(beforeBalance);\n\n        totalRewards = totalRewards.add(addedBalance);\n        deallocate(addedBalance);\n    }\n\n    function markAsFunded() public onlyOwner {\n        funded = true;\n    }\n\n    /// @notice Stakers can receive KEEP rewards from multiple keeps of their choice\n    /// in one transaction to reduce total cost comparing to single calls for rewards.\n    /// It is a caller responsibility to determine the cost and consumed gas when\n    /// receiving rewards from multiple keeps.\n    /// @param keepIdentifiers An array of keep identifiers.\n    function receiveRewards(bytes32[] memory keepIdentifiers) public {\n        for (uint256 i = 0; i < keepIdentifiers.length; i++) {\n            receiveReward(keepIdentifiers[i]);\n        }\n    }\n\n    /// @notice Sends the reward for a keep to the keep members.\n    /// @param keepIdentifier A unique identifier for the keep,\n    /// e.g. address or number converted to a `bytes32`.\n    function receiveReward(bytes32 keepIdentifier)\n        factoryMustRecognize(keepIdentifier)\n        rewardsNotClaimed(keepIdentifier)\n        mustBeClosed(keepIdentifier)\n        public\n    {\n        _processKeep(true, keepIdentifier);\n    }\n\n    /// @notice Report about the terminated keeps in batch. All the allocated\n    /// rewards in these keeps will be returned to the unallocated pool.\n    /// @param keepIdentifiers An array of keep identifiers.\n    function reportTerminations(bytes32[] memory keepIdentifiers) public {\n        for (uint256 i = 0; i < keepIdentifiers.length; i++) {\n            reportTermination(keepIdentifiers[i]);\n        }\n    }\n\n    /// @notice Report that the keep was terminated, and return its allocated\n    /// rewards to the unallocated pool.\n    /// @param keepIdentifier The terminated keep.\n    function reportTermination(bytes32 keepIdentifier)\n        factoryMustRecognize(keepIdentifier)\n        rewardsNotClaimed(keepIdentifier)\n        mustBeTerminated(keepIdentifier)\n        public\n    {\n        _processKeep(false, keepIdentifier);\n    }\n\n    /// @notice Checks if a keep is eligible to receive rewards.\n    /// @dev Keeps that close dishonorably or early are not eligible for rewards.\n    /// @param _keep The keep to check.\n    /// @return True if the keep is eligible, false otherwise\n    function eligibleForReward(bytes32 _keep) public view returns (bool){\n        return _recognizedByFactory(_keep) && _isClosed(_keep) && !rewardClaimed(_keep);\n    }\n\n    /// @notice Checks if a keep is terminated and thus its rewards can be\n    /// returned to the unallocated pool.\n    /// @param _keep The keep to check.\n    /// @return True if the keep is terminated, false otherwise\n    function eligibleButTerminated(bytes32 _keep) public view returns (bool) {\n        return _recognizedByFactory(_keep) && _isTerminated(_keep);\n    }\n\n    /// @notice Return the interval number the provided timestamp falls within.\n    /// @dev If the timestamp is before `firstIntervalStart`, the interval is 0.\n    /// @param timestamp The timestamp whose interval is queried.\n    /// @return The interval of the timestamp.\n    function intervalOf(uint256 timestamp) public view returns (uint256) {\n        uint256 _firstIntervalStart = firstIntervalStart;\n        uint256 _termLength = termLength;\n\n        if (timestamp < _firstIntervalStart) {\n            return 0;\n        }\n\n        uint256 difference = timestamp.sub(_firstIntervalStart);\n        uint256 interval = difference.div(_termLength);\n\n        return interval;\n    }\n\n    /// @notice Return the timestamp corresponding to the start of the interval.\n    /// @dev The start of an interval is inclusive;\n    /// a keep created at the timestamp `startOf(i)` is in interval `i`.\n    /// @param interval The interval whose start is queried.\n    /// @return The start timestamp of the interval.\n    function startOf(uint256 interval) public view returns (uint256) {\n        return firstIntervalStart.add(interval.mul(termLength));\n    }\n\n    /// @notice Return the timestamp corresponding to the end of the interval.\n    /// @dev The end of an interval is exclusive;\n    /// a keep created at the timestamp `endOf(i)` is in interval `i+1`.\n    /// @param interval The interval whose end is queried.\n    /// @return The end timestamp of the interval.\n    function endOf(uint256 interval) public view returns (uint256) {\n        return startOf(interval.add(1));\n    }\n\n    /// @notice Return whether the given interval is finished.\n    /// @param interval The interval.\n    /// @return Whether the interval is finished.\n    function isFinished(uint256 interval) public view returns (bool) {\n        return block.timestamp >= endOf(interval);\n    }\n\n    /// @notice Return whether the given keep has already claimed rewards\n    /// or had its rewards reallocated due to termination.\n    /// @param _keep The identifier of the keep.\n    /// @return True if rewards have been paid out for the keep,\n    /// or its termination has been reported.\n    /// False otherwise.\n    function rewardClaimed(bytes32 _keep) public view returns (bool) {\n        return claimed[_keep];\n    }\n\n    /// @notice Return the number of keeps created before `intervalEndpoint`\n    /// @dev Wraps the binary search of `_find`\n    /// with a number of checks for edge cases.\n    function _findEndpoint(uint256 intervalEndpoint) internal view returns (uint256) {\n        require(\n            intervalEndpoint <= block.timestamp,\n            \"interval hasn't ended yet\"\n        );\n        uint256 keepCount = _getKeepCount();\n        // no keeps created yet -> return 0\n        if (keepCount == 0) {\n            return 0;\n        }\n\n        uint256 lb = 0; // lower bound, inclusive\n        uint256 timestampLB = _getCreationTime(_getKeepAtIndex(lb));\n        // all keeps created after the interval -> return 0\n        if (timestampLB >= intervalEndpoint) {\n            return 0;\n        }\n\n        uint256 ub = keepCount.sub(1); // upper bound, inclusive\n        uint256 timestampUB = _getCreationTime(_getKeepAtIndex(ub));\n        // all keeps created in or before the interval -> return keep count\n        if (timestampUB < intervalEndpoint) {\n            return keepCount;\n        }\n\n        // The above cases also cover the case\n        // where only 1 keep has been created;\n        // lb == ub\n        // if it was created after the interval, return 0\n        // otherwise, return 1\n\n        return _find(lb, timestampLB, ub, timestampUB, intervalEndpoint);\n    }\n\n    /// @notice Return the number of keeps created before `targetTime`,\n    /// with specified upper and lower bounds.\n    /// @dev Binary search assumes the following invariants:\n    ///   lower bound >= 0, lbTime < targetTime\n    ///   upper bound < keepCount, ubTime >= targetTime\n    /// @param _lb The lower bound of the search (inclusive)\n    /// @param _lbTime The creation time of keep number `lb`\n    /// @param _ub The upper bound of the search (inclusive)\n    /// @param _ubTime The creation time of keep number `ub`\n    /// @param targetTime The target time\n    function _find(\n        uint256 _lb,\n        uint256 _lbTime,\n        uint256 _ub,\n        uint256 _ubTime,\n        uint256 targetTime\n    ) internal view returns (uint256) {\n        uint256 lb = _lb;\n        uint256 lbTime = _lbTime;\n        uint256 ub = _ub;\n        uint256 ubTime = _ubTime;\n        uint256 len = ub.sub(lb);\n        while (len > 1) {\n            // upper bound >= lower bound + 2\n            // mid > lower bound\n            uint256 mid = lb.add(len.div(2));\n            uint256 midTime = _getCreationTime(_getKeepAtIndex(mid));\n\n            if (midTime >= targetTime) {\n                ub = mid;\n                ubTime = midTime;\n            } else {\n                lb = mid;\n                lbTime = midTime;\n            }\n            len = ub.sub(lb);\n        }\n        return ub;\n    }\n\n    /// @notice Return the endpoint index of the interval,\n    /// i.e. the number of keeps created in and before the interval.\n    /// The interval must have ended; otherwise the endpoint might still change.\n    /// @dev Uses a locally cached result, and stores the result if it isn't\n    /// cached yet. All keeps created before the initiation fall in interval 0.\n    /// @param interval The number of the interval.\n    /// @return endpoint The number of keeps the factory had created\n    /// before the end of the interval.\n    function _getEndpoint(uint256 interval)\n        mustBeFinished(interval)\n        internal\n        returns (uint256 endpoint)\n    {\n        // Get the endpoint from local cache;\n        // might not be recorded yet\n        uint256 maybeEndpoint = keepsByInterval[interval];\n\n        // Either the endpoint is zero\n        // (no keeps created by the end of the interval)\n        // or the endpoint isn't cached yet\n        if (maybeEndpoint == 0) {\n            // Check what the real endpoint is\n            // if the actual value is 0, this call short-circuits\n            // so we don't need to special-case the zero\n            uint256 realEndpoint = _findEndpoint(endOf(interval));\n            // We didn't have the correct value cached,\n            // so store it\n            if (realEndpoint != 0) {\n                keepsByInterval[interval] = realEndpoint;\n            }\n            endpoint = realEndpoint;\n        } else {\n            endpoint = maybeEndpoint;\n        }\n        return endpoint;\n    }\n\n    /// @notice Get the endpoint of the previous interval.\n    /// @dev Like _getEndpoint, gracefully handles the beginning of interval 0.\n    /// @param interval The interval.\n    /// @return The number of keeps created by the end of the preceding interval.\n    function _getPreviousEndpoint(uint256 interval) internal returns (uint256) {\n        if (interval == 0) {\n            return 0;\n        } else {\n            return _getEndpoint(interval.sub(1));\n        }\n    }\n\n    /// @notice Return the number of keeps created in the specified interval.\n    /// @param interval The interval.\n    /// @return Number of keeps created in the interval.\n    function keepsInInterval(uint256 interval) public returns (uint256) {\n        return (_getEndpoint(interval).sub(_getPreviousEndpoint(interval)));\n    }\n\n    /// @notice Return the percentage of remaining unallocated rewards\n    /// that is to be allocated to the specified interval.\n    /// @param interval The interval.\n    /// @return The percentage weight of the interval.\n    function getIntervalWeight(uint256 interval) public view returns (uint256) {\n        if (interval < intervalWeights.length) {\n            return intervalWeights[interval];\n        } else {\n            return intervalWeights[intervalWeights.length - 1];\n        }\n    }\n\n    /// @notice Get the number of intervals with explicitly specified weights.\n    /// All subsequent intervals will have an implicit weight of 100.\n    /// @return The number of explicitly specified intervals.\n    function getIntervalCount() public view returns (uint256) {\n        return intervalWeights.length;\n    }\n\n    /// @notice Calculate the reward allocation for an interval\n    /// without adjusting for the number of keeps in the interval.\n    /// @param interval The next interval to be allocated.\n    /// Results for other intervals will not be accurate.\n    /// @return The base reward allocation for the interval.\n    function _baseAllocation(uint256 interval) internal view returns (uint256) {\n        uint256 _unallocatedRewards = unallocatedRewards;\n        uint256 weightPercentage = getIntervalWeight(interval);\n        return _unallocatedRewards.mul(weightPercentage).div(100);\n    }\n\n    /// @notice Calculate the reward allocation for an interval\n    /// after adjusting for the number of keeps in the interval.\n    /// @dev An interval with at least `minimumKeepsPerInterval` keeps\n    /// will have the full reward allocated to it.\n    /// An interval with fewer keeps will only be allocated a fraction of the\n    /// base reward equaling the fraction of the quota that was met.\n    /// The reward allocated for each keep in the interval is constant\n    /// regardless of the number of keeps in the interval until the quota is\n    /// met, and further increases in the number of keeps will lead to the same\n    /// allocation being shared among more of them. Each keep in an interval is\n    /// allocated the same reward. If the number of keeps in an interval meets\n    /// the quota, but the base allocation isn't divisible by the number of\n    /// keeps, the remainder will remain unallocated.\n    /// Allocations for an already allocated interval, or when all prior\n    /// intervals haven't been allocated yet, will produce incorrect results.\n    /// @param interval The next interval to be allocated.\n    /// @return The amount of tokens to allocate as rewards for the interval.\n    function _adjustedAllocation(uint256 interval) internal returns (uint256) {\n        uint256 __baseAllocation = _baseAllocation(interval);\n        if (__baseAllocation == 0) {\n            return 0;\n        }\n        uint256 keepCount = keepsInInterval(interval);\n        uint256 adjustmentCount = Math.max(keepCount, minimumKeepsPerInterval);\n        if (adjustmentCount == 0) {\n            return 0;\n        }\n        // Rewards divide equally among keeps\n        return __baseAllocation.mul(keepCount).div(adjustmentCount);\n    }\n\n    /// @notice Allocate rewards for unallocated intervals up to and including\n    /// the given interval.\n    /// @dev The given interval must be finished and unallocated.\n    /// To allocate rewards correctly, any earlier intervals that are still\n    /// unallocated will be allocated before the given interval.\n    /// With reasonable interval lengths this should not pose a problem,\n    /// and if allocating a later interval results in an out-of-gas issue,\n    /// forcing the allocation of an earlier interval should fix it.\n    /// @param interval The interval to allocate.\n    function allocateRewards(uint256 interval)\n        mustBeFinished(interval)\n        mustBeFunded\n        public\n    {\n        uint256 allocatedIntervals = intervalAllocations.length;\n        require(\n            !(interval < allocatedIntervals),\n            \"Interval already allocated\"\n        );\n        // Allocate previous intervals first\n        if (interval > allocatedIntervals) {\n            allocateRewards(interval.sub(1));\n        }\n        uint256 totalAllocation = _adjustedAllocation(interval);\n        unallocatedRewards = unallocatedRewards.sub(totalAllocation);\n        intervalAllocations.push(totalAllocation);\n    }\n\n    /// @notice Get the total amount of tokens\n    /// allocated for all keeps in the specified interval.\n    /// @dev This function returns correct results for any allocated interval.\n    /// Dividing the allocated rewards by the number of keeps in the interval\n    /// will give the correct reward for a keep in the interval.\n    /// However, if a keep in the interval is terminated\n    /// its reward will be returned to the pool of unallocated tokens.\n    /// This will not be reflected in the return value of this function.\n    /// @param interval A previously allocated interval.\n    /// @return The total number of tokens allocated for keeps in the interval.\n    function getAllocatedRewards(uint256 interval) public view returns (uint256) {\n        require(\n            interval < intervalAllocations.length,\n            \"Interval not allocated yet\"\n        );\n        return intervalAllocations[interval];\n    }\n\n    /// @notice Return whether the specified interval has been allocated.\n    /// @param interval The interval.\n    /// @return Whether the interval has been allocated yet.\n    function isAllocated(uint256 interval) public view returns (bool) {\n        uint256 allocatedIntervals = intervalAllocations.length;\n        return (interval < allocatedIntervals);\n    }\n\n    /// @notice Process the rewards for the given keep, allocating finished\n    /// intervals as necessary, and then either paying out the rewards to the\n    /// keep's members or returning them to the unallocated pool, depending on\n    /// the keep's eligibility.\n    /// @param eligible Whether the keep is eligible for rewards or not.\n    /// @param keepIdentifier The specified keep.\n    function _processKeep(\n        bool eligible,\n        bytes32 keepIdentifier\n    ) internal {\n        uint256 creationTime = _getCreationTime(keepIdentifier);\n        uint256 interval = intervalOf(creationTime);\n        if (!isAllocated(interval)) {\n            allocateRewards(interval);\n        }\n        uint256 allocation = intervalAllocations[interval];\n        uint256 _keepsInInterval = keepsInInterval(interval);\n        uint256 perKeepReward = allocation.div(_keepsInInterval);\n        claimed[keepIdentifier] = true;\n        intervalKeepsProcessed[interval] = intervalKeepsProcessed[interval].add(1);\n\n        if (eligible) {\n            dispensedRewards = dispensedRewards.add(perKeepReward);\n            _distributeReward(keepIdentifier, perKeepReward);\n            emit RewardReceived(keepIdentifier, perKeepReward);\n        } else {\n            // Return the reward to the unallocated pool\n            deallocate(perKeepReward);\n        }\n    }\n\n    /// @notice Initiates the process of upgrading to another rewards contract.\n    /// @param _newRewardsContract The address of a new rewards contract.\n    function initiateRewardsUpgrade(address _newRewardsContract) public onlyOwner {\n        upgradeInitiatedTimestamp = block.timestamp;\n        newRewardsContract = _newRewardsContract;\n        emit UpgradeInitiated(newRewardsContract);\n    }\n\n    /// @notice Finalizes the process of upgrading to another rewards contract\n    /// by allocating all past intervals and then, transferring the\n    /// not-yet-allocated tokens to a new rewards contract.\n    /// Can be called only when the interval during which the upgrade was\n    /// initiated ended.\n    /// Before finalizing the upgrade, make sure all terminated groups are\n    /// reported.\n    function finalizeRewardsUpgrade() public onlyOwner {\n        require(upgradeInitiatedTimestamp != 0, \"Upgrade not initiated\");\n        \n        uint256 currentInterval = intervalOf(block.timestamp);\n        uint256 upgradeInitiatedInterval = intervalOf(upgradeInitiatedTimestamp);\n\n        require(\n            currentInterval > upgradeInitiatedInterval,\n            \"Interval at which the upgrade was initiated hasn't ended yet\"\n        );\n\n        // ensure all past intervals are allocated\n        if (!isAllocated(currentInterval.sub(1))) {\n            allocateRewards(currentInterval.sub(1));\n        }\n\n        // transfer the unallocated KEEP to the new rewards contract and update\n        // this contract's balances\n        uint256 amountToTransfer = unallocatedRewards;\n\n        totalRewards = totalRewards.sub(amountToTransfer);\n        unallocatedRewards = 0;\n\n        emit UpgradeFinalized(amountToTransfer);\n\n        bool success = token.approveAndCall(\n            newRewardsContract,\n            amountToTransfer,\n            bytes(\"\")\n        );\n        require(success, \"Upgrade finalization failed\");\n        \n\n        upgradeInitiatedTimestamp = 0;\n        upgradeFinalizedTimestamp = block.timestamp;\n    }\n\n    /// @notice Return the given amount to the unallocated pool.\n    /// If the contract has been upgraded,\n    /// the deallocated amount will be sent to the new contract.\n    /// @param amount The amount to deallocate\n    function deallocate(uint256 amount) internal {\n        if (upgradeFinalizedTimestamp != 0) {\n            bool success = token.approveAndCall(\n                newRewardsContract,\n                amount,\n                bytes(\"\")\n            );\n            if (!success) {\n                unallocatedRewards = unallocatedRewards.add(amount);\n            }\n        } else {\n            unallocatedRewards = unallocatedRewards.add(amount);\n        }\n    }\n\n    /// @notice Get the total number of keeps ever created by the factory,\n    /// including closed and terminated keeps.\n    /// @return The number of keeps.\n    function _getKeepCount() internal view returns (uint256);\n\n    /// @notice Get the identifier of the keep at the given index,\n    /// when all keeps created by the factory are ordered by creation time.\n    /// @param index The index of the queried keep.\n    /// @return The `bytes32` identifier of the keep at the given index.\n    /// @dev Implementation is not required to check if a keep with the given\n    /// index exists.\n    function _getKeepAtIndex(uint256 index) internal view returns (bytes32);\n\n    /// @notice Get the creation time of the given keep.\n    /// @param _keep The identifier of the keep.\n    /// @return The creation timestamp of the keep.\n    /// @dev If the idenfifier is invalid or not recognized by factory, function\n    /// may revert or return 0.\n    function _getCreationTime(bytes32 _keep) internal view returns (uint256);\n\n    /// @notice Is the given keep closed.\n    /// @param _keep The identifier of the keep.\n    /// @return True if the keep is closed, false otherwise.\n    /// If the identifier is invalid, may return false or an error.\n    function _isClosed(bytes32 _keep) internal view returns (bool);\n\n    /// @notice Is the given keep terminated.\n    /// @param _keep The identifier of the keep.\n    /// @return True if the keep is terminated, false otherwise.\n    /// If the identifier is invalid, may return false or an error.\n    function _isTerminated(bytes32 _keep) internal view returns (bool);\n\n    /// @notice Does the given `bytes32` identifier match a valid keep.\n    /// @param _keep A possible keep identifier.\n    /// @return True if the identifier matches a keep created by the factory.\n    /// For any other identifier, must return false and not an error.\n    function _recognizedByFactory(bytes32 _keep) internal view returns (bool);\n\n    /// @notice Pay the given amount of tokens to members of the keep.\n    /// @param _keep The keep whose members to reward.\n    /// @param amount The total amount of tokens to distribute to the members.\n    function _distributeReward(bytes32 _keep, uint256 amount) internal;\n\n    modifier rewardsNotClaimed(bytes32 _keep) {\n        require(\n            !rewardClaimed(_keep),\n            \"Rewards already claimed\");\n        _;\n    }\n\n    modifier mustBeFinished(uint256 interval) {\n        require(\n            isFinished(interval),\n            \"Interval hasn't ended yet\");\n        _;\n    }\n\n    modifier mustBeClosed(bytes32 _keep) {\n        require(\n            _isClosed(_keep),\n            \"Keep is not closed\");\n        _;\n    }\n\n    modifier mustBeTerminated(bytes32 _keep) {\n        require(\n            _isTerminated(_keep),\n            \"Keep is not terminated\");\n        _;\n    }\n\n    modifier factoryMustRecognize(bytes32 _keep) {\n        require(\n            _recognizedByFactory(_keep),\n            \"Keep not recognized by factory\");\n        _;\n    }\n\n    modifier mustBeFunded() {\n        require(funded, \"Contract has not been funded yet\");\n        _;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/utils/ReentrancyGuard.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Contract module that helps prevent reentrant calls to a function.\n *\n * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier\n * available, which can be applied to functions to make sure there are no nested\n * (reentrant) calls to them.\n *\n * Note that because there is a single `nonReentrant` guard, functions marked as\n * `nonReentrant` may not call one another. This can be worked around by making\n * those functions `private`, and then adding `external` `nonReentrant` entry\n * points to them.\n */\ncontract ReentrancyGuard {\n    // counter to allow mutex lock with only one SSTORE operation\n    uint256 private _guardCounter;\n\n    constructor () internal {\n        // The counter starts at one to prevent changing it from zero to a non-zero\n        // value, which is a more expensive operation.\n        _guardCounter = 1;\n    }\n\n    /**\n     * @dev Prevents a contract from calling itself, directly or indirectly.\n     * Calling a `nonReentrant` function from another `nonReentrant`\n     * function is not supported. It is possible to prevent this from happening\n     * by making the `nonReentrant` function external, and make it call a\n     * `private` function that does the actual work.\n     */\n    modifier nonReentrant() {\n        _guardCounter += 1;\n        uint256 localCounter = _guardCounter;\n        _;\n        require(localCounter == _guardCounter, \"ReentrancyGuard: reentrant call\");\n    }\n}\n"
    },
    "solidity/contracts/GasPriceOracle.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\nimport \"openzeppelin-solidity/contracts/ownership/Ownable.sol\";\n\n/// @dev Interface expected to be implemented by contracts added as a gas price\n/// oracle consumers. Consumers are notified by GasPriceOracle every time gas\n/// price change is finalized by calling their refreshGasPrice function. Each\n/// consumer may decide to pull the new value from the oracle immediatelly or\n/// at the moment right for the consumer.\n/// Consumers must be trusted contracts whose refreshGasPrice must always\n/// succeed and never consume excessive gas.\ninterface GasPriceOracleConsumer {\n    function refreshGasPrice() external;\n}\n\n/// @notice Oracle presenting the current gas price. The oracle is manually\n/// updated by its owner.\ncontract GasPriceOracle is Ownable {\n    using SafeMath for uint256;\n\n    event GasPriceUpdated(uint256 newValue);\n\n    uint256 public constant governanceDelay = 1 hours; \n\n    uint256 public gasPrice;\n\n    uint256 public newGasPrice;\n    uint256 public gasPriceChangeInitiated;\n\n    address[] public consumerContracts;\n\n    modifier onlyAfterGovernanceDelay {\n        require(gasPriceChangeInitiated > 0, \"Change not initiated\");\n        require(\n            block.timestamp.sub(gasPriceChangeInitiated) >= governanceDelay,\n            \"Governance delay has not elapsed\"\n        );\n        _;\n    }\n\n    /// @notice Initialize the gas price update. Change is finalized after\n    /// the governance delay elapses.\n    /// @param _newGasPrice New gas price in wei.\n    function beginGasPriceUpdate(uint256 _newGasPrice) public onlyOwner {\n        newGasPrice = _newGasPrice;\n        gasPriceChangeInitiated = block.timestamp;\n    }\n\n    /// @notice Finalizes the gas price update. Finalization may happen only\n    /// after the governance delay elapses.\n    function finalizeGasPriceUpdate() public onlyAfterGovernanceDelay {\n        gasPrice = newGasPrice;\n\n        newGasPrice = 0;\n        gasPriceChangeInitiated = 0;\n\n        emit GasPriceUpdated(gasPrice);\n        \n        for (uint256 i = 0; i < consumerContracts.length; i++) {\n            GasPriceOracleConsumer(consumerContracts[i]).refreshGasPrice();\n        }\n    }\n\n    /// @notice Adds a new consumer contract to the oracle. Consumer contract is\n    /// expected to implement GasPriceOracleConsumer interface and receives\n    /// a notifcation every time gas price update is finalized.\n    /// @param consumerContract The new consumer contract to add to the oracle.\n    function addConsumerContract(address consumerContract) public onlyOwner {\n        consumerContracts.push(consumerContract);\n    }\n\n    /// @notice Removes consumer contract from the oracle by its index.\n    /// @param index Index of the consumer contract to be removed.\n    function removeConsumerContract(uint256 index) public onlyOwner {\n        require(index < consumerContracts.length, \"Invalid index\");\n        consumerContracts[index] = consumerContracts[consumerContracts.length - 1];\n        consumerContracts.length--;\n    }\n\n    /// @notice Returns all consumer contracts currently registered in the\n    /// oracle.\n    function getConsumerContracts() public view returns (address[] memory) {\n        return consumerContracts;\n    }\n}"
    },
    "solidity/contracts/cryptography/BLS.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"./AltBn128.sol\";\n\n\n/**\n * @title BLS signatures verification\n * @dev Library for verification of 2-pairing-check BLS signatures, including\n * basic, aggregated, or reconstructed threshold BLS signatures, generated\n * using the AltBn128 curve.\n */\nlibrary BLS {\n\n    /**\n     * @dev Creates a signature over message using the provided secret key.\n     */\n    function sign(bytes memory message, uint256 secretKey) public view returns(bytes memory) {\n        AltBn128.G1Point memory p_1 = AltBn128.g1HashToPoint(message);\n        AltBn128.G1Point memory p_2 = AltBn128.scalarMultiply(p_1, secretKey);\n\n        return AltBn128.g1Marshal(p_2);\n    }\n\n    /**\n     * @dev Verify performs the pairing operation to check if the signature\n     * is correct for the provided message and the corresponding public key.\n     * Public key must be a valid point on G2 curve in an uncompressed format.\n     * Message must be a valid point on G1 curve in an uncompressed format.\n     * Signature must be a valid point on G1 curve in an uncompressed format.\n     */\n    function verify(\n        bytes memory publicKey,\n        bytes memory message,\n        bytes memory signature\n    ) public view returns (bool) {\n\n        AltBn128.G1Point memory _signature = AltBn128.g1Unmarshal(signature);\n\n        return AltBn128.pairing(\n            AltBn128.G1Point(_signature.x, AltBn128.getP() - _signature.y),\n            AltBn128.g2(),\n            AltBn128.g1Unmarshal(message),\n            AltBn128.g2Unmarshal(publicKey)\n        );\n    }\n\n    /**\n     * @dev VerifyBytes wraps the functionality of BLS.verify, but hashes a message\n     * to a point on G1 and marshal to bytes first to allow raw bytes verificaion.\n     */\n    function verifyBytes(\n        bytes memory publicKey,\n        bytes memory message,\n        bytes memory signature\n    ) public view returns (bool) {\n        AltBn128.G1Point memory point = AltBn128.g1HashToPoint(message);\n        bytes memory messageAsPoint = AltBn128.g1Marshal(point);\n\n        return verify(publicKey, messageAsPoint, signature);\n    }\n}\n"
    },
    "solidity/contracts/libraries/operator/GroupSelection.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"../../utils/BytesLib.sol\";\n\n/// @title Group Selection\n/// @notice The group selection protocol is an interactive method of selecting\n/// candidate group from the set of all stakers given a pseudorandom seed value.\n///\n/// The protocol produces a representative result, where each staker's profit is\n/// proportional to the number of tokens they have staked. Produced candidate\n/// groups are of constant size.\n///\n/// Group selection protocol accepts seed as an input - a pseudorandom value\n/// used to construct candidate tickets. Each candidate group member can\n/// submit their tickets. The maximum number of tickets one can submit depends\n/// on their staking weight - relation of the minimum stake to the candidate's\n/// stake.\n///\n/// There is a certain timeout, expressed in blocks, when tickets can be\n/// submitted. Each ticket is a mix of staker's address, virtual staker index\n/// and group selection seed. Candidate group members are selected based on\n/// the best tickets submitted. There has to be a minimum number of tickets\n/// submitted, equal to the candidate group size so that the protocol can\n/// complete successfully.\nlibrary GroupSelection {\n\n    using SafeMath for uint256;\n    using BytesLib for bytes;\n\n    struct Storage {\n        // Tickets submitted by member candidates during the current group\n        // selection execution and accepted by the protocol for the\n        // consideration.\n        uint64[] tickets;\n\n        // Information about ticket submitters (group member candidates).\n        mapping(uint256 => address) candidate;\n\n        // Pseudorandom seed value used as an input for the group selection.\n        uint256 seed;\n\n        // Timeout in blocks after which the ticket submission is finished.\n        uint256 ticketSubmissionTimeout;\n\n        // Number of block at which the group selection started and from which\n        // ticket submissions are accepted.\n        uint256 ticketSubmissionStartBlock;\n\n        // Indicates whether a group selection is currently in progress.\n        // Concurrent group selections are not allowed.\n        bool inProgress;\n\n        // Captures the minimum stake when group selection starts. This is to ensure the\n        // same staking weight divisor is applied for all member candidates participating.\n        uint256 minimumStake;\n\n        // Map simulates a sorted linked list of ticket values by their indexes.\n        // key -> value represent indices from the tickets[] array.\n        // 'key' index holds an index of a ticket and 'value' holds an index\n        // of the next ticket. Tickets are sorted by their value in\n        // descending order starting from the tail.\n        // Ex. tickets = [151, 42, 175, 7]\n        // tail: 2 because tickets[2] = 175\n        // previousTicketIndex[0] -> 1\n        // previousTicketIndex[1] -> 3\n        // previousTicketIndex[2] -> 0\n        // previousTicketIndex[3] -> 3 note: index that holds a lowest\n        // value points to itself because there is no `nil` in Solidity.\n        // Traversing from tail: [2]->[0]->[1]->[3] result in 175->151->42->7\n        bytes previousTicketIndices;\n\n        // Tail represents an index of a ticket in a tickets[] array which holds\n        // the highest ticket value. It is a tail of the linked list defined by\n        // `previousTicketIndex`.\n        uint256 tail;\n\n        // Size of a group in the threshold relay.\n        uint256 groupSize;\n    }\n\n    /// @notice Starts group selection protocol.\n    /// @param _seed pseudorandom seed value used as an input for the group\n    /// selection. All submitted tickets needs to have the seed mixed-in into the\n    /// value.\n    function start(Storage storage self, uint256 _seed) public {\n        // We execute the minimum required cleanup here needed in case the\n        // previous group selection failed and did not clean up properly in\n        // finish function.\n        cleanupTickets(self);\n        self.inProgress = true;\n        self.seed = _seed;\n        self.ticketSubmissionStartBlock = block.number;\n    }\n\n    /// @notice Finishes group selection protocol clearing up all the submitted\n    /// tickets. This function may be expensive if not executed as a part of\n    /// another transaction consuming a lot of gas and as a result, getting\n    /// gas refund for clearing up the storage.\n    function finish(Storage storage self) public {\n        cleanupCandidates(self);\n        cleanupTickets(self);\n        self.inProgress = false;\n    }\n\n    /// @notice Submits ticket to request to participate in a new candidate group.\n    /// @param ticket Bytes representation of a ticket that holds the following:\n    /// - ticketValue: first 8 bytes of a result of keccak256 cryptography hash\n    ///   function on the combination of the group selection seed (previous\n    ///   beacon output), staker-specific value (address) and virtual staker index.\n    /// - stakerValue: a staker-specific value which is the address of the staker.\n    /// - virtualStakerIndex: 4-bytes number within a range of 1 to staker's weight;\n    ///   has to be unique for all tickets submitted by the given staker for the\n    ///   current candidate group selection.\n    /// @param stakingWeight Ratio of the minimum stake to the candidate's\n    /// stake.\n    function submitTicket(\n        Storage storage self,\n        bytes32 ticket,\n        uint256 stakingWeight\n    ) public {\n        uint64 ticketValue;\n        uint160 stakerValue;\n        uint32 virtualStakerIndex;\n\n        bytes memory ticketBytes = abi.encodePacked(ticket);\n        /* solium-disable-next-line */\n        assembly {\n            // ticket value is 8 bytes long\n            ticketValue := mload(add(ticketBytes, 8))\n            // staker value is 20 bytes long\n            stakerValue := mload(add(ticketBytes, 28))\n            // virtual staker index is 4 bytes long\n            virtualStakerIndex := mload(add(ticketBytes, 32))\n        }\n\n        submitTicket(\n            self,\n            ticketValue,\n            uint256(stakerValue),\n            uint256(virtualStakerIndex),\n            stakingWeight\n        );\n    }\n\n    /// @notice Submits ticket to request to participate in a new candidate group.\n    /// @param ticketValue First 8 bytes of a result of keccak256 cryptography hash\n    /// function on the combination of the group selection seed (previous\n    /// beacon output), staker-specific value (address) and virtual staker index.\n    /// @param stakerValue Staker-specific value which is the address of the staker.\n    /// @param virtualStakerIndex 4-bytes number within a range of 1 to staker's weight;\n    /// has to be unique for all tickets submitted by the given staker for the\n    /// current candidate group selection.\n    /// @param stakingWeight Ratio of the minimum stake to the candidate's\n    /// stake.\n    function submitTicket(\n        Storage storage self,\n        uint64 ticketValue,\n        uint256 stakerValue,\n        uint256 virtualStakerIndex,\n        uint256 stakingWeight\n    ) public {\n        if (block.number > self.ticketSubmissionStartBlock.add(self.ticketSubmissionTimeout)) {\n            revert(\"Ticket submission is over\");\n        }\n\n        if (self.candidate[ticketValue] != address(0)) {\n            revert(\"Duplicate ticket\");\n        }\n\n        if (isTicketValid(\n            ticketValue,\n            stakerValue,\n            virtualStakerIndex,\n            stakingWeight,\n            self.seed\n        )) {\n            addTicket(self, ticketValue);\n        } else {\n            revert(\"Invalid ticket\");\n        }\n    }\n\n    /// @notice Performs full verification of the ticket.\n    function isTicketValid(\n        uint64 ticketValue,\n        uint256 stakerValue,\n        uint256 virtualStakerIndex,\n        uint256 stakingWeight,\n        uint256 groupSelectionSeed\n    ) internal view returns(bool) {\n        uint64 ticketValueExpected;\n        bytes memory ticketBytes = abi.encodePacked(\n            keccak256(\n                abi.encodePacked(\n                    groupSelectionSeed,\n                    stakerValue,\n                    virtualStakerIndex\n                )\n            )\n        );\n        // use first 8 bytes to compare ticket values\n        /* solium-disable-next-line */\n        assembly {\n            ticketValueExpected := mload(add(ticketBytes, 8))\n        }\n\n        bool isVirtualStakerIndexValid = virtualStakerIndex > 0 && virtualStakerIndex <= stakingWeight;\n        bool isStakerValueValid = stakerValue == uint256(msg.sender);\n        bool isTicketValueValid = ticketValue == ticketValueExpected;\n\n        return isVirtualStakerIndexValid && isStakerValueValid && isTicketValueValid;\n    }\n\n    /// @notice Adds a new, verified ticket. Ticket is accepted when it is lower\n    /// than the currently highest ticket or when the number of tickets is still\n    /// below the group size.\n    function addTicket(Storage storage self, uint64 newTicketValue) internal {\n        uint256[] memory previousTicketIndex = readPreviousTicketIndices(self);\n        uint256[] memory ordered = getTicketValueOrderedIndices(\n            self,\n            previousTicketIndex\n        );\n\n        // any ticket goes when the tickets array size is lower than the group size\n        if (self.tickets.length < self.groupSize) {\n            // no tickets\n            if (self.tickets.length == 0) {\n                self.tickets.push(newTicketValue);\n            // higher than the current highest\n            } else if (newTicketValue > self.tickets[self.tail]) {\n                self.tickets.push(newTicketValue);\n                uint256 oldTail = self.tail;\n                self.tail = self.tickets.length-1;\n                previousTicketIndex[self.tail] = oldTail;\n            // lower than the current lowest\n            } else if (newTicketValue < self.tickets[ordered[0]]) {\n                self.tickets.push(newTicketValue);\n                // last element points to itself\n                previousTicketIndex[self.tickets.length - 1] = self.tickets.length - 1;\n                // previous lowest ticket points to the new lowest\n                previousTicketIndex[ordered[0]] = self.tickets.length - 1;\n            // higher than the lowest ticket value and lower than the highest ticket value\n            } else {\n                self.tickets.push(newTicketValue);\n                uint256 j = findReplacementIndex(self, newTicketValue, ordered);\n                previousTicketIndex[self.tickets.length - 1] = previousTicketIndex[j];\n                previousTicketIndex[j] = self.tickets.length - 1;\n            }\n            self.candidate[newTicketValue] = msg.sender;\n        } else if (newTicketValue < self.tickets[self.tail]) {\n            uint256 ticketToRemove = self.tickets[self.tail];\n            // new ticket is lower than currently lowest\n            if (newTicketValue < self.tickets[ordered[0]]) {\n                // replacing highest ticket with the new lowest\n                self.tickets[self.tail] = newTicketValue;\n                uint256 newTail = previousTicketIndex[self.tail];\n                previousTicketIndex[ordered[0]] = self.tail;\n                previousTicketIndex[self.tail] = self.tail;\n                self.tail = newTail;\n            } else { // new ticket is between lowest and highest\n                uint256 j = findReplacementIndex(self, newTicketValue, ordered);\n                self.tickets[self.tail] = newTicketValue;\n                // do not change the order if a new ticket is still highest\n                if (j != self.tail) {\n                    uint newTail = previousTicketIndex[self.tail];\n                    previousTicketIndex[self.tail] = previousTicketIndex[j];\n                    previousTicketIndex[j] = self.tail;\n                    self.tail = newTail;\n                }\n            }\n            // we are replacing tickets so we also need to replace information\n            // about the submitter\n            delete self.candidate[ticketToRemove];\n            self.candidate[newTicketValue] = msg.sender;\n        }\n        storePreviousTicketIndices(self, previousTicketIndex);\n    }\n\n    /// @notice Use binary search to find an index for a new ticket in the tickets[] array\n    function findReplacementIndex(\n        Storage storage self,\n        uint64 newTicketValue,\n        uint256[] memory ordered\n    ) internal view returns (uint256) {\n        uint256 lo = 0;\n        uint256 hi = ordered.length - 1;\n        uint256 mid = 0;\n        while (lo <= hi) {\n            mid = (lo + hi) >> 1;\n            if (newTicketValue < self.tickets[ordered[mid]]) {\n                hi = mid - 1;\n            } else if (newTicketValue > self.tickets[ordered[mid]]) {\n                lo = mid + 1;\n            } else {\n                return ordered[mid];\n            }\n        }\n\n        return ordered[lo];\n    }\n\n    function readPreviousTicketIndices(Storage storage self)\n        internal view returns (uint256[] memory uncompressed)\n    {\n        bytes memory compressed = self.previousTicketIndices;\n        uncompressed = new uint256[](self.groupSize);\n        for (uint256 i = 0; i < compressed.length; i++) {\n            uncompressed[i] = uint256(uint8(compressed[i]));\n        }\n    }\n\n    function storePreviousTicketIndices(\n        Storage storage self,\n        uint256[] memory uncompressed\n    ) internal {\n        bytes memory compressed = new bytes(uncompressed.length);\n        for (uint256 i = 0; i < compressed.length; i++) {\n            compressed[i] = bytes1(uint8(uncompressed[i]));\n        }\n        self.previousTicketIndices = compressed;\n    }\n\n    /// @notice Creates an array of ticket indexes based on their values in the\n    ///  ascending order:\n    ///\n    /// ordered[n-1] = tail\n    /// ordered[n-2] = previousTicketIndex[tail]\n    /// ordered[n-3] = previousTicketIndex[ordered[n-2]]\n    function getTicketValueOrderedIndices(\n        Storage storage self,\n        uint256[] memory previousIndices\n    ) internal view returns (uint256[] memory) {\n        uint256[] memory ordered = new uint256[](self.tickets.length);\n        if (ordered.length > 0) {\n            ordered[self.tickets.length-1] = self.tail;\n            if (ordered.length > 1) {\n                for (uint256 i = self.tickets.length - 1; i > 0; i--) {\n                    ordered[i-1] = previousIndices[ordered[i]];\n                }\n            }\n        }\n\n        return ordered;\n    }\n\n    /// @notice Gets selected participants in ascending order of their tickets.\n    function selectedParticipants(Storage storage self) public view returns (address[] memory) {\n        require(\n            block.number >= self.ticketSubmissionStartBlock.add(self.ticketSubmissionTimeout),\n            \"Ticket submission in progress\"\n        );\n\n        require(self.tickets.length >= self.groupSize, \"Not enough tickets submitted\");\n\n        uint256[] memory previousTicketIndex = readPreviousTicketIndices(self);\n        address[] memory selected = new address[](self.groupSize);\n        uint256 ticketIndex = self.tail;\n        selected[self.tickets.length - 1] = self.candidate[self.tickets[ticketIndex]];\n        for (uint256 i = self.tickets.length - 1; i > 0; i--) {\n            ticketIndex = previousTicketIndex[ticketIndex];\n            selected[i-1] = self.candidate[self.tickets[ticketIndex]];\n        }\n\n        return selected;\n    }\n\n    /// @notice Clears up data of the group selection tickets.\n    function cleanupTickets(Storage storage self) internal {\n        delete self.tickets;\n        self.tail = 0;\n    }\n\n    /// @notice Clears up data of the group selection candidates.\n    /// This operation may have a significant cost if not executed as a part of\n    /// another transaction consuming a lot of gas and as a result, getting\n    /// gas refund for clearing up the storage.\n    function cleanupCandidates(Storage storage self) internal {\n        for (uint i = 0; i < self.tickets.length; i++) {\n            delete self.candidate[self.tickets[i]];\n        }\n    }\n}\n"
    },
    "solidity/contracts/libraries/operator/Groups.sol": {
      "content": "pragma solidity 0.5.17;\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"../../utils/BytesLib.sol\";\nimport \"../../utils/PercentUtils.sol\";\nimport \"../../cryptography/AltBn128.sol\";\nimport \"../../cryptography/BLS.sol\";\nimport \"../../TokenStaking.sol\";\n\n\nlibrary Groups {\n    using SafeMath for uint256;\n    using PercentUtils for uint256;\n    using BytesLib for bytes;\n\n    // The index of a group is flagged with the most significant bit set,\n    // to distinguish the group `0` from null.\n    // The flag is toggled with bitwise XOR (`^`)\n    // which keeps all other bits intact but flips the flag bit.\n    // The flag should be set before writing to `groupIndices`,\n    // and unset after reading from `groupIndices`\n    // before using the value.\n    uint256 constant GROUP_INDEX_FLAG = 1 << 255;\n\n    uint256 constant ONE_MONTH = 86400 * 30;\n    uint256 constant THREE_MONTHS = 3 * ONE_MONTH;\n    uint256 constant SIX_MONTHS = 6 * ONE_MONTH;\n\n    struct Group {\n        bytes groupPubKey;\n        uint256 registrationBlockHeight;\n        bool terminated;\n        uint248 registrationTime;\n    }\n\n    struct Storage {\n        // Time in blocks after which a group expires.\n        uint256 groupActiveTime;\n\n        // Duplicated constant from operator contract to avoid extra call.\n        // The value is set when the operator contract is added.\n        uint256 relayEntryTimeout;\n\n        // Mapping of `groupPubKey` to flagged `groupIndex`\n        mapping (bytes => uint256) groupIndices;\n        Group[] groups;\n        uint256[] activeTerminatedGroups;\n        mapping (bytes => address[]) groupMembers;\n\n        // Sum of all group member rewards earned so far. The value is the same for\n        // all group members. Submitter reward and reimbursement is paid immediately\n        // and is not included here. Each group member can withdraw no more than\n        // this value.\n        mapping (bytes => uint256) groupMemberRewards;\n\n        // Mapping of `groupPubKey, operator`\n        // to whether the operator has withdrawn rewards from that group.\n        mapping(bytes => mapping(address => bool)) withdrawn;\n\n        // expiredGroupOffset is pointing to the first active group, it is also the\n        // expired groups counter\n        uint256 expiredGroupOffset;\n\n        TokenStaking stakingContract;\n    }\n\n    /// @notice Adds a new group.\n    function addGroup(\n        Storage storage self,\n        bytes memory groupPubKey\n    ) public {\n        self.groupIndices[groupPubKey] = (self.groups.length ^ GROUP_INDEX_FLAG);\n        self.groups.push(Group(groupPubKey, block.number, false, uint248(block.timestamp)));\n    }\n\n    /// @notice Sets addresses of members for the group with the given public key\n    /// eliminating members at positions pointed by the misbehaved array.\n    /// @param groupPubKey Group public key.\n    /// @param members Group member addresses as outputted by the group selection\n    /// protocol.\n    /// @param misbehaved Bytes array of misbehaved (disqualified or inactive)\n    /// group members indexes in ascending order; Indexes reflect positions of\n    /// members in the group as outputted by the group selection protocol -\n    /// member indexes start from 1.\n    function setGroupMembers(\n        Storage storage self,\n        bytes memory groupPubKey,\n        address[] memory members,\n        bytes memory misbehaved\n    ) public {\n        self.groupMembers[groupPubKey] = members;\n\n        // Iterate misbehaved array backwards, replace misbehaved\n        // member with the last element and reduce array length\n        uint256 i = misbehaved.length;\n        while (i > 0) {\n             // group member indexes start from 1, so we need to -1 on misbehaved\n            uint256 memberArrayPosition = misbehaved.toUint8(i - 1) - 1;\n            self.groupMembers[groupPubKey][memberArrayPosition] = self.groupMembers[groupPubKey][self.groupMembers[groupPubKey].length - 1];\n            self.groupMembers[groupPubKey].length--;\n            i--;\n        }\n    }\n\n    /// @notice Adds group member reward per group so the accumulated amount can\n    /// be withdrawn later.\n    function addGroupMemberReward(\n        Storage storage self,\n        bytes memory groupPubKey,\n        uint256 amount\n    ) internal {\n        self.groupMemberRewards[groupPubKey] = self.groupMemberRewards[groupPubKey].add(amount);\n    }\n\n    /// @notice Returns accumulated group member rewards for provided group.\n    function getGroupMemberRewards(\n        Storage storage self,\n        bytes memory groupPubKey\n    ) internal view returns (uint256) {\n        return self.groupMemberRewards[groupPubKey];\n    }\n\n    /// @notice Gets group public key.\n    function getGroupPublicKey(\n        Storage storage self,\n        uint256 groupIndex\n    ) internal view returns (bytes memory) {\n        return self.groups[groupIndex].groupPubKey;\n    }\n\n    /// @notice Gets group member.\n    function getGroupMember(\n        Storage storage self,\n        bytes memory groupPubKey,\n        uint256 memberIndex\n    ) internal view returns (address) {\n        return self.groupMembers[groupPubKey][memberIndex];\n    }\n\n    /// @notice Terminates group with the provided index. Reverts if the group\n    /// is already terminated.\n    function terminateGroup(\n        Storage storage self,\n        uint256 groupIndex\n    ) public {\n        require(\n            !isGroupTerminated(self, groupIndex),\n            \"Group has been already terminated\"\n        );\n        self.groups[groupIndex].terminated = true;\n        self.activeTerminatedGroups.length++;\n\n        // Sorting activeTerminatedGroups in ascending order so a non-terminated\n        // group is properly selected.\n        uint256 i;\n        for (\n            i = self.activeTerminatedGroups.length - 1;\n            i > 0 && self.activeTerminatedGroups[i - 1] > groupIndex;\n            i--\n        ) {\n            self.activeTerminatedGroups[i] = self.activeTerminatedGroups[i - 1];\n        }\n        self.activeTerminatedGroups[i] = groupIndex;\n    }\n\n    /// @notice Checks if group with the given index is terminated.\n    function isGroupTerminated(\n        Storage storage self,\n        uint256 groupIndex\n    ) internal view returns(bool) {\n        return self.groups[groupIndex].terminated;\n    }\n\n    /// @notice Checks if group with the given public key is registered.\n    function isGroupRegistered(\n        Storage storage self,\n        bytes memory groupPubKey\n    ) internal view returns(bool) {\n        // Values in `groupIndices` are flagged with `GROUP_INDEX_FLAG`\n        // and thus nonzero, even for group 0\n        return self.groupIndices[groupPubKey] > 0;\n    }\n\n    /// @notice Gets the cutoff time in blocks until which the given group is\n    /// considered as an active group assuming it hasn't been terminated before.\n    function groupActiveTimeOf(\n        Storage storage self,\n        Group memory group\n    ) internal view returns(uint256) {\n        return uint256(group.registrationBlockHeight).add(self.groupActiveTime);\n    }\n\n    /// @notice Gets the cutoff time in blocks after which the given group is\n    /// considered as stale. Stale group is an expired group which is no longer\n    /// performing any operations.\n    function groupStaleTime(\n        Storage storage self,\n        Group memory group\n    ) internal view returns(uint256) {\n        return groupActiveTimeOf(self, group).add(self.relayEntryTimeout);\n    }\n\n    /// @notice Checks if a group with the given public key is a stale group.\n    /// Stale group is an expired group which is no longer performing any\n    /// operations. It is important to understand that an expired group may\n    /// still perform some operations for which it was selected when it was still\n    /// active. We consider a group to be stale when it's expired and when its\n    /// expiration time and potentially executed operation timeout are both in\n    /// the past.\n    function isStaleGroup(\n        Storage storage self,\n        bytes memory groupPubKey\n    ) public view returns(bool) {\n        uint256 flaggedIndex = self.groupIndices[groupPubKey];\n        require(flaggedIndex != 0, \"Group does not exist\");\n        uint256 index = flaggedIndex ^ GROUP_INDEX_FLAG;\n        bool isExpired = self.expiredGroupOffset > index;\n        bool isStale = groupStaleTime(self, self.groups[index]) < block.number;\n        return isExpired && isStale;\n    }\n\n    /// @notice Checks if a group with the given index is a stale group.\n    /// Stale group is an expired group which is no longer performing any\n    /// operations. It is important to understand that an expired group may\n    /// still perform some operations for which it was selected when it was still\n    /// active. We consider a group to be stale when it's expired and when its\n    /// expiration time and potentially executed operation timeout are both in\n    /// the past.\n    function isStaleGroup(\n        Storage storage self,\n        uint256 groupIndex\n    ) public view returns(bool) {\n        return groupStaleTime(self, self.groups[groupIndex]) < block.number;\n    }\n\n    /// @notice Gets the number of active groups. Expired and terminated groups are\n    /// not counted as active.\n    function numberOfGroups(\n        Storage storage self\n    ) internal view returns(uint256) {\n        return self.groups.length.sub(self.expiredGroupOffset).sub(self.activeTerminatedGroups.length);\n    }\n\n    /// @notice Goes through groups starting from the oldest one that is still\n    /// active and checks if it hasn't expired. If so, updates the information\n    /// about expired groups so that all expired groups are marked as such.\n    function expireOldGroups(Storage storage self) public {\n        // Move expiredGroupOffset as long as there are some groups that should\n        // be marked as expired. It is possible that expired group offset will\n        // move out of the groups array by one position. It means that all groups\n        // are expired (it points to the first active group) and that place in\n        // groups array - currently empty - will be possibly filled later by\n        // a new group.\n        while(\n            self.expiredGroupOffset < self.groups.length &&\n            groupActiveTimeOf(self, self.groups[self.expiredGroupOffset]) < block.number\n        ) {\n            self.expiredGroupOffset++;\n        }\n\n        // Go through all activeTerminatedGroups and if some of the terminated\n        // groups are expired, remove them from activeTerminatedGroups collection.\n        // This is needed because we evaluate the shift of selected group index\n        // based on how many non-expired groups has been terminated.\n        for (uint i = 0; i < self.activeTerminatedGroups.length; i++) {\n            if (self.expiredGroupOffset > self.activeTerminatedGroups[i]) {\n                self.activeTerminatedGroups[i] = self.activeTerminatedGroups[self.activeTerminatedGroups.length - 1];\n                self.activeTerminatedGroups.length--;\n            }\n        }\n    }\n\n    /// @notice Returns an index of a randomly selected active group. Terminated\n    /// and expired groups are not considered as active.\n    /// Before new group is selected, information about expired groups\n    /// is updated. At least one active group needs to be present for this\n    /// function to succeed.\n    /// @param seed Random number used as a group selection seed.\n    function selectGroup(\n        Storage storage self,\n        uint256 seed\n    ) public returns(uint256) {\n        expireOldGroups(self);\n\n        require(numberOfGroups(self) > 0, \"No active groups\");\n\n        uint256 selectedGroup = seed % numberOfGroups(self);\n        return shiftByTerminatedGroups(self, shiftByExpiredGroups(self, selectedGroup));\n    }\n\n    /// @notice Evaluates the shift of selected group index based on the number of\n    /// expired groups.\n    function shiftByExpiredGroups(\n        Storage storage self,\n        uint256 selectedIndex\n    ) internal view returns(uint256) {\n        return self.expiredGroupOffset.add(selectedIndex);\n    }\n\n    /// @notice Evaluates the shift of selected group index based on the number of\n    /// non-expired, terminated groups.\n    function shiftByTerminatedGroups(\n        Storage storage self,\n        uint256 selectedIndex\n    ) internal view returns(uint256) {\n        uint256 shiftedIndex = selectedIndex;\n        for (uint i = 0; i < self.activeTerminatedGroups.length; i++) {\n            if (self.activeTerminatedGroups[i] <= shiftedIndex) {\n                shiftedIndex++;\n            }\n        }\n\n        return shiftedIndex;\n    }\n\n    /// @notice Withdraws accumulated group member rewards for operator\n    /// using the provided group index.\n    /// Once the accumulated reward is withdrawn from the selected group,\n    /// the operator is flagged as withdrawn.\n    /// Rewards can be withdrawn only from stale group.\n    /// @param operator Operator address.\n    /// @param groupIndex Group index.\n    function withdrawFromGroup(\n        Storage storage self,\n        address operator,\n        uint256 groupIndex\n    ) public returns (uint256 rewards) {\n        bool isExpired = self.expiredGroupOffset > groupIndex;\n        bool isStale = isStaleGroup(self, groupIndex);\n        require(isExpired && isStale, \"Group must be expired and stale\");\n        bytes memory groupPublicKey = getGroupPublicKey(self, groupIndex);\n        require(\n            !(self.withdrawn[groupPublicKey][operator]),\n            \"Rewards already withdrawn\"\n        );\n        self.withdrawn[groupPublicKey][operator] = true;\n        for (uint i = 0; i < self.groupMembers[groupPublicKey].length; i++) {\n            if (operator == self.groupMembers[groupPublicKey][i]) {\n                rewards = rewards.add(self.groupMemberRewards[groupPublicKey]);\n            }\n        }\n    }\n\n    /// @notice Returns members of the given group by group public key.\n    /// @param groupPubKey Group public key.\n    function getGroupMembers(\n        Storage storage self,\n        bytes memory groupPubKey\n    ) public view returns (address[] memory members) {\n        return self.groupMembers[groupPubKey];\n    }\n\n    /// @notice Returns addresses of all the members in the provided group.\n    function getGroupMembers(\n        Storage storage self,\n        uint256 groupIndex\n    ) public view returns (address[] memory members) {\n        bytes memory groupPubKey = self.groups[groupIndex].groupPubKey;\n        return self.groupMembers[groupPubKey];\n    }\n\n    function getGroupRegistrationTime(\n        Storage storage self,\n        uint256 groupIndex\n    ) public view returns (uint256) {\n        return uint256(self.groups[groupIndex].registrationTime);\n    }\n\n    /// @notice Reports unauthorized signing for the provided group. Must provide\n    /// a valid signature of the group address as a message. Successful signature\n    /// verification means the private key has been leaked and all group members\n    /// should be punished by seizing their tokens. The submitter of this proof is\n    /// rewarded with 5% of the total seized amount scaled by the reward adjustment\n    /// parameter and the rest 95% is burned. Group has to be active or expired.\n    /// Unauthorized signing cannot be reported for stale or terminated group.\n    /// In case of reporting unauthorized signing for stale group,\n    /// terminated group, or when the signature is inavlid, function reverts.\n    function reportUnauthorizedSigning(\n        Storage storage self,\n        uint256 groupIndex,\n        bytes memory signedMsgSender,\n        uint256 minimumStake\n    ) public {\n        require(!isStaleGroup(self, groupIndex), \"Group can not be stale\");\n        bytes memory groupPubKey = getGroupPublicKey(self, groupIndex);\n\n        require(\n            BLS.verifyBytes(\n                groupPubKey,\n                abi.encodePacked(msg.sender),\n                signedMsgSender\n            ),\n            \"Invalid signature\"\n        );\n\n        terminateGroup(self, groupIndex);\n        self.stakingContract.seize(minimumStake, 100, msg.sender, self.groupMembers[groupPubKey]);\n    }\n\n    function reportRelayEntryTimeout(\n        Storage storage self,\n        uint256 groupIndex,\n        uint256 groupSize\n    ) public {\n        uint256 punishment = relayEntryTimeoutPunishment(self);\n        terminateGroup(self, groupIndex);\n        // Reward is limited to min(1, 20 / group_size) of the maximum tattletale reward, see the Yellow Paper for more details.\n        uint256 rewardAdjustment = uint256(20 * 100).div(groupSize); // Reward adjustment in percentage\n        rewardAdjustment = rewardAdjustment > 100 ? 100:rewardAdjustment; // Reward adjustment can be 100% max\n        self.stakingContract.seize(punishment, rewardAdjustment, msg.sender, getGroupMembers(self, groupIndex));\n    }\n\n    /// @notice Evaluates relay entry timeout punishment using the following\n    /// rules:\n    /// - 1% of the minimum stake for the first 3 months,\n    /// - 50% of the minimum stake between the first 3 and 6 months,\n    /// - 100% of the minimum stake after the first 6 months.\n    function relayEntryTimeoutPunishment(\n        Storage storage self\n    ) public view returns (uint256) {\n        uint256 minimumStake = self.stakingContract.minimumStake();\n\n        uint256 stakingContractDeployedAt = self.stakingContract.deployedAt();\n        /* solium-disable-next-line security/no-block-members */\n        if (now < stakingContractDeployedAt + THREE_MONTHS) {\n            return minimumStake.percent(1);\n        /* solium-disable-next-line security/no-block-members */\n        } else if (now < stakingContractDeployedAt + SIX_MONTHS) {\n            return minimumStake.percent(50);\n        } else {\n            return minimumStake;\n        }\n    }\n\n    /// @notice Return whether the given operator\n    /// has withdrawn their rewards from the given group.\n    function hasWithdrawnRewards(\n        Storage storage self,\n        address operator,\n        uint256 groupIndex\n    ) public view returns (bool) {\n        return self.withdrawn[getGroupPublicKey(self, groupIndex)][operator];\n    }\n}\n"
    },
    "solidity/contracts/libraries/operator/DKGResultVerification.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/cryptography/ECDSA.sol\";\nimport \"../../utils/BytesLib.sol\";\nimport \"./GroupSelection.sol\";\n\nlibrary DKGResultVerification {\n    using BytesLib for bytes;\n    using ECDSA for bytes32;\n    using GroupSelection for GroupSelection.Storage;\n\n    struct Storage {\n        // Time in blocks after which DKG result is complete and ready to be\n        // published by clients.\n        uint256 timeDKG;\n\n        // Time in blocks after which the next group member is eligible\n        // to submit DKG result.\n        uint256 resultPublicationBlockStep;\n\n        // Size of a group in the threshold relay.\n        uint256 groupSize;\n\n        // The minimum number of signatures required to support DKG result.\n        // This number needs to be at least the same as the signing threshold\n        // and it is recommended to make it higher than the signing threshold\n        // to keep a safety margin for misbehaving members.\n        uint256 signatureThreshold;\n    }\n\n    /// @notice Verifies the submitted DKG result against supporting member\n    /// signatures and if the submitter is eligible to submit at the current\n    /// block. Every signature supporting the result has to be from a unique\n    /// group member.\n    ///\n    /// @param submitterMemberIndex Claimed submitter candidate group member index\n    /// @param groupPubKey Generated candidate group public key\n    /// @param misbehaved Bytes array of misbehaved (disqualified or inactive)\n    /// group members indexes; Indexes reflect positions of members in the group,\n    /// as outputted by the group selection protocol.\n    /// @param signatures Concatenation of signatures from members supporting the\n    /// result.\n    /// @param signingMemberIndices Indices of members corresponding to each\n    /// signature. Indices have to be unique.\n    /// @param members Addresses of candidate group members as outputted by the\n    /// group selection protocol.\n    /// @param groupSelectionEndBlock Block height at which the group selection\n    /// protocol ended.\n    function verify(\n        Storage storage self,\n        uint256 submitterMemberIndex,\n        bytes memory groupPubKey,\n        bytes memory misbehaved,\n        bytes memory signatures,\n        uint256[] memory signingMemberIndices,\n        address[] memory members,\n        uint256 groupSelectionEndBlock\n    ) public view {\n        require(submitterMemberIndex > 0, \"Invalid submitter index\");\n        require(\n            members[submitterMemberIndex - 1] == msg.sender,\n            \"Unexpected submitter index\"\n        );\n\n        uint T_init = groupSelectionEndBlock + self.timeDKG;\n        require(\n            block.number >= (T_init + (submitterMemberIndex-1) * self.resultPublicationBlockStep),\n            \"Submitter not eligible\"\n        );\n\n        require(groupPubKey.length == 128, \"Malformed group public key\");\n\n        require(\n            misbehaved.length <= self.groupSize - self.signatureThreshold,\n            \"Malformed misbehaved bytes\"\n        );\n\n        uint256 signaturesCount = signatures.length / 65;\n        require(signatures.length >= 65, \"Too short signatures array\");\n        require(signatures.length % 65 == 0, \"Malformed signatures array\");\n        require(signaturesCount == signingMemberIndices.length, \"Unexpected signatures count\");\n        require(signaturesCount >= self.signatureThreshold, \"Too few signatures\");\n        require(signaturesCount <= self.groupSize, \"Too many signatures\");\n\n        bytes32 resultHash = keccak256(abi.encodePacked(groupPubKey, misbehaved));\n\n        bytes memory current; // Current signature to be checked.\n\n        bool[] memory usedMemberIndices = new bool[](self.groupSize);\n\n        for(uint i = 0; i < signaturesCount; i++){\n            uint256 memberIndex = signingMemberIndices[i];\n            require(memberIndex > 0, \"Invalid index\");\n            require(memberIndex <= members.length, \"Index out of range\");\n\n            require(!usedMemberIndices[memberIndex - 1], \"Duplicate member index\");\n            usedMemberIndices[memberIndex - 1] = true;\n\n            current = signatures.slice(65*i, 65);\n            address recoveredAddress = resultHash.toEthSignedMessageHash().recover(current);\n            require(members[memberIndex - 1] == recoveredAddress, \"Invalid signature\");\n        }\n    }\n}\n"
    },
    "solidity/contracts/libraries/operator/Reimbursements.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"../../utils/BytesLib.sol\";\nimport \"../../TokenStaking.sol\";\n\nlibrary Reimbursements {\n    using SafeMath for uint256;\n    using BytesLib for bytes;\n\n    /// @notice Reimburses callback execution cost and surplus based on actual gas\n    /// usage to the submitter's beneficiary address and if necessary to the\n    /// callback requestor (surplus recipient).\n    /// @param stakingContract Staking contract to get the address of the beneficiary\n    /// @param gasPriceCeiling Gas price ceiling in wei\n    /// @param gasLimit Gas limit set for the callback\n    /// @param gasSpent Gas spent by the submitter on the callback\n    /// @param callbackFee Fee paid for the callback by the requestor\n    /// @param callbackSurplusRecipientData Data containing surplus recipient address\n    function reimburseCallback(\n        TokenStaking stakingContract,\n        uint256 gasPriceCeiling,\n        uint256 gasLimit,\n        uint256 gasSpent,\n        uint256 callbackFee,\n        bytes memory callbackSurplusRecipientData\n    ) public {\n        uint256 gasPrice = gasPriceCeiling;\n        // We need to check if tx.gasprice is non-zero as a workaround to a bug\n        // in go-ethereum:\n        // https://github.com/ethereum/go-ethereum/pull/20189\n        if (tx.gasprice > 0 && tx.gasprice < gasPriceCeiling) {\n            gasPrice = tx.gasprice;\n        }\n\n        // Obtain the actual callback gas expenditure and refund the surplus.\n        //\n        // In case of heavily underpriced transactions, EVM may wrap the call\n        // with additional opcodes. In this case gasSpent > gasLimit.\n        // The worst scenario cost is included in entry verification fee.\n        // If this happens we return just the gasLimit here.\n        uint256 actualCallbackGas = gasSpent < gasLimit ? gasSpent : gasLimit;\n        uint256 actualCallbackFee = actualCallbackGas.mul(gasPrice);\n\n        // Get the beneficiary.\n        address payable beneficiary = stakingContract.beneficiaryOf(msg.sender);\n\n        // If we spent less on the callback than the customer transferred for the\n        // callback execution, we need to reimburse the difference.\n        if (actualCallbackFee < callbackFee) {\n            uint256 callbackSurplus = callbackFee.sub(actualCallbackFee);\n            // Reimburse submitter with his actual callback cost.\n            beneficiary.call.value(actualCallbackFee)(\"\");\n\n            // Return callback surplus to the requestor.\n            // Expecting 32 bytes data containing 20 byte address\n            if (callbackSurplusRecipientData.length == 32) {\n                address surplusRecipient = callbackSurplusRecipientData.toAddress(12);\n                surplusRecipient.call.gas(8000).value(callbackSurplus)(\"\");\n            }\n        } else {\n            // Reimburse submitter with the callback payment sent by the requestor.\n            beneficiary.call.value(callbackFee)(\"\");\n        }\n    }\n}\n"
    },
    "solidity/contracts/libraries/operator/DelayFactor.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\nlibrary DelayFactor {\n    using SafeMath for uint256;\n\n    /// @notice Gets delay factor for rewards calculation.\n    /// @return Integer representing floating-point number with 16 decimals places.\n    function calculate(\n        uint256 currentRequestStartBlock,\n        uint256 relayEntryTimeout\n    ) public view returns(uint256 delayFactor) {\n        uint256 decimals = 1e16; // Adding 16 decimals to perform float division.\n\n        // T_deadline is the earliest block when no submissions are accepted\n        // and an entry timed out. The last block the entry can be published in is\n        //     currentRequestStartBlock + relayEntryTimeout\n        // and submission are no longer accepted from block\n        //     currentRequestStartBlock + relayEntryTimeout + 1.\n        uint256 deadlineBlock = currentRequestStartBlock.add(relayEntryTimeout).add(1);\n\n        // T_begin is the earliest block the result can be published in.\n        // Relay entry can be generated instantly after relay request is\n        // registered on-chain so a new entry can be published at the next\n        // block the earliest.\n        uint256 submissionStartBlock = currentRequestStartBlock.add(1);\n\n        // Use submissionStartBlock block as entryReceivedBlock if entry submitted earlier than expected.\n        uint256 entryReceivedBlock = block.number <= submissionStartBlock ? submissionStartBlock:block.number;\n\n        // T_remaining = T_deadline - T_received\n        uint256 remainingBlocks = deadlineBlock.sub(entryReceivedBlock);\n\n        // T_deadline - T_begin\n        uint256 submissionWindow = deadlineBlock.sub(submissionStartBlock);\n\n        // delay factor = [ T_remaining / (T_deadline - T_begin)]^2\n        //\n        // Since we add 16 decimal places to perform float division, we do:\n        // delay factor = [ T_temaining * decimals / (T_deadline - T_begin)]^2 / decimals =\n        //    = [T_remaining / (T_deadline - T_begin) ]^2 * decimals\n        delayFactor = ((remainingBlocks.mul(decimals).div(submissionWindow))**2).div(decimals);\n    }\n}"
    },
    "solidity/contracts/KeepRandomBeaconOperator.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"openzeppelin-solidity/contracts/utils/ReentrancyGuard.sol\";\nimport \"./TokenStaking.sol\";\nimport \"./KeepRegistry.sol\";\nimport \"./GasPriceOracle.sol\";\nimport \"./cryptography/BLS.sol\";\nimport \"./utils/AddressArrayUtils.sol\";\nimport \"./utils/PercentUtils.sol\";\nimport \"./libraries/operator/GroupSelection.sol\";\nimport \"./libraries/operator/Groups.sol\";\nimport \"./libraries/operator/DKGResultVerification.sol\";\nimport \"./libraries/operator/Reimbursements.sol\";\nimport \"./libraries/operator/DelayFactor.sol\";\n\ninterface ServiceContract {\n    function entryCreated(uint256 requestId, bytes calldata entry, address payable submitter) external;\n    function fundRequestSubsidyFeePool() external payable;\n    function fundDkgFeePool() external payable;\n    function callbackSurplusRecipient(uint256 requestId) external view returns(address payable);\n}\n\n/// @title KeepRandomBeaconOperator\n/// @notice Keep client facing contract for random beacon security-critical operations.\n/// Handles group creation and expiration, BLS signature verification and incentives.\n/// The contract is not upgradeable. New functionality can be implemented by deploying\n/// new versions following Keep client update and re-authorization by the stakers.\ncontract KeepRandomBeaconOperator is ReentrancyGuard, GasPriceOracleConsumer {\n    using SafeMath for uint256;\n    using PercentUtils for uint256;\n    using AddressArrayUtils for address[];\n    using GroupSelection for GroupSelection.Storage;\n    using Groups for Groups.Storage;\n    using DKGResultVerification for DKGResultVerification.Storage;\n\n    event OnGroupRegistered(bytes groupPubKey);\n    event DkgResultSubmittedEvent(\n        uint256 memberIndex,\n        bytes groupPubKey,\n        bytes misbehaved\n    );\n    event RelayEntryRequested(bytes previousEntry, bytes groupPublicKey);\n    event RelayEntrySubmitted();\n    event GroupSelectionStarted(uint256 newEntry);\n    event GroupMemberRewardsWithdrawn(\n        address indexed beneficiary,\n        address operator,\n        uint256 amount,\n        uint256 groupIndex\n    );\n    event RelayEntryTimeoutReported(uint256 indexed groupIndex);\n    event UnauthorizedSigningReported(uint256 indexed groupIndex);\n\n    GroupSelection.Storage groupSelection;\n    Groups.Storage groups;\n    DKGResultVerification.Storage dkgResultVerification;\n\n    address[] internal serviceContracts;\n\n    KeepRegistry internal registry;\n\n    TokenStaking internal stakingContract;\n\n    GasPriceOracle internal gasPriceOracle;\n\n    /// @dev Each signing group member reward expressed in wei.\n    uint256 public groupMemberBaseReward = 1000000*1e9; // 1M Gwei\n\n    /// @dev Gas price ceiling value used to calculate the gas price for reimbursement\n    /// next to the actual gas price from the transaction. We use gas price\n    /// ceiling to defend against malicious miner-submitters who can manipulate\n    /// transaction gas price.\n    uint256 public gasPriceCeiling = 60*1e9; // (60 Gwei = 60 * 10^9 wei)\n\n    /// @dev Size of a group in the threshold relay.\n    uint256 public groupSize = 64;\n\n    /// @dev Minimum number of group members needed to interact according to the\n    /// protocol to produce a relay entry.\n    uint256 public groupThreshold = 33;\n\n    /// @dev Time in blocks after which the next group member is eligible\n    /// to submit the result.\n    uint256 public resultPublicationBlockStep = 6;\n\n    /// @dev Timeout in blocks for a relay entry to appear on the chain. Blocks\n    /// are counted from the moment relay request occur.\n    ///\n    /// Timeout is never shorter than the time needed by clients to generate\n    /// relay entry and the time it takes for the last group member to become\n    /// eligible to submit the result plus at least one block to submit it.\n    uint256 public relayEntryTimeout = groupSize.mul(resultPublicationBlockStep);\n\n    /// @dev Gas required to verify BLS signature and produce successful relay\n    /// entry. Excludes callback and DKG gas. The worst case (most expensive)\n    /// scenario.\n    uint256 public entryVerificationGasEstimate = 280000;\n\n    /// @dev Gas required to submit DKG result. Excludes initiation of group selection.\n    uint256 public dkgGasEstimate = 1740000;\n\n    /// @dev Gas required to trigger DKG (starting group selection).\n    uint256 public groupSelectionGasEstimate = 200000;\n\n    /// @dev Reimbursement for the submitter of the DKG result. This value is set\n    /// when a new DKG request comes to the operator contract.\n    ///\n    /// When submitting DKG result, the submitter is reimbursed with the actual cost\n    /// and some part of the fee stored in this field may be returned to the service\n    /// contract.\n    uint256 public dkgSubmitterReimbursementFee;\n\n    /// @dev Seed value used for the genesis group selection.\n    /// https://www.wolframalpha.com/input/?i=pi+to+78+digits\n    uint256 internal constant _genesisGroupSeed = 31415926535897932384626433832795028841971693993751058209749445923078164062862;\n\n    /// @dev Service contract that triggered current group selection.\n    ServiceContract internal groupSelectionStarterContract;\n\n    // current relay request data\n    uint256 internal currentRequestId;\n    uint256 public currentRequestStartBlock;\n    uint256 public currentRequestGroupIndex;\n    bytes public currentRequestPreviousEntry;\n    uint256 internal  currentRequestEntryVerificationAndProfitFee;\n    uint256 internal currentRequestCallbackFee;\n    address internal currentRequestServiceContract;\n\n\n    /// @notice Triggers group selection if there are no active groups.\n    function genesis() public payable {\n        // If we run into a very unlikely situation when there are no active\n        // groups on the contract because of slashing and groups terminated\n        // or because beacon has not been used for a very long time and all\n        // groups expired, we first want to make a cleanup.\n        groups.expireOldGroups();\n        require(numberOfGroups() == 0, \"Groups exist\");\n        // Cleanup after potential failed DKG\n        groupSelection.finish();\n        // Set latest added service contract as a group selection starter to receive any DKG fee surplus.\n        groupSelectionStarterContract = ServiceContract(serviceContracts[serviceContracts.length.sub(1)]);\n        startGroupSelection(_genesisGroupSeed, msg.value);\n    }\n\n    modifier onlyServiceContract() {\n        require(\n            serviceContracts.contains(msg.sender),\n            \"Caller is not a service contract\"\n        );\n        _;\n    }\n\n    constructor(\n        address _serviceContract,\n        address _tokenStaking,\n        address _keepRegistry,\n        address _gasPriceOracle\n    ) public {\n        serviceContracts.push(_serviceContract);\n\n        stakingContract = TokenStaking(_tokenStaking);\n        registry = KeepRegistry(_keepRegistry);\n        gasPriceOracle = GasPriceOracle(_gasPriceOracle);\n\n        groups.stakingContract = stakingContract;\n        groups.groupActiveTime = 86400 * 14 / 15; // 14 days equivalent in 15s blocks\n        groups.relayEntryTimeout = relayEntryTimeout;\n\n        // There are 78 blocks to submit group selection tickets. To minimize\n        // the submitter's cost by minimizing the number of redundant tickets\n        // that are not selected into the group, the following approach is\n        // recommended:\n        //\n        // Tickets are submitted in 11 rounds, each round taking 6 blocks.\n        // As the basic principle, the number of leading zeros in the ticket\n        // value is subtracted from the number of rounds to determine the round\n        // the ticket should be submitted in:\n        // - in round 0, tickets with 11 or more leading zeros are submitted\n        // - in round 1, tickets with 10 or more leading zeros are submitted\n        // (...)\n        // - in round 11, tickets with no leading zeros are submitted.\n        //\n        // In each round, group member candidate needs to monitor tickets\n        // submitted by other candidates and compare them against tickets of\n        // the candidate not yet submitted to determine if continuing with\n        // ticket submission still makes sense.\n        //\n        // After 66 blocks, there is a 12 blocks mining lag allowing all\n        // outstanding ticket submissions to have a higher chance of being\n        // mined before the deadline.\n        groupSelection.ticketSubmissionTimeout = 6 * 11 + 12;\n\n        groupSelection.groupSize = groupSize;\n\n        dkgResultVerification.timeDKG = 5*(1+5) + 2*(1+10) + 20;\n        dkgResultVerification.resultPublicationBlockStep = resultPublicationBlockStep;\n        dkgResultVerification.groupSize = groupSize;\n        dkgResultVerification.signatureThreshold = groupThreshold + (groupSize - groupThreshold) / 2;\n    }\n\n    /// @notice Adds service contract\n    /// @param serviceContract Address of the service contract.\n    function addServiceContract(address serviceContract) public {\n        require(\n            registry.serviceContractUpgraderFor(address(this)) == msg.sender,\n            \"Not authorized\"\n        );\n\n        serviceContracts.push(serviceContract);\n    }\n\n    /// @notice Pulls the most recent gas price from gas price oracle.\n    function refreshGasPrice() public {\n        gasPriceCeiling = gasPriceOracle.gasPrice();\n    }\n\n    /// @notice Triggers the selection process of a new candidate group.\n    /// @param _newEntry New random beacon value that stakers will use to\n    /// generate their tickets.\n    /// @param submitter Operator of this contract.\n    function createGroup(uint256 _newEntry, address payable submitter) public payable onlyServiceContract {\n        uint256 groupSelectionStartFee = groupSelectionGasEstimate.mul(gasPriceCeiling);\n\n        groupSelectionStarterContract = ServiceContract(msg.sender);\n        startGroupSelection(_newEntry, msg.value.sub(groupSelectionStartFee));\n\n        // reimbursing a submitter that triggered group selection\n        (bool success, ) = stakingContract.beneficiaryOf(submitter).call.value(groupSelectionStartFee)(\"\");\n        require(success, \"Group selection reimbursement failed\");\n    }\n\n    function startGroupSelection(uint256 _newEntry, uint256 _payment) internal {\n        require(\n            _payment >= gasPriceCeiling.mul(dkgGasEstimate),\n            \"Insufficient DKG fee\"\n        );\n\n        require(isGroupSelectionPossible(), \"Group selection in progress\");\n\n        // If previous group selection failed and there is reimbursement left\n        // return it to the DKG fee pool.\n        if (dkgSubmitterReimbursementFee > 0) {\n            uint256 surplus = dkgSubmitterReimbursementFee;\n            dkgSubmitterReimbursementFee = 0;\n            ServiceContract(groupSelectionStarterContract).fundDkgFeePool.value(surplus)();\n        }\n\n        groupSelection.minimumStake = stakingContract.minimumStake();\n        groupSelection.start(_newEntry);\n        emit GroupSelectionStarted(_newEntry);\n        dkgSubmitterReimbursementFee = _payment;\n    }\n\n    /// @notice Checks if it is possible to fire a new group selection.\n    /// Triggering new group selection is only possible when there is no\n    /// pending group selection or when the pending group selection timed out.\n    function isGroupSelectionPossible() public view returns (bool) {\n        if (!groupSelection.inProgress) {\n            return true;\n        }\n\n        // dkgTimeout is the time after key generation protocol is expected to\n        // be complete plus the expected time to submit the result.\n        uint256 dkgTimeout = groupSelection.ticketSubmissionStartBlock +\n        groupSelection.ticketSubmissionTimeout +\n        dkgResultVerification.timeDKG +\n        groupSize * resultPublicationBlockStep;\n\n        return block.number > dkgTimeout;\n    }\n\n    /// @notice Submits ticket to request to participate in a new candidate group.\n    /// @param ticket Bytes representation of a ticket that holds the following:\n    /// - ticketValue: first 8 bytes of a result of keccak256 cryptography hash\n    ///   function on the combination of the group selection seed (previous\n    ///   beacon output), staker-specific value (address) and virtual staker index.\n    /// - stakerValue: a staker-specific value which is the address of the staker.\n    /// - virtualStakerIndex: 4-bytes number within a range of 1 to staker's weight;\n    ///   has to be unique for all tickets submitted by the given staker for the\n    ///   current candidate group selection.\n    function submitTicket(bytes32 ticket) public {\n        uint256 stakingWeight = stakingContract.eligibleStake(\n            msg.sender, address(this)\n        ).div(groupSelection.minimumStake);\n        groupSelection.submitTicket(ticket, stakingWeight);\n    }\n\n    /// @notice Gets the timeout in blocks after which group candidate ticket\n    /// submission is finished.\n    function ticketSubmissionTimeout() public view returns (uint256) {\n        return groupSelection.ticketSubmissionTimeout;\n    }\n\n    /// @notice Gets the submitted group candidate tickets so far.\n    function submittedTickets() public view returns (uint64[] memory) {\n        return groupSelection.tickets;\n    }\n\n    /// @notice Gets selected participants in ascending order of their tickets.\n    function selectedParticipants() public view returns (address[] memory) {\n        return groupSelection.selectedParticipants();\n    }\n\n    /// @notice Submits result of DKG protocol. It is on-chain part of phase 14 of\n    /// the protocol.\n    /// @param submitterMemberIndex Claimed submitter candidate group member index\n    /// @param groupPubKey Generated candidate group public key\n    /// @param misbehaved Bytes array of misbehaved (disqualified or inactive)\n    /// group members indexes in ascending order; Indexes reflect positions of\n    /// members in the group as outputted by the group selection protocol.\n    /// @param signatures Concatenation of signatures from members supporting the\n    /// result.\n    /// @param signingMembersIndexes Indices of members corresponding to each\n    /// signature.\n    function submitDkgResult(\n        uint256 submitterMemberIndex,\n        bytes memory groupPubKey,\n        bytes memory misbehaved,\n        bytes memory signatures,\n        uint[] memory signingMembersIndexes\n    ) public nonReentrant {\n        address[] memory members = selectedParticipants();\n\n        dkgResultVerification.verify(\n            submitterMemberIndex,\n            groupPubKey,\n            misbehaved,\n            signatures,\n            signingMembersIndexes,\n            members,\n            groupSelection.ticketSubmissionStartBlock + groupSelection.ticketSubmissionTimeout\n        );\n\n        groups.setGroupMembers(groupPubKey, members, misbehaved);\n        groups.addGroup(groupPubKey);\n        reimburseDkgSubmitter();\n        emit DkgResultSubmittedEvent(submitterMemberIndex, groupPubKey, misbehaved);\n        groupSelection.finish();\n    }\n\n    /// @notice Compare the reimbursement fee calculated based on the current\n    /// transaction gas price and the current price feed estimate with the DKG\n    /// reimbursement fee calculated and paid at the moment when the DKG was\n    /// requested. If there is any surplus, it will be returned to the DKG fee\n    /// pool of the service contract which triggered the DKG.\n    function reimburseDkgSubmitter() internal {\n        uint256 gasPrice = gasPriceCeiling;\n        // We need to check if tx.gasprice is non-zero as a workaround to a bug\n        // in go-ethereum:\n        // https://github.com/ethereum/go-ethereum/pull/20189\n        if (tx.gasprice > 0 && tx.gasprice < gasPriceCeiling) {\n            gasPrice = tx.gasprice;\n        }\n\n        uint256 reimbursementFee = dkgGasEstimate.mul(gasPrice);\n        address payable beneficiary = stakingContract.beneficiaryOf(msg.sender);\n\n        if (reimbursementFee < dkgSubmitterReimbursementFee) {\n            uint256 surplus = dkgSubmitterReimbursementFee.sub(reimbursementFee);\n            dkgSubmitterReimbursementFee = 0;\n            // Reimburse submitter with actual DKG cost.\n            beneficiary.call.value(reimbursementFee)(\"\");\n\n            // Return surplus to the contract that started DKG.\n            groupSelectionStarterContract.fundDkgFeePool.value(surplus)();\n        } else {\n            // If submitter used higher gas price reimburse only\n            // dkgSubmitterReimbursementFee max.\n            reimbursementFee = dkgSubmitterReimbursementFee;\n            dkgSubmitterReimbursementFee = 0;\n            beneficiary.call.value(reimbursementFee)(\"\");\n        }\n    }\n\n    /// @notice Creates a request to generate a new relay entry, which will include\n    /// a random number (by signing the previous entry's random number).\n    /// @param requestId Request Id trackable by service contract\n    /// @param previousEntry Previous relay entry\n    function sign(\n        uint256 requestId,\n        bytes memory previousEntry\n    ) public payable onlyServiceContract {\n        uint256 entryVerificationAndProfitFee = groupProfitFee().add(\n            entryVerificationFee()\n        );\n        require(\n            msg.value >= entryVerificationAndProfitFee,\n            \"Insufficient new entry fee\"\n        );\n        uint256 callbackFee = msg.value.sub(entryVerificationAndProfitFee);\n        signRelayEntry(\n            requestId, previousEntry, msg.sender,\n            entryVerificationAndProfitFee, callbackFee\n        );\n    }\n\n    function signRelayEntry(\n        uint256 requestId,\n        bytes memory previousEntry,\n        address serviceContract,\n        uint256 entryVerificationAndProfitFee,\n        uint256 callbackFee\n    ) internal {\n        require(!isEntryInProgress(), \"Beacon is busy\");\n\n        uint256 groupIndex = groups.selectGroup(uint256(keccak256(previousEntry)));\n\n        currentRequestId = requestId;\n        currentRequestStartBlock = block.number;\n        currentRequestEntryVerificationAndProfitFee = entryVerificationAndProfitFee;\n        currentRequestCallbackFee = callbackFee;\n        currentRequestGroupIndex = groupIndex;\n        currentRequestPreviousEntry = previousEntry;\n        currentRequestServiceContract = serviceContract;\n\n        bytes memory groupPubKey = groups.getGroupPublicKey(groupIndex);\n        emit RelayEntryRequested(previousEntry, groupPubKey);\n    }\n\n    /// @notice Creates a new relay entry and stores the associated data on the chain.\n    /// @param _groupSignature Group BLS signature over the concatenation of the\n    /// previous entry and seed.\n    function relayEntry(bytes memory _groupSignature) public nonReentrant {\n        require(isEntryInProgress(), \"Entry was submitted\");\n        require(!hasEntryTimedOut(), \"Entry timed out\");\n\n        bytes memory groupPubKey = groups.getGroupPublicKey(currentRequestGroupIndex);\n\n        require(\n            BLS.verify(\n                groupPubKey,\n                currentRequestPreviousEntry,\n                _groupSignature\n            ),\n            \"Invalid signature\"\n        );\n\n        emit RelayEntrySubmitted();\n\n        // Spend no more than groupSelectionGasEstimate + 40000 gas max\n        // This will prevent relayEntry failure in case the service contract is compromised\n        currentRequestServiceContract.call.gas(groupSelectionGasEstimate.add(40000))(\n            abi.encodeWithSignature(\n                \"entryCreated(uint256,bytes,address)\",\n                currentRequestId,\n                _groupSignature,\n                msg.sender\n            )\n        );\n\n        if (currentRequestCallbackFee > 0) {\n            executeCallback(uint256(keccak256(_groupSignature)));\n        }\n\n        (uint256 groupMemberReward, uint256 submitterReward, uint256 subsidy) = newEntryRewardsBreakdown();\n        groups.addGroupMemberReward(groupPubKey, groupMemberReward);\n\n        stakingContract.beneficiaryOf(msg.sender).call.value(submitterReward)(\"\");\n\n        if (subsidy > 0) {\n            currentRequestServiceContract.call.gas(35000).value(subsidy)(\n                abi.encodeWithSignature(\"fundRequestSubsidyFeePool()\")\n            );\n        }\n\n        currentRequestStartBlock = 0;\n    }\n\n    /// @notice Executes customer specified callback for the relay entry request.\n    /// @param entry The generated random number.\n    function executeCallback(uint256 entry) internal {\n        // Make sure not to spend more than what was received from the service\n        // contract for the callback\n        uint256 gasLimit = currentRequestCallbackFee.div(gasPriceCeiling);\n\n        // Make sure not to spend more than 2 million gas on a callback.\n        // This is to protect members from relay entry failure and potential\n        // slashing in case of any changes in .call() gas limit.\n        gasLimit = gasLimit > 2000000 ? 2000000 : gasLimit;\n\n        bytes memory callbackSurplusRecipientData;\n        (, callbackSurplusRecipientData) = currentRequestServiceContract.call.gas(\n            40000\n        )(abi.encodeWithSignature(\n            \"callbackSurplusRecipient(uint256)\",\n            currentRequestId\n        ));\n\n        uint256 gasBeforeCallback = gasleft();\n        currentRequestServiceContract.call.gas(\n            gasLimit\n        )(abi.encodeWithSignature(\n            \"executeCallback(uint256,uint256)\",\n            currentRequestId,\n            entry\n        ));\n\n        uint256 gasAfterCallback = gasleft();\n        uint256 gasSpent = gasBeforeCallback.sub(gasAfterCallback);\n\n        Reimbursements.reimburseCallback(\n            stakingContract,\n            gasPriceCeiling,\n            gasLimit,\n            gasSpent,\n            currentRequestCallbackFee,\n            callbackSurplusRecipientData\n        );\n    }\n\n    /// @notice Get rewards breakdown in wei for successful entry for the\n    /// current signing request.\n    function newEntryRewardsBreakdown() internal view returns(\n        uint256 groupMemberReward,\n        uint256 submitterReward,\n        uint256 subsidy\n    ) {\n        uint256 decimals = 1e16; // Adding 16 decimals to perform float division.\n\n        uint256 delayFactor = DelayFactor.calculate(\n            currentRequestStartBlock,\n            relayEntryTimeout\n        );\n        groupMemberReward = groupMemberBaseReward.mul(delayFactor).div(decimals);\n\n        // delay penalty = base reward * (1 - delay factor)\n        uint256 groupMemberDelayPenalty = groupMemberBaseReward.mul(decimals.sub(delayFactor));\n\n        // The submitter reward consists of:\n        // The callback gas expenditure (reimbursed by the service contract)\n        // The entry verification fee to cover the cost of verifying the submission,\n        // paid regardless of their gas expenditure\n        // Submitter extra reward - 5% of the delay penalties of the entire group\n        uint256 submitterExtraReward = groupMemberDelayPenalty.mul(groupSize).percent(5).div(decimals);\n        uint256 entryVerificationFee = currentRequestEntryVerificationAndProfitFee.sub(groupProfitFee());\n        submitterReward = entryVerificationFee.add(submitterExtraReward);\n\n        // Rewards not paid out to the operators are paid out to requesters to subsidize new requests.\n        subsidy = groupProfitFee().sub(groupMemberReward.mul(groupSize)).sub(submitterExtraReward);\n    }\n\n    /// @notice Returns true if generation of a new relay entry is currently in\n    /// progress.\n    function isEntryInProgress() public view returns (bool) {\n        return currentRequestStartBlock != 0;\n    }\n\n    /// @notice Returns true if the currently ongoing new relay entry generation\n    /// operation timed out. There is a certain timeout for a new relay entry\n    /// to be produced, see `relayEntryTimeout` value.\n    function hasEntryTimedOut() internal view returns (bool) {\n        return currentRequestStartBlock != 0 && block.number > currentRequestStartBlock + relayEntryTimeout;\n    }\n\n    /// @notice Function used to inform about the fact the currently ongoing\n    /// new relay entry generation operation timed out. As a result, the group\n    /// which was supposed to produce a new relay entry is immediately\n    /// terminated and a new group is selected to produce a new relay entry.\n    /// All members of the group are punished by seizing minimum stake of\n    /// their tokens. The submitter of the transaction is rewarded with a\n    /// tattletale reward which is limited to min(1, 20 / group_size) of the\n    /// maximum tattletale reward.\n    function reportRelayEntryTimeout() public {\n        require(hasEntryTimedOut(), \"Entry did not time out\");\n        groups.reportRelayEntryTimeout(currentRequestGroupIndex, groupSize);\n        currentRequestStartBlock = 0;\n\n        // We could terminate the last active group. If that's the case,\n        // do not try to execute signing again because there is no group\n        // which can handle it.\n        if (numberOfGroups() > 0) {\n            signRelayEntry(\n                currentRequestId,\n                currentRequestPreviousEntry,\n                currentRequestServiceContract,\n                currentRequestEntryVerificationAndProfitFee,\n                currentRequestCallbackFee\n            );\n        }\n\n        emit RelayEntryTimeoutReported(currentRequestGroupIndex);\n    }\n\n    /// @notice Gets group profit fee expressed in wei.\n    function groupProfitFee() public view returns(uint256) {\n        return groupMemberBaseReward.mul(groupSize);\n    }\n\n    /// @notice Checks if the specified account has enough active stake to become\n    /// network operator and that this contract has been authorized for potential\n    /// slashing.\n    ///\n    /// Having the required minimum of active stake makes the operator eligible\n    /// to join the network. If the active stake is not currently undelegating,\n    /// operator is also eligible for work selection.\n    ///\n    /// @param staker Staker's address\n    /// @return True if has enough active stake to participate in the network,\n    /// false otherwise.\n    function hasMinimumStake(address staker) public view returns(bool) {\n        return stakingContract.hasMinimumStake(staker, address(this));\n    }\n\n    /// @notice Checks if group with the given public key is registered.\n    function isGroupRegistered(bytes memory groupPubKey) public view returns(bool) {\n        return groups.isGroupRegistered(groupPubKey);\n    }\n\n    /// @notice Checks if a group with the given public key is a stale group.\n    /// Stale group is an expired group which is no longer performing any\n    /// operations. It is important to understand that an expired group may\n    /// still perform some operations for which it was selected when it was still\n    /// active. We consider a group to be stale when it's expired and when its\n    /// expiration time and potentially executed operation timeout are both in\n    /// the past.\n    function isStaleGroup(bytes memory groupPubKey) public view returns(bool) {\n        return groups.isStaleGroup(groupPubKey);\n    }\n\n    /// @notice Gets the number of active groups as currently marked in the\n    /// contract. This is the state from when the expired groups were last updated\n    /// without accounting for recent expirations.\n    ///\n    /// @dev Even if numberOfGroups() > 0, it is still possible requesting for\n    /// a new relay entry will revert with \"no active groups\" failure message.\n    /// This function returns the number of active groups as they are currently\n    /// marked on-chain. However, during relay request, before group selection,\n    /// we run group expiration and it may happen that some groups seen as active\n    /// turns out to be expired.\n    function numberOfGroups() public view returns(uint256) {\n        return groups.numberOfGroups();\n    }\n\n    /// @notice Returns accumulated group member rewards for provided group.\n    function getGroupMemberRewards(bytes memory groupPubKey) public view returns (uint256) {\n        return groups.groupMemberRewards[groupPubKey];\n    }\n\n    /// @notice Return whether the given operator has withdrawn their rewards\n    /// from the given group.\n    function hasWithdrawnRewards(address operator, uint256 groupIndex)\n        public view returns (bool) {\n        return groups.hasWithdrawnRewards(operator, groupIndex);\n    }\n\n    /// @notice Withdraws accumulated group member rewards for operator\n    /// using the provided group index.\n    /// Once the accumulated reward is withdrawn from the selected group,\n    /// the operator is flagged as withdrawn.\n    /// Rewards can be withdrawn only from stale group.\n    /// @param operator Operator address\n    /// @param groupIndex Group index\n    function withdrawGroupMemberRewards(address operator, uint256 groupIndex)\n        public nonReentrant {\n        uint256 accumulatedRewards = groups.withdrawFromGroup(operator, groupIndex);\n        (bool success, ) = stakingContract.beneficiaryOf(operator).call.value(accumulatedRewards)(\"\");\n        if (success) {\n            emit GroupMemberRewardsWithdrawn(stakingContract.beneficiaryOf(operator), operator, accumulatedRewards, groupIndex);\n        }\n    }\n\n    /// @notice Gets the index of the first active group.\n    function getFirstActiveGroupIndex() public view returns (uint256) {\n        return groups.expiredGroupOffset;\n    }\n\n    /// @notice Gets public key of the group with the given index.\n    function getGroupPublicKey(uint256 groupIndex) public view returns (bytes memory) {\n        return groups.getGroupPublicKey(groupIndex);\n    }\n\n    /// @notice Returns fee for entry verification in wei. Does not include group\n    /// profit fee, DKG contribution or callback fee.\n    function entryVerificationFee() public view returns (uint256) {\n        return entryVerificationGasEstimate.mul(gasPriceCeiling);\n    }\n\n    /// @notice Returns fee for group creation in wei. Includes the cost of DKG\n    /// and the cost of triggering group selection.\n    function groupCreationFee() public view returns (uint256) {\n        return dkgGasEstimate.add(groupSelectionGasEstimate).mul(gasPriceCeiling);\n    }\n\n    /// @notice Returns members of the given group by group public key.\n    function getGroupMembers(bytes memory groupPubKey) public view returns (address[] memory members) {\n        return groups.getGroupMembers(groupPubKey);\n    }\n\n    function getNumberOfCreatedGroups() public view returns (uint256) {\n        return groups.groups.length;\n    }\n\n    function getGroupRegistrationTime(uint256 groupIndex) public view returns (uint256) {\n        return groups.getGroupRegistrationTime(groupIndex);\n    }\n\n    function isGroupTerminated(uint256 groupIndex) public view returns (bool) {\n        return groups.isGroupTerminated(groupIndex);\n    }\n\n    /// @notice Reports unauthorized signing for the provided group. Must provide\n    /// a valid signature of the tattletale address as a message. Successful signature\n    /// verification means the private key has been leaked and all group members\n    /// should be punished by seizing their tokens. The submitter of this proof is\n    /// rewarded with 5% of the total seized amount scaled by the reward adjustment\n    /// parameter and the rest 95% is burned.\n    function reportUnauthorizedSigning(\n        uint256 groupIndex,\n        bytes memory signedMsgSender\n    ) public {\n        groups.reportUnauthorizedSigning(\n            groupIndex,\n            signedMsgSender,\n            stakingContract.minimumStake()\n        );\n        emit UnauthorizedSigningReported(groupIndex);\n    }\n}\n"
    },
    "solidity/contracts/TokenStaking.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Burnable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"./StakeDelegatable.sol\";\nimport \"./libraries/staking/MinimumStakeSchedule.sol\";\nimport \"./libraries/staking/GrantStaking.sol\";\nimport \"./libraries/staking/Locks.sol\";\nimport \"./libraries/staking/TopUps.sol\";\nimport \"./utils/PercentUtils.sol\";\nimport \"./utils/BytesLib.sol\";\nimport \"./Authorizations.sol\";\nimport \"./TokenStakingEscrow.sol\";\nimport \"./TokenSender.sol\";\n\n\n/// @title TokenStaking\n/// @notice A token staking contract for a specified standard ERC20Burnable token.\n/// A holder of the specified token can stake delegate its tokens to this contract\n/// and recover the stake after undelegation period is over.\ncontract TokenStaking is Authorizations, StakeDelegatable {\n    using BytesLib for bytes;\n    using SafeMath for uint256;\n    using PercentUtils for uint256;\n    using SafeERC20 for ERC20Burnable;\n    using GrantStaking for GrantStaking.Storage;\n    using Locks for Locks.Storage;\n    using TopUps for TopUps.Storage;\n\n    event StakeDelegated(\n        address indexed owner,\n        address indexed operator\n    );\n    event OperatorStaked(\n        address indexed operator,\n        address indexed beneficiary,\n        address indexed authorizer,\n        uint256 value\n    );\n    event StakeOwnershipTransferred(\n        address indexed operator,\n        address indexed newOwner\n    );\n    event TopUpInitiated(address indexed operator, uint256 topUp);\n    event TopUpCompleted(address indexed operator, uint256 newAmount);\n    event Undelegated(address indexed operator, uint256 undelegatedAt);\n    event RecoveredStake(address operator);\n    event TokensSlashed(address indexed operator, uint256 amount);\n    event TokensSeized(address indexed operator, uint256 amount);\n    event StakeLocked(address indexed operator, address lockCreator, uint256 until);\n    event LockReleased(address indexed operator, address lockCreator);\n    event ExpiredLockReleased(address indexed operator, address lockCreator);\n\n    uint256 public deployedAt;\n    uint256 public initializationPeriod; // varies between mainnet and testnet\n\n    ERC20Burnable internal token;\n    TokenGrant internal tokenGrant;\n    TokenStakingEscrow internal escrow;\n\n    GrantStaking.Storage internal grantStaking;\n    Locks.Storage internal locks;\n    TopUps.Storage internal topUps;\n\n    uint256 internal constant twoWeeks = 1209600; // [sec]\n    uint256 internal constant twoMonths = 5184000; // [sec]\n\n    // 2020-04-28; the date of deploying KEEP token.\n    // TX:  0xea22d72bc7de4c82798df7194734024a1f2fd57b173d0e065864ff4e9d3dc014\n    uint256 internal constant minimumStakeScheduleStart = 1588042366;\n\n    /// @notice Creates a token staking contract for a provided Standard ERC20Burnable token.\n    /// @param _token KEEP token contract.\n    /// @param _tokenGrant KEEP token grant contract.\n    /// @param _escrow Escrow dedicated for this staking contract.\n    /// @param _registry Keep contract registry contract.\n    /// @param _initializationPeriod To avoid certain attacks on work selection, recently created\n    /// operators must wait for a specific period of time before being eligible for work selection.\n    constructor(\n        ERC20Burnable _token,\n        TokenGrant _tokenGrant,\n        TokenStakingEscrow _escrow,\n        KeepRegistry _registry,\n        uint256 _initializationPeriod\n    ) Authorizations(_registry) public {\n        token = _token;\n        tokenGrant = _tokenGrant;\n        escrow = _escrow;\n        registry = _registry;\n        initializationPeriod = _initializationPeriod;\n        deployedAt = block.timestamp;\n    }\n\n    /// @notice Returns minimum amount of KEEP that allows sMPC cluster client to\n    /// participate in the Keep network. Expressed as number with 18-decimal places.\n    /// Initial minimum stake is higher than the final and lowered periodically based\n    /// on the amount of steps and the length of the minimum stake schedule in seconds.\n    function minimumStake() public view returns (uint256) {\n        return MinimumStakeSchedule.current(minimumStakeScheduleStart);\n    }\n\n    /// @notice Returns the current value of the undelegation period.\n    /// The staking contract guarantees that an undelegated operator’s stakes\n    /// will stay locked for a period of time after undelegation, and thus\n    /// available as collateral for any work the operator is engaged in.\n    /// The undelegation period is two weeks for the first two months and\n    /// two months after that.\n    function undelegationPeriod() public view returns(uint256) {\n        return block.timestamp < deployedAt.add(twoMonths) ? twoWeeks : twoMonths;\n    }\n\n    /// @notice Receives approval of token transfer and stakes the approved\n    /// amount or adds the approved amount to an existing delegation (a “top-up”).\n    /// In case of a top-up, it is expected that the operator stake is not\n    /// undelegated and that the top-up is performed from the same source of\n    /// tokens as the initial delegation. That is, if the tokens were delegated\n    /// from a grant, top-up has to be performed from the same grant. If the\n    /// delegation was done using liquid tokens, only liquid tokens from the\n    /// same owner can be used to top-up the stake.\n    /// Top-up can not be cancelled so it is important to be careful with the\n    /// amount of KEEP added to the stake.\n    /// @dev Requires that the provided token contract be the same one linked to\n    /// this contract.\n    /// @param _from The owner of the tokens who approved them to transfer.\n    /// @param _value Approved amount for the transfer and stake.\n    /// @param _token Token contract address.\n    /// @param _extraData Data for stake delegation. This byte array must have\n    /// the following values concatenated:\n    /// - Beneficiary address (20 bytes), ignored for a top-up\n    /// - Operator address (20 bytes)\n    /// - Authorizer address (20 bytes), ignored for a top-up\n    /// - Grant ID (32 bytes) - required only when called by TokenStakingEscrow\n    function receiveApproval(\n        address _from,\n        uint256 _value,\n        address _token,\n        bytes memory _extraData\n    ) public {\n        require(ERC20Burnable(_token) == token, \"Unrecognized token\");\n        require(_extraData.length >= 60, \"Corrupted delegation data\");\n\n        // Transfer tokens to this contract.\n        token.safeTransferFrom(_from, address(this), _value);\n\n        address operator = _extraData.toAddress(20);\n        // See if there is an existing delegation for this operator...\n        if (operators[operator].packedParams.getCreationTimestamp() == 0) {\n            // If there is no existing delegation, delegate tokens using\n            // beneficiary and authorizer passed in _extraData.\n            delegate(_from, _value, operator, _extraData);\n        } else {\n            // If there is an existing delegation, top-up the stake.\n            topUp(_from, _value, operator, _extraData);\n        }\n    }\n\n    /// @notice Delegates tokens to a new operator using beneficiary and\n    /// authorizer passed in _extraData parameter.\n    /// @param _from The owner of the tokens who approved them to transfer.\n    /// @param _value Approved amount for the transfer and stake.\n    /// @param _operator The new operator address.\n    /// @param _extraData Data for stake delegation as passed to receiveApproval.\n    function delegate(\n        address _from,\n        uint256 _value,\n        address _operator,\n        bytes memory _extraData\n    ) internal {\n        require(_value >= minimumStake(), \"Less than the minimum stake\");\n        \n        address payable beneficiary = address(uint160(_extraData.toAddress(0)));\n        address authorizer = _extraData.toAddress(40);\n\n        operators[_operator] = Operator(\n            OperatorParams.pack(_value, block.timestamp, 0),\n            _from,\n            beneficiary,\n            authorizer\n        );\n\n        grantStaking.tryCapturingDelegationData(\n            tokenGrant,\n            address(escrow),\n            _from,\n            _operator,\n            _extraData\n        );\n\n        emit StakeDelegated(_from, _operator);\n        emit OperatorStaked(_operator, beneficiary, authorizer, _value);\n    }\n\n    /// @notice Performs top-up to an existing operator. Tokens added during\n    /// stake initialization period are immediatelly added to the stake and\n    /// stake initialization timer is reset to the current block. Tokens added\n    /// in a top-up after the stake initialization period is over are not\n    /// included in the operator stake until the initialization period for\n    /// a top-up passes and top-up is committed. Operator must not have the stake\n    /// undelegated. It is expected that the top-up is done from the same source\n    /// of tokens as the initial delegation. That is, if the tokens were\n    /// delegated from a grant, top-up has to be performed from the same grant.\n    /// If the delegation was done using liquid tokens, only liquid tokens from\n    /// the same owner can be used to top-up the stake.\n    /// Top-up can not be cancelled so it is important to be careful with the\n    /// amount of KEEP added to the stake.\n    /// @param _from The owner of the tokens who approved them to transfer.\n    /// @param _value Approved amount for the transfer and top-up to\n    /// an existing stake.\n    /// @param _operator The new operator address.\n    /// @param _extraData Data for stake delegation as passed to receiveApproval\n    function topUp(\n        address _from,\n        uint256 _value,\n        address _operator,\n        bytes memory _extraData\n    ) internal {\n        // Top-up comes from a grant if it's been initiated from TokenGrantStake\n        // contract or if it's been initiated from TokenStakingEscrow by\n        // redelegation.\n        bool isFromGrant = address(tokenGrant.grantStakes(_operator)) == _from ||\n            address(escrow) == _from;\n\n        if (grantStaking.hasGrantDelegated(_operator)) {\n            // Operator has grant delegated. We need to see if the top-up\n            // is performed also from a grant.\n            require(isFromGrant, \"Must be from a grant\");\n            // If it is from a grant, we need to make sure it's from the same\n            // grant as the original delegation. We do not want to mix unlocking\n            // schedules.\n            uint256 previousGrantId = grantStaking.getGrantForOperator(_operator);\n            (, uint256 grantId) = grantStaking.tryCapturingDelegationData(\n                tokenGrant, address(escrow), _from, _operator, _extraData\n            );\n            require(grantId == previousGrantId, \"Not the same grant\");\n        } else {\n            // Operator has no grant delegated. We need to see if the top-up\n            // is performed from liquid tokens of the same owner.\n            require(!isFromGrant, \"Must not be from a grant\");\n            require(operators[_operator].owner == _from, \"Not the same owner\");\n        }\n\n        uint256 operatorParams = operators[_operator].packedParams;\n        if (!_isInitialized(operatorParams)) {\n            // If the stake is not yet initialized, we add tokens immediately\n            // but we also reset stake initialization time counter.\n            operators[_operator].packedParams = topUps.instantComplete(\n                _value, _operator, operatorParams, escrow\n            );\n        } else {\n            // If the stake is initialized, we do NOT add tokens immediately.\n            // We initiate the top-up and will add tokens to the stake only\n            // after the initialization period for a top-up passes.\n            topUps.initiate(_value, _operator, operatorParams, escrow);\n        }\n    }\n\n    /// @notice Commits pending top-up for the provided operator. If the top-up\n    /// did not pass the initialization period, the function fails.\n    /// @param _operator The operator with a pending top-up that is getting\n    /// committed.\n    function commitTopUp(address _operator) public {\n        operators[_operator].packedParams = topUps.commit(\n            _operator,\n            operators[_operator].packedParams,\n            initializationPeriod\n        );\n    }\n\n    /// @notice Cancels stake of tokens within the operator initialization period\n    /// without being subjected to the token lockup for the undelegation period.\n    /// This can be used to undo mistaken delegation to the wrong operator address.\n    /// @param _operator Address of the stake operator.\n    function cancelStake(address _operator) public {\n        address owner = operators[_operator].owner;\n        require(\n            msg.sender == owner ||\n            msg.sender == _operator ||\n            grantStaking.canUndelegate(_operator, tokenGrant),\n            \"Not authorized\"\n        );\n        uint256 operatorParams = operators[_operator].packedParams;\n\n        require(\n            !_isInitialized(operatorParams),\n            \"Initialized stake\"\n        );\n\n        uint256 amount = operatorParams.getAmount();\n        operators[_operator].packedParams = operatorParams.setAmount(0);\n\n        transferOrDeposit(owner, _operator, amount);\n    }\n\n    /// @notice Undelegates staked tokens. You will be able to recover your stake by calling\n    /// `recoverStake()` with operator address once undelegation period is over.\n    /// @param _operator Address of the stake operator.\n    function undelegate(address _operator) public {\n        undelegateAt(_operator, block.timestamp);\n    }\n\n    /// @notice Set an undelegation time for staked tokens.\n    /// Undelegation will begin at the specified timestamp.\n    /// You will be able to recover your stake by calling\n    /// `recoverStake()` with operator address once undelegation period is over.\n    /// @param _operator Address of the stake operator.\n    /// @param _undelegationTimestamp The timestamp undelegation is to start at.\n    function undelegateAt(\n        address _operator,\n        uint256 _undelegationTimestamp\n    ) public {\n        require(\n            msg.sender == _operator ||\n            msg.sender == operators[_operator].owner ||\n            grantStaking.canUndelegate(_operator, tokenGrant),\n            \"Not authorized\"\n        );\n        uint256 oldParams = operators[_operator].packedParams;\n        require(\n            _undelegationTimestamp >= block.timestamp &&\n            _undelegationTimestamp > oldParams.getCreationTimestamp().add(initializationPeriod),\n            \"Invalid timestamp\"\n        );\n        uint256 existingUndelegationTimestamp = oldParams.getUndelegationTimestamp();\n        require(\n            // Undelegation not in progress OR\n            existingUndelegationTimestamp == 0 ||\n            // Undelegating sooner than previously set time OR\n            existingUndelegationTimestamp > _undelegationTimestamp ||\n            // We have already checked above that msg.sender is owner, grantee,\n            // or operator. Only owner and grantee are eligible to postpone the\n            // delegation so it is enough if we exclude operator here.\n            msg.sender != _operator,\n            \"Operator may not postpone\"\n        );\n        operators[_operator].packedParams = oldParams.setUndelegationTimestamp(\n            _undelegationTimestamp\n        );\n        emit Undelegated(_operator, _undelegationTimestamp);\n    }\n\n    /// @notice Recovers staked tokens and transfers them back to the owner.\n    /// Recovering tokens can only be performed when the operator finished\n    /// undelegating.\n    /// @param _operator Operator address.\n    function recoverStake(address _operator) public {\n        uint256 operatorParams = operators[_operator].packedParams;\n        require(\n            operatorParams.getUndelegationTimestamp() != 0,\n            \"Not undelegated\"\n        );\n        require(\n            _isUndelegatingFinished(operatorParams),\n            \"Still undelegating\"\n        );\n        require(\n            !isStakeLocked(_operator),\n            \"Locked stake\"\n        );\n\n        uint256 amount = operatorParams.getAmount();\n\n        // If there is a pending top-up, force-commit it before returning tokens.\n        amount = amount.add(topUps.cancel(_operator));\n\n        operators[_operator].packedParams = operatorParams.setAmount(0);\n        transferOrDeposit(operators[_operator].owner, _operator, amount);\n\n        emit RecoveredStake(_operator);\n    }\n\n    /// @notice Gets stake delegation info for the given operator.\n    /// @param _operator Operator address.\n    /// @return amount The amount of tokens the given operator delegated.\n    /// @return createdAt The time when the stake has been delegated.\n    /// @return undelegatedAt The time when undelegation has been requested.\n    /// If undelegation has not been requested, 0 is returned.\n    function getDelegationInfo(address _operator)\n    public view returns (uint256 amount, uint256 createdAt, uint256 undelegatedAt) {\n        return operators[_operator].packedParams.unpack();\n    }\n\n    /// @notice Locks given operator stake for the specified duration.\n    /// Locked stake may not be recovered until the lock expires or is released,\n    /// even if the normal undelegation period has passed.\n    /// Only previously authorized operator contract can lock the stake.\n    /// @param operator Operator address.\n    /// @param duration Lock duration in seconds.\n    function lockStake(\n        address operator,\n        uint256 duration\n    ) public onlyApprovedOperatorContract(msg.sender) {\n        require(\n            isAuthorizedForOperator(operator, msg.sender),\n            \"Not authorized\"\n        );\n\n        uint256 operatorParams = operators[operator].packedParams;\n\n        require(\n            _isInitialized(operatorParams),\n            \"Inactive stake\"\n        );\n        require(\n            !_isUndelegating(operatorParams),\n            \"Undelegating stake\"\n        );\n\n        locks.lockStake(operator, duration);\n    }\n\n    /// @notice Removes a lock the caller had previously placed on the operator.\n    /// @dev Only for operator contracts.\n    /// To remove expired or disabled locks, use `releaseExpiredLocks`.\n    /// The authorization check ensures that the caller must have been able\n    /// to place a lock on the operator sometime in the past.\n    /// We don't need to check for current approval status of the caller\n    /// because unlocking stake cannot harm the operator\n    /// nor interfere with other operator contracts.\n    /// Therefore even disabled operator contracts may freely unlock stake.\n    /// @param operator Operator address.\n    function unlockStake(\n        address operator\n    ) public {\n        require(\n            isAuthorizedForOperator(operator, msg.sender),\n            \"Not authorized\"\n        );\n        locks.releaseLock(operator);\n    }\n\n    /// @notice Removes the lock of the specified operator contract\n    /// if the lock has expired or the contract has been disabled.\n    /// @dev Necessary for removing locks placed by contracts\n    /// that have been disabled by the panic button.\n    /// Also applicable to prevent inadvertent DoS of `recoverStake`\n    /// if too many operator contracts have failed to clean up their locks.\n    function releaseExpiredLock(\n        address operator,\n        address operatorContract\n    ) public {\n        locks.releaseExpiredLock(operator, operatorContract, address(this));\n    }\n\n    /// @notice Check whether the operator has any active locks\n    /// that haven't expired yet\n    /// and whose creators aren't disabled by the panic button.\n    function isStakeLocked(address operator) public view returns (bool) {\n        return locks.isStakeLocked(operator, address(this));\n    }\n\n    /// @notice Get the locks placed on the operator.\n    /// @return creators The addresses of operator contracts\n    /// that have placed a lock on the operator.\n    /// @return expirations The expiration times\n    /// of the locks placed on the operator.\n    function getLocks(address operator)\n        public\n        view\n        returns (address[] memory creators, uint256[] memory expirations) {\n        return locks.getLocks(operator);\n    }\n\n    /// @notice Slash provided token amount from every member in the misbehaved\n    /// operators array and burn 100% of all the tokens.\n    /// @param amountToSlash Token amount to slash from every misbehaved operator.\n    /// @param misbehavedOperators Array of addresses to seize the tokens from.\n    function slash(uint256 amountToSlash, address[] memory misbehavedOperators)\n        public\n        onlyApprovedOperatorContract(msg.sender) {\n\n        uint256 totalAmountToBurn;\n        address authoritySource = getAuthoritySource(msg.sender);\n        for (uint i = 0; i < misbehavedOperators.length; i++) {\n            address operator = misbehavedOperators[i];\n            require(authorizations[authoritySource][operator], \"Not authorized\");\n\n            uint256 operatorParams = operators[operator].packedParams;\n            require(\n                _isInitialized(operatorParams),\n                \"Inactive stake\"\n            );\n\n            require(\n                !_isStakeReleased(operator, operatorParams, msg.sender),\n                \"Stake is released\"\n            );\n\n            uint256 currentAmount = operatorParams.getAmount();\n\n            if (currentAmount < amountToSlash) {\n                totalAmountToBurn = totalAmountToBurn.add(currentAmount);\n                operators[operator].packedParams = operatorParams.setAmount(0);\n                emit TokensSlashed(operator, currentAmount);\n            } else {\n                totalAmountToBurn = totalAmountToBurn.add(amountToSlash);\n                operators[operator].packedParams = operatorParams.setAmount(\n                    currentAmount.sub(amountToSlash)\n                );\n                emit TokensSlashed(operator, amountToSlash);\n            }\n        }\n\n        token.burn(totalAmountToBurn);\n    }\n\n    /// @notice Seize provided token amount from every member in the misbehaved\n    /// operators array. The tattletale is rewarded with 5% of the total seized\n    /// amount scaled by the reward adjustment parameter and the rest 95% is burned.\n    /// @param amountToSeize Token amount to seize from every misbehaved operator.\n    /// @param rewardMultiplier Reward adjustment in percentage. Min 1% and 100% max.\n    /// @param tattletale Address to receive the 5% reward.\n    /// @param misbehavedOperators Array of addresses to seize the tokens from.\n    function seize(\n        uint256 amountToSeize,\n        uint256 rewardMultiplier,\n        address tattletale,\n        address[] memory misbehavedOperators\n    ) public onlyApprovedOperatorContract(msg.sender) {\n        uint256 totalAmountToBurn;\n        address authoritySource = getAuthoritySource(msg.sender);\n        for (uint i = 0; i < misbehavedOperators.length; i++) {\n            address operator = misbehavedOperators[i];\n            require(authorizations[authoritySource][operator], \"Not authorized\");\n\n            uint256 operatorParams = operators[operator].packedParams;\n            require(\n                _isInitialized(operatorParams),\n                \"Inactive stake\"\n            );\n\n            require(\n                !_isStakeReleased(operator, operatorParams, msg.sender),\n                \"Stake is released\"\n            );\n\n            uint256 currentAmount = operatorParams.getAmount();\n\n            if (currentAmount < amountToSeize) {\n                totalAmountToBurn = totalAmountToBurn.add(currentAmount);\n                operators[operator].packedParams = operatorParams.setAmount(0);\n                emit TokensSeized(operator, currentAmount);\n            } else {\n                totalAmountToBurn = totalAmountToBurn.add(amountToSeize);\n                operators[operator].packedParams = operatorParams.setAmount(\n                    currentAmount.sub(amountToSeize)\n                );\n                emit TokensSeized(operator, amountToSeize);\n            }\n        }\n\n        uint256 tattletaleReward = (totalAmountToBurn.percent(5)).percent(rewardMultiplier);\n\n        token.safeTransfer(tattletale, tattletaleReward);\n        token.burn(totalAmountToBurn.sub(tattletaleReward));\n    }\n\n    /// @notice Allows the current staking relationship owner to transfer the\n    /// ownership to someone else.\n    /// @param operator Address of the stake operator.\n    /// @param newOwner Address of the new staking relationship owner.\n    function transferStakeOwnership(address operator, address newOwner) public {\n        require(msg.sender == operators[operator].owner, \"Not authorized\");\n        operators[operator].owner = newOwner;\n        emit StakeOwnershipTransferred(operator, newOwner);\n    }\n\n    /// @notice Gets the eligible stake balance of the specified address.\n    /// An eligible stake is a stake that passed the initialization period\n    /// and is not currently undelegating. Also, the operator had to approve\n    /// the specified operator contract.\n    ///\n    /// Operator with a minimum required amount of eligible stake can join the\n    /// network and participate in new work selection.\n    ///\n    /// @param _operator address of stake operator.\n    /// @param _operatorContract address of operator contract.\n    /// @return an uint256 representing the eligible stake balance.\n    function eligibleStake(\n        address _operator,\n        address _operatorContract\n    ) public view returns (uint256 balance) {\n        uint256 operatorParams = operators[_operator].packedParams;\n        // To be eligible for work selection, the operator must:\n        // - have the operator contract authorized\n        // - have the stake initialized\n        // - must not be undelegating; keep in mind the `undelegatedAt` may be\n        // set to a time in the future, to schedule undelegation in advance.\n        // In this case the operator is still eligible until the timestamp\n        // `undelegatedAt`.\n        if (\n            isAuthorizedForOperator(_operator, _operatorContract) &&\n            _isInitialized(operatorParams) &&\n            !_isUndelegating(operatorParams)\n        ) {\n            balance = operatorParams.getAmount();\n        }\n    }\n\n    /// @notice Gets the active stake balance of the specified address.\n    /// An active stake is a stake that passed the initialization period,\n    /// and may be in the process of undelegation\n    /// but has not been released yet,\n    /// either because the undelegation period is not over,\n    /// or because the operator contract has an active lock on the operator.\n    /// Also, the operator had to approve the specified operator contract.\n    ///\n    /// The difference between eligible stake is that active stake does not make\n    /// the operator eligible for work selection but it may be still finishing\n    /// earlier work until the stake is released.\n    /// Operator with a minimum required\n    /// amount of active stake can join the network but cannot be selected to any\n    /// new work.\n    ///\n    /// @param _operator address of stake operator.\n    /// @param _operatorContract address of operator contract.\n    /// @return an uint256 representing the eligible stake balance.\n    function activeStake(\n        address _operator,\n        address _operatorContract\n    ) public view returns (uint256 balance) {\n        uint256 operatorParams = operators[_operator].packedParams;\n        if (\n            isAuthorizedForOperator(_operator, _operatorContract) &&\n            _isInitialized(operatorParams) &&\n            !_isStakeReleased(\n                _operator,\n                operatorParams,\n                _operatorContract\n            )\n        ) {\n            balance = operatorParams.getAmount();\n        }\n    }\n\n    /// @notice Checks if the specified account has enough active stake to become\n    /// network operator and that the specified operator contract has been\n    /// authorized for potential slashing.\n    ///\n    /// Having the required minimum of active stake makes the operator eligible\n    /// to join the network. If the active stake is not currently undelegating,\n    /// operator is also eligible for work selection.\n    ///\n    /// @param staker Staker's address\n    /// @param operatorContract Operator contract's address\n    /// @return True if has enough active stake to participate in the network,\n    /// false otherwise.\n    function hasMinimumStake(\n        address staker,\n        address operatorContract\n    ) public view returns(bool) {\n        return activeStake(staker, operatorContract) >= minimumStake();\n    }\n\n    /// @notice Is the operator with the given params initialized\n    function _isInitialized(uint256 _operatorParams)\n        internal view returns (bool) {\n        return block.timestamp > _operatorParams.getCreationTimestamp().add(initializationPeriod);\n    }\n\n    /// @notice Is the operator with the given params undelegating\n    function _isUndelegating(uint256 _operatorParams)\n        internal view returns (bool) {\n        uint256 undelegatedAt = _operatorParams.getUndelegationTimestamp();\n        return (undelegatedAt != 0) && (block.timestamp > undelegatedAt);\n    }\n\n    /// @notice Has the operator with the given params finished undelegating\n    function _isUndelegatingFinished(uint256 _operatorParams)\n        internal view returns (bool) {\n        uint256 undelegatedAt = _operatorParams.getUndelegationTimestamp();\n        return (undelegatedAt != 0) && (block.timestamp > undelegatedAt.add(undelegationPeriod()));\n    }\n\n    /// @notice Get whether the operator's stake is released\n    /// as far as the operator contract is concerned.\n    /// If the operator contract has a lock on the operator,\n    /// the operator's stake is be released when the lock expires.\n    /// Otherwise the stake is released when the operator finishes undelegating.\n    function _isStakeReleased(\n        address _operator,\n        uint256 _operatorParams,\n        address _operatorContract\n    ) internal view returns (bool) {\n        return _isUndelegatingFinished(_operatorParams) &&\n            locks.isStakeReleased(_operator, _operatorContract);\n    }\n\n    function transferOrDeposit(\n        address _owner,\n        address _operator,\n        uint256 _amount\n    ) internal {\n        if (grantStaking.hasGrantDelegated(_operator)) {\n            // For tokens staked from a grant, transfer them to the escrow.\n            TokenSender(address(token)).approveAndCall(\n                address(escrow),\n                _amount,\n                abi.encode(_operator, grantStaking.getGrantForOperator(_operator))\n            );\n        } else {\n            // For liquid tokens staked, transfer them straight to the owner.\n            token.safeTransfer(_owner, _amount);\n        }\n    }\n}\n"
    },
    "solidity/contracts/Authorizations.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity 0.5.17;\n\nimport \"./KeepRegistry.sol\";\n\n/// @title AuthorityDelegator\n/// @notice An operator contract can delegate authority to other operator\n/// contracts by implementing the AuthorityDelegator interface.\n///\n/// To delegate authority,\n/// the recipient of delegated authority must call `claimDelegatedAuthority`,\n/// specifying the contract it wants delegated authority from.\n/// The staking contract calls `delegator.__isRecognized(recipient)`\n/// and if the call returns `true`,\n/// the named delegator contract is set as the recipient's authority delegator.\n/// Any future checks of registry approval or per-operator authorization\n/// will transparently mirror the delegator's status.\n///\n/// Authority can be delegated recursively;\n/// an operator contract receiving delegated authority\n/// can recognize other operator contracts as recipients of its authority.\ninterface AuthorityDelegator {\n    function __isRecognized(address delegatedAuthorityRecipient) external returns (bool);\n}\n\n/// @title AuthorityVerifier\n/// @notice An operator contract can delegate authority to other operator\n/// contracts. Entry in the registry is not updated and source contract remains\n/// listed there as authorized. This interface is a verifier that support verification\n/// of contract authorization in case of authority delegation from the source contract.\ninterface AuthorityVerifier {\n    /// @notice Returns true if the given operator contract has been approved\n    /// for use. The function never reverts.\n    function isApprovedOperatorContract(address _operatorContract)\n        external\n        view\n        returns (bool);\n}\n\ncontract Authorizations is AuthorityVerifier {\n    // Authorized operator contracts.\n    mapping(address => mapping (address => bool)) internal authorizations;\n\n    // Granters of delegated authority to operator contracts.\n    // E.g. keep factories granting delegated authority to keeps.\n    // `delegatedAuthority[keep] = factory`\n    mapping(address => address) internal delegatedAuthority;\n\n    // Registry contract with a list of approved operator contracts and upgraders.\n    KeepRegistry internal registry;\n\n    modifier onlyApprovedOperatorContract(address operatorContract) {\n        require(\n            isApprovedOperatorContract(operatorContract),\n            \"Operator contract unapproved\"\n        );\n        _;\n    }\n\n    constructor(KeepRegistry _registry) public {\n        registry = _registry;\n    }\n\n    /// @notice Gets the authorizer for the specified operator address.\n    /// @return Authorizer address.\n    function authorizerOf(address _operator) public view returns (address);\n\n    /// @notice Authorizes operator contract to access staked token balance of\n    /// the provided operator. Can only be executed by stake operator authorizer.\n    /// Contracts using delegated authority\n    /// cannot be authorized with `authorizeOperatorContract`.\n    /// Instead, authorize `getAuthoritySource(_operatorContract)`.\n    /// @param _operator address of stake operator.\n    /// @param _operatorContract address of operator contract.\n    function authorizeOperatorContract(address _operator, address _operatorContract)\n        public\n        onlyApprovedOperatorContract(_operatorContract) {\n        require(\n            authorizerOf(_operator) == msg.sender,\n            \"Not operator authorizer\"\n        );\n        require(\n            getAuthoritySource(_operatorContract) == _operatorContract,\n            \"Delegated authority used\"\n        );\n        authorizations[_operatorContract][_operator] = true;\n    }\n\n    /// @notice Checks if operator contract has access to the staked token balance of\n    /// the provided operator.\n    /// @param _operator address of stake operator.\n    /// @param _operatorContract address of operator contract.\n    function isAuthorizedForOperator(\n        address _operator,\n        address _operatorContract\n    ) public view returns (bool) {\n        return authorizations[getAuthoritySource(_operatorContract)][_operator];\n    }\n\n    /// @notice Grant the sender the same authority as `delegatedAuthoritySource`\n    /// @dev If `delegatedAuthoritySource` is an approved operator contract\n    /// and recognizes the claimant, this relationship will be recorded in\n    /// `delegatedAuthority`. Later, the claimant can slash, seize, place locks etc.\n    /// on operators that have authorized the `delegatedAuthoritySource`.\n    /// If the `delegatedAuthoritySource` is disabled with the panic button,\n    /// any recipients of delegated authority from it will also be disabled.\n    function claimDelegatedAuthority(\n        address delegatedAuthoritySource\n    ) public onlyApprovedOperatorContract(delegatedAuthoritySource) {\n        require(\n            AuthorityDelegator(delegatedAuthoritySource).__isRecognized(msg.sender),\n            \"Unrecognized claimant\"\n        );\n        delegatedAuthority[msg.sender] = delegatedAuthoritySource;\n    }\n\n    /// @notice Checks if the operator contract is authorized in the registry.\n    /// If the contract uses delegated authority it checks authorization of the\n    /// source contract.\n    /// @param _operatorContract address of operator contract.\n    /// @return True if operator contract is approved, false if operator contract\n    /// has not been approved or if it was disabled by the panic button.\n    function isApprovedOperatorContract(address _operatorContract)\n        public\n        view\n        returns (bool)\n    {\n        return\n            registry.isApprovedOperatorContract(\n                getAuthoritySource(_operatorContract)\n            );\n    }\n\n    /// @notice Get the source of the operator contract's authority.\n    /// If the contract uses delegated authority,\n    /// returns the original source of the delegated authority.\n    /// If the contract doesn't use delegated authority,\n    /// returns the contract itself.\n    /// Authorize `getAuthoritySource(operatorContract)`\n    /// to grant `operatorContract` the authority to penalize an operator.\n    function getAuthoritySource(\n        address operatorContract\n    ) public view returns (address) {\n        address delegatedAuthoritySource = delegatedAuthority[operatorContract];\n        if (delegatedAuthoritySource == address(0)) {\n            return operatorContract;\n        }\n        return getAuthoritySource(delegatedAuthoritySource);\n    }\n}\n"
    },
    "solidity/contracts/KeepRegistry.sol": {
      "content": "pragma solidity 0.5.17;\n\n\n/// @title KeepRegistry\n/// @notice Governance owned registry of approved contracts and roles.\ncontract KeepRegistry {\n    enum ContractStatus {New, Approved, Disabled}\n\n    // Governance role is to enable recovery from key compromise by rekeying\n    // other roles. Also, it can disable operator contract panic buttons\n    // permanently.\n    address public governance;\n\n    // Registry Keeper maintains approved operator contracts. Each operator\n    // contract must be approved before it can be authorized by a staker or\n    // used by a service contract.\n    address public registryKeeper;\n\n    // Each operator contract has a Panic Button which can disable malicious\n    // or malfunctioning contract that have been previously approved by the\n    // Registry Keeper.\n    //\n    // New operator contract added to the registry has a default panic button\n    // value assigned (defaultPanicButton). Panic button for each operator\n    // contract can be later updated by Governance to individual value.\n    //\n    // It is possible to disable panic button for individual contract by\n    // setting the panic button to zero address. In such case, operator contract\n    // can not be disabled and is permanently approved in the registry.\n    mapping(address => address) public panicButtons;\n\n    // Default panic button for each new operator contract added to the\n    // registry. Can be later updated for each contract.\n    address public defaultPanicButton;\n\n    // Each service contract has a Operator Contract Upgrader whose purpose\n    // is to manage operator contracts for that specific service contract.\n    // The Operator Contract Upgrader can add new operator contracts to the\n    // service contract’s operator contract list, and deprecate old ones.\n    mapping(address => address) public operatorContractUpgraders;\n\n    // Operator contract may have a Service Contract Upgrader whose purpose is\n    // to manage service contracts for that specific operator contract.\n    // Service Contract Upgrader can add and remove service contracts\n    // from the list of service contracts approved to work with the operator\n    // contract. List of service contracts is maintained in the operator\n    // contract and is optional - not every operator contract needs to have\n    // a list of service contracts it wants to cooperate with.\n    mapping(address => address) public serviceContractUpgraders;\n\n    // The registry of operator contracts\n    mapping(address => ContractStatus) public operatorContracts;\n\n    event OperatorContractApproved(address operatorContract);\n    event OperatorContractDisabled(address operatorContract);\n\n    event GovernanceUpdated(address governance);\n    event RegistryKeeperUpdated(address registryKeeper);\n    event DefaultPanicButtonUpdated(address defaultPanicButton);\n    event OperatorContractPanicButtonDisabled(address operatorContract);\n    event OperatorContractPanicButtonUpdated(\n        address operatorContract,\n        address panicButton\n    );\n    event OperatorContractUpgraderUpdated(\n        address serviceContract,\n        address upgrader\n    );\n    event ServiceContractUpgraderUpdated(\n        address operatorContract,\n        address keeper\n    );\n\n    modifier onlyGovernance() {\n        require(governance == msg.sender, \"Not authorized\");\n        _;\n    }\n\n    modifier onlyRegistryKeeper() {\n        require(registryKeeper == msg.sender, \"Not authorized\");\n        _;\n    }\n\n    modifier onlyPanicButton(address _operatorContract) {\n        address panicButton = panicButtons[_operatorContract];\n        require(panicButton != address(0), \"Panic button disabled\");\n        require(panicButton == msg.sender, \"Not authorized\");\n        _;\n    }\n\n    modifier onlyForNewContract(address _operatorContract) {\n        require(\n            isNewOperatorContract(_operatorContract),\n            \"Not a new operator contract\"\n        );\n        _;\n    }\n\n    modifier onlyForApprovedContract(address _operatorContract) {\n        require(\n            isApprovedOperatorContract(_operatorContract),\n            \"Not an approved operator contract\"\n        );\n        _;\n    }\n\n    constructor() public {\n        governance = msg.sender;\n        registryKeeper = msg.sender;\n        defaultPanicButton = msg.sender;\n    }\n\n    function setGovernance(address _governance) public onlyGovernance {\n        governance = _governance;\n        emit GovernanceUpdated(governance);\n    }\n\n    function setRegistryKeeper(address _registryKeeper) public onlyGovernance {\n        registryKeeper = _registryKeeper;\n        emit RegistryKeeperUpdated(registryKeeper);\n    }\n\n    function setDefaultPanicButton(address _panicButton) public onlyGovernance {\n        defaultPanicButton = _panicButton;\n        emit DefaultPanicButtonUpdated(defaultPanicButton);\n    }\n\n    function setOperatorContractPanicButton(\n        address _operatorContract,\n        address _panicButton\n    ) public onlyForApprovedContract(_operatorContract) onlyGovernance {\n        require(\n            panicButtons[_operatorContract] != address(0),\n            \"Disabled panic button cannot be updated\"\n        );\n        require(\n            _panicButton != address(0),\n            \"Panic button must be non-zero address\"\n        );\n\n        panicButtons[_operatorContract] = _panicButton;\n\n        emit OperatorContractPanicButtonUpdated(\n            _operatorContract,\n            _panicButton\n        );\n    }\n\n    function disableOperatorContractPanicButton(address _operatorContract)\n        public\n        onlyForApprovedContract(_operatorContract)\n        onlyGovernance\n    {\n        require(\n            panicButtons[_operatorContract] != address(0),\n            \"Panic button already disabled\"\n        );\n\n        panicButtons[_operatorContract] = address(0);\n\n        emit OperatorContractPanicButtonDisabled(_operatorContract);\n    }\n\n    function setOperatorContractUpgrader(\n        address _serviceContract,\n        address _operatorContractUpgrader\n    ) public onlyGovernance {\n        operatorContractUpgraders[_serviceContract] = _operatorContractUpgrader;\n        emit OperatorContractUpgraderUpdated(\n            _serviceContract,\n            _operatorContractUpgrader\n        );\n    }\n\n    function setServiceContractUpgrader(\n        address _operatorContract,\n        address _serviceContractUpgrader\n    ) public onlyGovernance {\n        serviceContractUpgraders[_operatorContract] = _serviceContractUpgrader;\n        emit ServiceContractUpgraderUpdated(\n            _operatorContract,\n            _serviceContractUpgrader\n        );\n    }\n\n    function approveOperatorContract(address operatorContract)\n        public\n        onlyForNewContract(operatorContract)\n        onlyRegistryKeeper\n    {\n        operatorContracts[operatorContract] = ContractStatus.Approved;\n        panicButtons[operatorContract] = defaultPanicButton;\n        emit OperatorContractApproved(operatorContract);\n    }\n\n    function disableOperatorContract(address operatorContract)\n        public\n        onlyForApprovedContract(operatorContract)\n        onlyPanicButton(operatorContract)\n    {\n        operatorContracts[operatorContract] = ContractStatus.Disabled;\n        emit OperatorContractDisabled(operatorContract);\n    }\n\n    function isNewOperatorContract(address operatorContract)\n        public\n        view\n        returns (bool)\n    {\n        return operatorContracts[operatorContract] == ContractStatus.New;\n    }\n\n    function isApprovedOperatorContract(address operatorContract)\n        public\n        view\n        returns (bool)\n    {\n        return operatorContracts[operatorContract] == ContractStatus.Approved;\n    }\n\n    function operatorContractUpgraderFor(address _serviceContract)\n        public\n        view\n        returns (address)\n    {\n        return operatorContractUpgraders[_serviceContract];\n    }\n\n    function serviceContractUpgraderFor(address _operatorContract)\n        public\n        view\n        returns (address)\n    {\n        return serviceContractUpgraders[_operatorContract];\n    }\n}\n"
    },
    "solidity/contracts/libraries/grant/UnlockingSchedule.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\nlibrary UnlockingSchedule {\n    using SafeMath for uint256;\n\n    function getUnlockedAmount(\n        uint256 _now,\n        uint256 grantedAmount,\n        uint256 duration,\n        uint256 start,\n        uint256 cliff\n    ) internal pure returns (uint256) {\n        bool cliffNotReached = _now < cliff;\n        if (cliffNotReached) { return 0; }\n\n        uint256 timeElapsed = _now.sub(start);\n\n        bool unlockingPeriodFinished = timeElapsed >= duration;\n        if (unlockingPeriodFinished) { return grantedAmount; }\n\n        return grantedAmount.mul(timeElapsed).div(duration);\n    }\n}\n"
    },
    "solidity/contracts/utils/AddressArrayUtils.sol": {
      "content": "pragma solidity 0.5.17;\n\n\nlibrary AddressArrayUtils {\n\n    function contains(address[] memory self, address _address)\n        internal\n        pure\n        returns (bool)\n    {\n        for (uint i = 0; i < self.length; i++) {\n            if (_address == self[i]) {\n                return true;\n            }\n        }\n        return false;\n    }\n\n    function removeAddress(address[] storage self, address _addressToRemove)\n        internal\n        returns (address[] storage)\n    {\n        for (uint i = 0; i < self.length; i++) {\n            // If address is found in array.\n            if (_addressToRemove == self[i]) {\n                // Delete element at index and shift array.\n                for (uint j = i; j < self.length-1; j++) {\n                    self[j] = self[j+1];\n                }\n                self.length--;\n                i--;\n            }\n        }\n        return self;\n    }\n}\n"
    },
    "solidity/contracts/TokenGrantStake.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Burnable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"./TokenStaking.sol\";\nimport \"./TokenSender.sol\";\nimport \"./utils/BytesLib.sol\";\n\n/// @dev Interface of sender contract for approveAndCall pattern.\ninterface tokenSender {\n    function approveAndCall(address _spender, uint256 _value, bytes calldata _extraData) external;\n}\n\ncontract TokenGrantStake {\n    using SafeMath for uint256;\n    using BytesLib for bytes;\n\n    ERC20Burnable token;\n    TokenStaking tokenStaking;\n\n    address tokenGrant; // Address of the master grant contract.\n\n    uint256 grantId; // ID of the grant for this stake.\n    uint256 amount; // Amount of staked tokens.\n    address operator; // Operator of the stake.\n\n    constructor(\n        address _tokenAddress,\n        uint256 _grantId,\n        address _tokenStaking\n    ) public {\n        require(\n            _tokenAddress != address(0x0),\n            \"Token address can't be zero.\"\n        );\n        require(\n            _tokenStaking != address(0x0),\n            \"Staking contract address can't be zero.\"\n        );\n\n        token = ERC20Burnable(_tokenAddress);\n        tokenGrant = msg.sender;\n        grantId = _grantId;\n        tokenStaking = TokenStaking(_tokenStaking);\n    }\n\n    function stake(\n        uint256 _amount,\n        bytes memory _extraData\n    ) public onlyGrant {\n        amount = _amount;\n        operator = _extraData.toAddress(20);\n        tokenSender(address(token)).approveAndCall(\n            address(tokenStaking),\n            _amount,\n            _extraData\n        );\n    }\n\n    function getGrantId() public view onlyGrant returns (uint256) {\n        return grantId;\n    }\n\n    function getAmount() public view onlyGrant returns (uint256) {\n        return amount;\n    }\n\n    function getStakingContract() public view onlyGrant returns (address) {\n        return address(tokenStaking);\n    }\n\n    function getDetails() public view onlyGrant returns (\n        uint256 _grantId,\n        uint256 _amount,\n        address _tokenStaking\n    ) {\n        return (\n            grantId,\n            amount,\n            address(tokenStaking)\n        );\n    }\n\n    function cancelStake() public onlyGrant returns (uint256) {\n        tokenStaking.cancelStake(operator);\n        return returnTokens();\n    }\n\n    function undelegate() public onlyGrant {\n        tokenStaking.undelegate(operator);\n    }\n\n    function recoverStake() public onlyGrant returns (uint256) {\n        tokenStaking.recoverStake(operator);\n        return returnTokens();\n    }\n\n    function returnTokens() internal returns (uint256) {\n        uint256 returnedAmount = token.balanceOf(address(this));\n        amount -= returnedAmount;\n        token.transfer(tokenGrant, returnedAmount);\n        return returnedAmount;\n    }\n\n    modifier onlyGrant {\n        require(\n            msg.sender == tokenGrant,\n            \"For token grant contract only\"\n        );\n        _;\n    }\n}\n"
    },
    "solidity/contracts/GrantStakingPolicy.sol": {
      "content": "pragma solidity 0.5.17;\n\n/// @title GrantStakingPolicy\n/// @notice A staking policy defines the function `getStakeableAmount`\n/// which calculates how many tokens may be staked from a token grant.\ncontract GrantStakingPolicy {\n    function getStakeableAmount(\n        uint256 _now,\n        uint256 grantedAmount,\n        uint256 duration,\n        uint256 start,\n        uint256 cliff,\n        uint256 withdrawn) public view returns (uint256);\n}\n"
    },
    "solidity/contracts/TokenStakingEscrow.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/ownership/Ownable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\nimport \"./libraries/grant/UnlockingSchedule.sol\";\nimport \"./utils/BytesLib.sol\";\nimport \"./KeepToken.sol\";\nimport \"./utils/BytesLib.sol\";\nimport \"./TokenGrant.sol\";\nimport \"./ManagedGrant.sol\";\nimport \"./TokenSender.sol\";\n\n/// @title TokenStakingEscrow\n/// @notice Escrow lets the staking contract to deposit undelegated, granted\n/// tokens and either withdraw them based on the grant unlocking schedule or\n/// re-delegate them to another operator.\n/// @dev The owner of TokenStakingEscrow is TokenStaking contract and only owner\n/// can deposit. This contract works with an assumption that operator is unique\n/// in the scope of `TokenStaking`, that is, no more than one delegation in the\n/// `TokenStaking` can be done do the given operator ever. Even if the previous\n/// delegation ended, operator address cannot be reused.\ncontract TokenStakingEscrow is Ownable {\n\n    using SafeERC20 for IERC20;\n    using SafeMath for uint256;\n    using BytesLib for bytes;\n    using UnlockingSchedule for uint256;\n\n    event Deposited(\n        address indexed operator,\n        uint256 indexed grantId,\n        uint256 amount\n    );\n    event DepositRedelegated(\n        address indexed previousOperator,\n        address indexed newOperator,\n        uint256 indexed grantId,\n        uint256 amount\n    );\n    event DepositWithdrawn(\n        address indexed operator,\n        address indexed grantee,\n        uint256 amount\n    );\n    event RevokedDepositWithdrawn(\n        address indexed operator,\n        address indexed grantManager,\n        uint256 amount\n    );\n    event EscrowAuthorized(\n        address indexed grantManager,\n        address escrow\n    );\n\n    IERC20 public keepToken;\n    TokenGrant public tokenGrant;\n\n    struct Deposit {\n        uint256 grantId;\n        uint256 amount;\n        uint256 withdrawn;\n        uint256 redelegated;\n    }\n\n    // operator address -> KEEP deposit\n    mapping(address => Deposit) internal deposits;\n\n    // Other escrows authorized by grant manager. Grantee may request to migrate\n    // tokens to another authorized escrow.\n    // grant manager -> escrow -> authorized?\n    mapping(address => mapping (address => bool)) internal authorizedEscrows;\n\n    constructor(\n        KeepToken _keepToken,\n        TokenGrant _tokenGrant\n    ) public {\n        keepToken = _keepToken;\n        tokenGrant = _tokenGrant;\n    }\n\n    /// @notice receiveApproval accepts deposits from staking contract and\n    /// stores them in the escrow by the operator address from which they were\n    /// undelegated. Function expects operator address and grant identifier to\n    /// be passed as ABI-encoded information in extraData. Grant with the given\n    /// identifier has to exist.\n    /// @param from Address depositing tokens - it has to be the address of\n    /// TokenStaking contract owning TokenStakingEscrow.\n    /// @param value The amount of KEEP tokens deposited.\n    /// @param token The address of KEEP token contract.\n    /// @param extraData ABI-encoded data containing operator address (32 bytes)\n    /// and grant ID (32 bytes).\n    function receiveApproval(\n        address from,\n        uint256 value,\n        address token,\n        bytes memory extraData\n    ) public {\n        require(IERC20(token) == keepToken, \"Not a KEEP token\");\n        require(msg.sender == token, \"KEEP token is not the sender\");\n        require(extraData.length == 64, \"Unexpected data length\");\n\n        (address operator, uint256 grantId) = abi.decode(\n            extraData, (address, uint256)\n        );\n        receiveDeposit(from, value, operator, grantId);\n    }\n\n    /// @notice Redelegates deposit or part of the deposit to another operator.\n    /// Uses the same staking contract as the original delegation.\n    /// @param previousOperator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    /// @dev Only grantee is allowed to call this function. For managed grant,\n    /// caller has to be the managed grantee.\n    /// @param amount Amount of tokens to delegate.\n    /// @param extraData Data for stake delegation. This byte array must have\n    /// the following values concatenated:\n    /// - Beneficiary address (20 bytes)\n    /// - Operator address (20 bytes)\n    /// - Authorizer address (20 bytes)\n    function redelegate(\n        address previousOperator,\n        uint256 amount,\n        bytes memory extraData\n    ) public {\n        require(extraData.length == 60, \"Corrupted delegation data\");\n\n        Deposit memory deposit = deposits[previousOperator];\n\n        uint256 grantId = deposit.grantId;\n        address newOperator = extraData.toAddress(20);\n        require(isGrantee(msg.sender, grantId), \"Not authorized\");\n        require(getAmountRevoked(grantId) == 0, \"Grant revoked\");\n        require(\n            availableAmount(previousOperator) >= amount,\n            \"Insufficient balance\"\n        );\n        require(\n            !hasDeposit(newOperator),\n            \"Redelegating to previously used operator is not allowed\"\n        );\n\n        deposits[previousOperator].redelegated = deposit.redelegated.add(amount);\n\n        TokenSender(address(keepToken)).approveAndCall(\n            owner(), // TokenStaking contract associated with the escrow\n            amount,\n            abi.encodePacked(extraData, grantId)\n        );\n\n        emit DepositRedelegated(\n            previousOperator,\n            newOperator,\n            grantId,\n            amount\n        );\n    }\n\n    /// @notice Returns true if there is a deposit for the given operator in\n    /// the escrow. Otherwise, returns false.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function hasDeposit(address operator) public view returns (bool) {\n        return depositedAmount(operator) > 0;\n    }\n\n    /// @notice Returns the currently available amount deposited in the escrow\n    /// that may or may not be currently withdrawable. The available amount\n    /// is the amount initially deposited minus the amount withdrawn and\n    /// redelegated so far from that deposit.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function availableAmount(address operator) public view returns (uint256) {\n        Deposit memory deposit = deposits[operator];\n        return deposit.amount.sub(deposit.withdrawn).sub(deposit.redelegated);\n    }\n\n    /// @notice Returns the total amount deposited in the escrow after\n    /// undelegating it from the provided operator.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function depositedAmount(address operator) public view returns (uint256) {\n        return deposits[operator].amount;\n    }\n\n    /// @notice Returns grant ID for the amount deposited in the escrow after\n    /// undelegating it from the provided operator.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function depositGrantId(address operator) public view returns (uint256) {\n        return deposits[operator].grantId;\n    }\n\n    /// @notice Returns the amount withdrawn so far from the value deposited\n    /// in the escrow contract after undelegating it from the provided operator.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function depositWithdrawnAmount(address operator) public view returns (uint256) {\n        return deposits[operator].withdrawn;\n    }\n\n    /// @notice Returns the total amount redelegated so far from the value\n    /// deposited in the escrow contract after undelegating it from the provided\n    /// operator.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function depositRedelegatedAmount(address operator) public view returns (uint256) {\n        return deposits[operator].redelegated;\n    }\n\n    /// @notice Returns the currently withdrawable amount that was previously\n    /// deposited in the escrow after undelegating it from the provided operator.\n    /// Tokens are unlocked based on their grant unlocking schedule.\n    /// Function returns 0 for non-existing deposits and revoked grants if they\n    /// have been revoked before they fully unlocked.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function withdrawable(address operator) public view returns (uint256) {\n        Deposit memory deposit = deposits[operator];\n\n        // Staked tokens can be only withdrawn by grantee for non-revoked grant\n        // assuming that grant has not fully unlocked before it's been\n        // revoked.\n        //\n        // It is not possible for the escrow to determine the number of tokens\n        // it should return to the grantee of a revoked grant given different\n        // possible staking contracts and staking policies.\n        //\n        // If the entire grant unlocked before it's been reverted, escrow\n        // lets to withdraw the entire deposited amount.\n        if (getAmountRevoked(deposit.grantId) == 0) {\n            (\n                uint256 duration,\n                uint256 start,\n                uint256 cliff\n            ) = getUnlockingSchedule(deposit.grantId);\n\n            uint256 unlocked = now.getUnlockedAmount(\n                deposit.amount,\n                duration,\n                start,\n                cliff\n            );\n\n            if (deposit.withdrawn.add(deposit.redelegated) < unlocked) {\n                return unlocked.sub(deposit.withdrawn).sub(deposit.redelegated);\n            }\n        }\n\n        return 0;\n    }\n\n    /// @notice Withdraws currently unlocked tokens deposited in the escrow\n    /// after undelegating them from the provided operator. Only grantee or\n    /// operator can call this function. Important: this function can not be\n    /// called for a `ManagedGrant` grantee. This may lead to locking tokens.\n    /// For `ManagedGrant`, please use `withdrawToManagedGrantee` instead.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function withdraw(address operator) public {\n        Deposit memory deposit = deposits[operator];\n        address grantee = getGrantee(deposit.grantId);\n\n        // Make sure this function is not called for a managed grant.\n        // If called for a managed grant, tokens could be locked there.\n        // Better be safe than sorry.\n        (bool success, ) = address(this).call(\n            abi.encodeWithSignature(\"getManagedGrantee(address)\", grantee)\n        );\n        require(!success, \"Can not be called for managed grant\");\n\n        require(\n            msg.sender == grantee || msg.sender == operator,\n            \"Only grantee or operator can withdraw\"\n        );\n\n        withdraw(deposit, operator, grantee);\n    }\n\n    /// @notice Withdraws currently unlocked tokens deposited in the escrow\n    /// after undelegating them from the provided operator. Only grantee or\n    /// operator can call this function. This function works only for\n    /// `ManagedGrant` grantees. For a standard grant, please use `withdraw`\n    /// instead.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function withdrawToManagedGrantee(address operator) public {\n        Deposit memory deposit = deposits[operator];\n        address managedGrant = getGrantee(deposit.grantId);\n        address grantee = getManagedGrantee(managedGrant);\n\n        require(\n            msg.sender == grantee || msg.sender == operator,\n            \"Only grantee or operator can withdraw\"\n        );\n\n        withdraw(deposit, operator, grantee);\n    }\n\n    /// @notice Migrates all available tokens to another authorized escrow.\n    /// Can be requested only by grantee.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    /// @param receivingEscrow Escrow to which tokens should be migrated.\n    /// @dev The receiving escrow needs to accept deposits from this escrow, at\n    /// least for the period of migration.\n    function migrate(\n        address operator,\n        address receivingEscrow\n    ) public {\n        Deposit memory deposit = deposits[operator];\n        require(isGrantee(msg.sender, deposit.grantId), \"Not authorized\");\n\n        address grantManager = getGrantManager(deposit.grantId);\n        require(\n            authorizedEscrows[grantManager][receivingEscrow],\n            \"Escrow not authorized\"\n        );\n\n        uint256 amountLeft = availableAmount(operator);\n        deposits[operator].withdrawn = deposit.withdrawn.add(amountLeft);\n        TokenSender(address(keepToken)).approveAndCall(\n            receivingEscrow,\n            amountLeft,\n            abi.encode(operator, deposit.grantId)\n        );\n    }\n\n    /// @notice Withdraws the entire amount that is still deposited in the\n    /// escrow in case the grant has been revoked. Anyone can call this function\n    /// and the entire amount is transferred back to the grant manager.\n    /// @param operator Address of the operator from the undelegated/canceled\n    /// delegation from which tokens were deposited.\n    function withdrawRevoked(address operator) public {\n        Deposit memory deposit = deposits[operator];\n\n        require(\n            getAmountRevoked(deposit.grantId) > 0,\n            \"No revoked tokens to withdraw\"\n        );\n\n        address grantManager = getGrantManager(deposit.grantId);\n        withdrawRevoked(deposit, operator, grantManager);\n    }\n\n    /// @notice Used by grant manager to authorize another escrows for\n    // funds migration.\n    function authorizeEscrow(address anotherEscrow) public {\n        require(\n            anotherEscrow != address(0x0),\n            \"Escrow address can't be zero\"\n        );\n        authorizedEscrows[msg.sender][anotherEscrow] = true;\n        emit EscrowAuthorized(msg.sender, anotherEscrow);\n    }\n\n    /// @notice Resolves the final grantee of ManagedGrant contract. If the\n    /// provided address is not a ManagedGrant contract, function reverts.\n    /// @param managedGrant Address of the managed grant contract.\n    function getManagedGrantee(\n        address managedGrant\n    ) public view returns(address) {\n        ManagedGrant grant = ManagedGrant(managedGrant);\n        return grant.grantee();\n    }\n\n    function receiveDeposit(\n        address from,\n        uint256 value,\n        address operator,\n        uint256 grantId\n    ) internal {\n        // This contract works with an assumption that operator is unique.\n        // This is fine as long as the staking contract works with the same\n        // assumption so we are limiting deposits to the staking contract only.\n        require(from == owner(), \"Only owner can deposit\");\n        require(\n            getAmountGranted(grantId) > 0,\n            \"Grant with this ID does not exist\"\n        );\n\n        require(\n            !hasDeposit(operator),\n            \"Stake for the operator already deposited in the escrow\"\n        );\n\n        keepToken.safeTransferFrom(from, address(this), value);\n        deposits[operator] = Deposit(grantId, value, 0, 0);\n\n        emit Deposited(operator, grantId, value);\n    }\n\n    function isGrantee(\n        address maybeGrantee,\n        uint256 grantId\n    ) internal returns (bool) {\n        // Let's check the simplest case first - standard grantee.\n        // If the given address is set as a grantee for grant with the given ID,\n        // we return true.\n        address grantee = getGrantee(grantId);\n        if (maybeGrantee == grantee) {\n            return true;\n        }\n\n        // If the given address is not a standard grantee, there is still\n        // a chance that address is a managed grantee. We are calling\n        // getManagedGrantee that will cast the grantee to ManagedGrant and try\n        // to call getGrantee() function. If this call returns non-zero address,\n        // it means we are dealing with a ManagedGrant.\n        (, bytes memory result) = address(this).call(\n            abi.encodeWithSignature(\"getManagedGrantee(address)\", grantee)\n        );\n        if (result.length == 0) {\n            return false;\n        }\n        // At this point we know we are dealing with a ManagedGrant, so the last\n        // thing we need to check is whether the managed grantee of that grant\n        // is the grantee address passed as a parameter.\n        address managedGrantee = abi.decode(result, (address));\n        return maybeGrantee == managedGrantee;\n    }\n\n    function withdraw(\n        Deposit memory deposit,\n        address operator,\n        address grantee\n    ) internal {\n        uint256 amount = withdrawable(operator);\n\n        deposits[operator].withdrawn = deposit.withdrawn.add(amount);\n        keepToken.safeTransfer(grantee, amount);\n\n        emit DepositWithdrawn(operator, grantee, amount);\n    }\n\n    function withdrawRevoked(\n        Deposit memory deposit,\n        address operator,\n        address grantManager\n    ) internal {\n        uint256 amount = availableAmount(operator);\n        deposits[operator].withdrawn = amount;\n        keepToken.safeTransfer(grantManager, amount);\n\n        emit RevokedDepositWithdrawn(operator, grantManager, amount);\n    }\n\n    function getAmountGranted(uint256 grantId) internal view returns (\n        uint256 amountGranted\n    ) {\n        (amountGranted,,,,,) = tokenGrant.getGrant(grantId);\n    }\n\n    function getAmountRevoked(uint256 grantId) internal view returns (\n        uint256 amountRevoked\n    ) {\n        (,,,amountRevoked,,) = tokenGrant.getGrant(grantId);\n    }\n\n    function getUnlockingSchedule(uint256 grantId) internal view returns (\n        uint256 duration,\n        uint256 start,\n        uint256 cliff\n    ) {\n        (,duration,start,cliff,) = tokenGrant.getGrantUnlockingSchedule(grantId);\n    }\n\n    function getGrantee(uint256 grantId) internal view returns (\n        address grantee\n    ) {\n        (,,,,,grantee) = tokenGrant.getGrant(grantId);\n    }\n\n    function getGrantManager(uint256 grantId) internal view returns (\n        address grantManager\n    ) {\n        (grantManager,,,,) = tokenGrant.getGrantUnlockingSchedule(grantId);\n    }\n}"
    },
    "solidity/contracts/TokenSender.sol": {
      "content": "pragma solidity 0.5.17;\n\n/// @dev Interface of sender contract for approveAndCall pattern.\ninterface TokenSender {\n    function approveAndCall(address _spender, uint256 _value, bytes calldata _extraData) external;\n}"
    },
    "solidity/contracts/KeepToken.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Burnable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Detailed.sol\";\n\n\n/// @dev Interface of recipient contract for approveAndCall pattern.\ninterface tokenRecipient { function receiveApproval(address _from, uint256 _value, address _token, bytes calldata _extraData) external; }\n\n/// @title KEEP Token\n/// @dev Standard ERC20Burnable token\ncontract KeepToken is ERC20Burnable, ERC20Detailed {\n    string public constant NAME = \"KEEP Token\";\n    string public constant SYMBOL = \"KEEP\";\n    uint8 public constant DECIMALS = 18; // The number of digits after the decimal place when displaying token values on-screen.\n    uint256 public constant INITIAL_SUPPLY = 10**27; // 1 billion tokens, 18 decimal places.\n\n    /// @dev Gives msg.sender all of existing tokens.\n    constructor() public ERC20Detailed(NAME, SYMBOL, DECIMALS) {\n        _mint(msg.sender, INITIAL_SUPPLY);\n    }\n\n    /// @notice Set allowance for other address and notify.\n    /// Allows `_spender` to spend no more than `_value` tokens\n    /// on your behalf and then ping the contract about it.\n    /// @param _spender The address authorized to spend.\n    /// @param _value The max amount they can spend.\n    /// @param _extraData Extra information to send to the approved contract.\n    function approveAndCall(address _spender, uint256 _value, bytes memory _extraData) public returns (bool success) {\n        tokenRecipient spender = tokenRecipient(_spender);\n        if (approve(_spender, _value)) {\n            spender.receiveApproval(msg.sender, _value, address(this), _extraData);\n            return true;\n        }\n    }\n\n}\n"
    },
    "solidity/contracts/ManagedGrant.sol": {
      "content": "pragma solidity ^0.5.4;\n\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Burnable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"./TokenGrant.sol\";\n\n/// @title ManagedGrant\n/// @notice A managed grant acts as the grantee towards the token grant contract,\n/// proxying instructions from the actual grantee.\n/// The address used by the actual grantee\n/// to issue instructions and withdraw tokens\n/// can be reassigned with the consent of the grant manager.\ncontract ManagedGrant {\n    using SafeERC20 for ERC20Burnable;\n\n    ERC20Burnable public token;\n    TokenGrant public tokenGrant;\n    address public grantManager;\n    uint256 public grantId;\n    address public grantee;\n    address public requestedNewGrantee;\n\n    event GranteeReassignmentRequested(\n        address newGrantee\n    );\n    event GranteeReassignmentConfirmed(\n        address oldGrantee,\n        address newGrantee\n    );\n    event GranteeReassignmentCancelled(\n        address cancelledRequestedGrantee\n    );\n    event GranteeReassignmentChanged(\n        address previouslyRequestedGrantee,\n        address newRequestedGrantee\n    );\n    event TokensWithdrawn(\n        address destination,\n        uint256 amount\n    );\n\n    constructor(\n        address _tokenAddress,\n        address _tokenGrant,\n        address _grantManager,\n        uint256 _grantId,\n        address _grantee\n    ) public {\n        token = ERC20Burnable(_tokenAddress);\n        tokenGrant = TokenGrant(_tokenGrant);\n        grantManager = _grantManager;\n        grantId = _grantId;\n        grantee = _grantee;\n    }\n\n    /// @notice Request a reassignment of the grantee address.\n    /// Can only be called by the grantee.\n    /// @param _newGrantee The requested new grantee.\n    function requestGranteeReassignment(address _newGrantee)\n        public\n        onlyGrantee\n        noRequestedReassignment\n    {\n        _setRequestedNewGrantee(_newGrantee);\n        emit GranteeReassignmentRequested(_newGrantee);\n    }\n\n    /// @notice Cancel a pending grantee reassignment request.\n    /// Can only be called by the grantee.\n    function cancelReassignmentRequest()\n        public\n        onlyGrantee\n        withRequestedReassignment\n    {\n        address cancelledGrantee = requestedNewGrantee;\n        requestedNewGrantee = address(0);\n        emit GranteeReassignmentCancelled(cancelledGrantee);\n    }\n\n    /// @notice Change a pending reassignment request to a different grantee.\n    /// Can only be called by the grantee.\n    /// @param _newGrantee The address of the new requested grantee.\n    function changeReassignmentRequest(address _newGrantee)\n        public\n        onlyGrantee\n        withRequestedReassignment\n    {\n        address previouslyRequestedGrantee = requestedNewGrantee;\n        require(\n            previouslyRequestedGrantee != _newGrantee,\n            \"Unchanged reassignment request\"\n        );\n        _setRequestedNewGrantee(_newGrantee);\n        emit GranteeReassignmentChanged(previouslyRequestedGrantee, _newGrantee);\n    }\n\n    /// @notice Confirm a grantee reassignment request and set the new grantee as the grantee.\n    /// Can only be called by the grant manager.\n    /// @param _newGrantee The address of the new grantee.\n    /// Must match the currently requested new grantee.\n    function confirmGranteeReassignment(address _newGrantee)\n        public\n        onlyManager\n        withRequestedReassignment\n    {\n        address oldGrantee = grantee;\n        require(\n            requestedNewGrantee == _newGrantee,\n            \"Reassignment address mismatch\"\n        );\n        grantee = requestedNewGrantee;\n        requestedNewGrantee = address(0);\n        emit GranteeReassignmentConfirmed(oldGrantee, _newGrantee);\n    }\n\n    /// @notice Withdraw all unlocked tokens from the grant.\n    function withdraw() public onlyGrantee {\n        require(\n            requestedNewGrantee == address(0),\n            \"Can not withdraw with pending reassignment\"\n        );\n        tokenGrant.withdraw(grantId);\n        uint256 amount = token.balanceOf(address(this));\n        token.safeTransfer(grantee, amount);\n        emit TokensWithdrawn(grantee, amount);\n    }\n\n    /// @notice Stake tokens from the grant.\n    /// @param _stakingContract The contract to stake the tokens on.\n    /// @param _amount The amount of tokens to stake.\n    /// @param _extraData Data for the stake delegation.\n    /// This byte array must have the following values concatenated:\n    /// beneficiary address (20 bytes)\n    /// operator address (20 bytes)\n    /// authorizer address (20 bytes)\n    function stake(\n        address _stakingContract,\n        uint256 _amount,\n        bytes memory _extraData\n    ) public onlyGrantee {\n        tokenGrant.stake(grantId, _stakingContract, _amount, _extraData);\n    }\n\n    /// @notice Cancel delegating tokens to the given operator.\n    function cancelStake(address _operator) public onlyGranteeOr(_operator) {\n        tokenGrant.cancelStake(_operator);\n    }\n\n    /// @notice Begin undelegating tokens from the given operator.\n    function undelegate(address _operator) public onlyGranteeOr(_operator) {\n        tokenGrant.undelegate(_operator);\n    }\n\n    /// @notice Recover tokens previously staked and delegated to the operator.\n    function recoverStake(address _operator) public {\n        tokenGrant.recoverStake(_operator);\n    }\n\n    function _setRequestedNewGrantee(address _newGrantee) internal {\n        require(_newGrantee != address(0), \"Invalid new grantee address\");\n        require(_newGrantee != grantee, \"New grantee same as current grantee\");\n\n        requestedNewGrantee = _newGrantee;\n    }\n\n    modifier withRequestedReassignment {\n        require(\n            requestedNewGrantee != address(0),\n            \"No reassignment requested\"\n        );\n        _;\n    }\n\n    modifier noRequestedReassignment {\n        require(\n            requestedNewGrantee == address(0),\n            \"Reassignment already requested\"\n        );\n        _;\n    }\n\n    modifier onlyGrantee {\n        require(\n            msg.sender == grantee,\n            \"Only grantee may perform this action\"\n        );\n        _;\n    }\n\n    modifier onlyGranteeOr(address _operator) {\n        require(\n            msg.sender == grantee || msg.sender == _operator,\n            \"Only grantee or operator may perform this action\"\n        );\n        _;\n    }\n\n    modifier onlyManager {\n        require(\n            msg.sender == grantManager,\n            \"Only grantManager may perform this action\"\n        );\n        _;\n    }\n}\n"
    },
    "solidity/contracts/libraries/RolesLookup.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"../utils/AddressArrayUtils.sol\";\nimport \"../StakeDelegatable.sol\";\nimport \"../TokenGrant.sol\";\nimport \"../ManagedGrant.sol\";\n\n/// @title Roles Lookup\n/// @notice Library facilitating lookup of roles in stake delegation setup.\nlibrary RolesLookup {\n    using AddressArrayUtils for address[];\n\n    /// @notice Returns true if the tokenOwner delegated tokens to operator\n    /// using the provided stakeDelegatable contract. Othwerwise, returns false.\n    /// This function works only for the case when tokenOwner own those tokens\n    /// and those are not tokens from a grant.\n    function isTokenOwnerForOperator(\n        address tokenOwner,\n        address operator,\n        StakeDelegatable stakeDelegatable\n    ) internal view returns (bool) {\n        return stakeDelegatable.ownerOf(operator) == tokenOwner;\n    }\n\n    /// @notice Returns true if the grantee delegated tokens to operator\n    /// with the provided tokenGrant contract. Otherwise, returns false.\n    /// This function works only for the case when tokens were generated from\n    /// a non-managed grant, that is, the grantee is a non-contract address to\n    /// which the delegated tokens were granted.\n    /// @dev This function does not validate the staking reltionship on\n    /// a particular staking contract. It only checks whether the grantee\n    /// staked at least one time with the given operator. If you are interested\n    /// in a particular token staking contract, you need to perform additional\n    /// check.\n    function isGranteeForOperator(\n        address grantee,\n        address operator,\n        TokenGrant tokenGrant\n    ) internal view returns (bool) {\n        address[] memory operators = tokenGrant.getGranteeOperators(grantee);\n        return operators.contains(operator);\n    }\n\n    /// @notice Returns true if the grantee from the given managed grant contract\n    /// delegated tokens to operator with the provided tokenGrant contract.\n    /// Otherwise, returns false. In case the grantee declared by the managed\n    /// grant contract does not match the provided grantee, function reverts.\n    /// This function works only for cases when grantee, from TokenGrant's\n    /// perspective, is a smart contract exposing grantee() function returning\n    /// the final grantee. One possibility is the ManagedGrant contract.\n    /// @dev This function does not validate the staking reltionship on\n    /// a particular staking contract. It only checks whether the grantee\n    /// staked at least one time with the given operator. If you are interested\n    /// in a particular token staking contract, you need to perform additional\n    /// check.\n    function isManagedGranteeForOperator(\n        address grantee,\n        address operator,\n        address managedGrantContract,\n        TokenGrant tokenGrant\n    ) internal view returns (bool) {\n        require(\n            ManagedGrant(managedGrantContract).grantee() == grantee,\n            \"Not a grantee of the provided contract\"\n        );\n\n        address[] memory operators = tokenGrant.getGranteeOperators(\n            managedGrantContract\n        );\n        return operators.contains(operator);\n    }\n\n    /// @notice Returns true if grant with the given ID has been created with\n    /// managed grant pointing currently to the grantee passed as a parameter.\n    /// @dev The function does not revert if grant has not been created with\n    /// a managed grantee. This function is not a view because it uses low-level\n    /// call to check if the grant has been created with a managed grant.\n    /// It does not however modify any state.\n    function isManagedGranteeForGrant(\n        address grantee,\n        uint256 grantId,\n        TokenGrant tokenGrant\n    ) internal returns (bool) {\n        (,,,,, address managedGrant) = tokenGrant.getGrant(grantId);\n        (, bytes memory result) = managedGrant.call(\n            abi.encodeWithSignature(\"grantee()\")\n        );\n        if (result.length == 0) {\n            return false;\n        }\n        address managedGrantee = abi.decode(result, (address));\n        return grantee == managedGrantee;\n    }\n}"
    },
    "solidity/contracts/libraries/staking/LockUtils.sol": {
      "content": "pragma solidity 0.5.17;\n\nlibrary LockUtils {\n    struct Lock {\n        address creator;\n        uint96 expiresAt;\n    }\n\n    /// @notice The LockSet is like an array of unique `uint256`s,\n    /// but additionally supports O(1) membership tests and removals.\n    /// @dev Because the LockSet relies on a mapping,\n    /// it can only be used in storage, not in memory.\n    struct LockSet {\n        // locks[positions[lock.creator] - 1] = lock\n        Lock[] locks;\n        mapping(address => uint256) positions;\n    }\n\n    /// @notice Check whether the LockSet `self` contains a lock by `creator`\n    function contains(LockSet storage self, address creator)\n        internal view returns (bool) {\n        return (self.positions[creator] != 0);\n    }\n\n    function getLockTime(LockSet storage self, address creator)\n        internal view returns (uint96) {\n        uint256 positionPlusOne = self.positions[creator];\n        if (positionPlusOne == 0) { return 0; }\n        return self.locks[positionPlusOne - 1].expiresAt;\n    }\n\n    /// @notice Set the lock of `creator` to `expiresAt`,\n    /// overriding the current value if any.\n    function setLock(\n        LockSet storage self,\n        address _creator,\n        uint96 _expiresAt\n    ) internal {\n        uint256 positionPlusOne = self.positions[_creator];\n        Lock memory lock = Lock(_creator, _expiresAt);\n        // No existing lock\n        if (positionPlusOne == 0) {\n            self.locks.push(lock);\n            self.positions[_creator] = self.locks.length;\n        // Existing lock present\n        } else {\n            self.locks[positionPlusOne - 1].expiresAt = _expiresAt;\n        }\n    }\n\n    /// @notice Remove the lock of `creator`.\n    /// If no lock present, do nothing.\n    function releaseLock(\n        LockSet storage self,\n        address _creator\n    ) internal {\n        uint256 positionPlusOne = self.positions[_creator];\n        if (positionPlusOne != 0) {\n            uint256 lockCount = self.locks.length;\n            if (positionPlusOne != lockCount) {\n                // Not the last lock,\n                // so we need to move the last lock into the emptied position.\n                Lock memory lastLock = self.locks[lockCount - 1];\n                self.locks[positionPlusOne - 1] = lastLock;\n                self.positions[lastLock.creator] = positionPlusOne;\n            }\n            self.locks.length--;\n            self.positions[_creator] = 0;\n        }\n    }\n\n    /// @notice Return the locks of the LockSet `self`.\n    function enumerate(LockSet storage self)\n        internal view returns (Lock[] memory) {\n        return self.locks;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/ERC20Burnable.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"../../GSN/Context.sol\";\nimport \"./ERC20.sol\";\n\n/**\n * @dev Extension of {ERC20} that allows token holders to destroy both their own\n * tokens and those that they have an allowance for, in a way that can be\n * recognized off-chain (via event analysis).\n */\ncontract ERC20Burnable is Context, ERC20 {\n    /**\n     * @dev Destroys `amount` tokens from the caller.\n     *\n     * See {ERC20-_burn}.\n     */\n    function burn(uint256 amount) public {\n        _burn(_msgSender(), amount);\n    }\n\n    /**\n     * @dev See {ERC20-_burnFrom}.\n     */\n    function burnFrom(address account, uint256 amount) public {\n        _burnFrom(account, amount);\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/GSN/Context.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/*\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with GSN meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\ncontract Context {\n    // Empty internal constructor, to prevent people from mistakenly deploying\n    // an instance of this contract, which should be used via inheritance.\n    constructor () internal { }\n    // solhint-disable-previous-line no-empty-blocks\n\n    function _msgSender() internal view returns (address payable) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view returns (bytes memory) {\n        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691\n        return msg.data;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/ERC20.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"../../GSN/Context.sol\";\nimport \"./IERC20.sol\";\nimport \"../../math/SafeMath.sol\";\n\n/**\n * @dev Implementation of the {IERC20} interface.\n *\n * This implementation is agnostic to the way tokens are created. This means\n * that a supply mechanism has to be added in a derived contract using {_mint}.\n * For a generic mechanism see {ERC20Mintable}.\n *\n * TIP: For a detailed writeup see our guide\n * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How\n * to implement supply mechanisms].\n *\n * We have followed general OpenZeppelin guidelines: functions revert instead\n * of returning `false` on failure. This behavior is nonetheless conventional\n * and does not conflict with the expectations of ERC20 applications.\n *\n * Additionally, an {Approval} event is emitted on calls to {transferFrom}.\n * This allows applications to reconstruct the allowance for all accounts just\n * by listening to said events. Other implementations of the EIP may not emit\n * these events, as it isn't required by the specification.\n *\n * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}\n * functions have been added to mitigate the well-known issues around setting\n * allowances. See {IERC20-approve}.\n */\ncontract ERC20 is Context, IERC20 {\n    using SafeMath for uint256;\n\n    mapping (address => uint256) private _balances;\n\n    mapping (address => mapping (address => uint256)) private _allowances;\n\n    uint256 private _totalSupply;\n\n    /**\n     * @dev See {IERC20-totalSupply}.\n     */\n    function totalSupply() public view returns (uint256) {\n        return _totalSupply;\n    }\n\n    /**\n     * @dev See {IERC20-balanceOf}.\n     */\n    function balanceOf(address account) public view returns (uint256) {\n        return _balances[account];\n    }\n\n    /**\n     * @dev See {IERC20-transfer}.\n     *\n     * Requirements:\n     *\n     * - `recipient` cannot be the zero address.\n     * - the caller must have a balance of at least `amount`.\n     */\n    function transfer(address recipient, uint256 amount) public returns (bool) {\n        _transfer(_msgSender(), recipient, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-allowance}.\n     */\n    function allowance(address owner, address spender) public view returns (uint256) {\n        return _allowances[owner][spender];\n    }\n\n    /**\n     * @dev See {IERC20-approve}.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function approve(address spender, uint256 amount) public returns (bool) {\n        _approve(_msgSender(), spender, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-transferFrom}.\n     *\n     * Emits an {Approval} event indicating the updated allowance. This is not\n     * required by the EIP. See the note at the beginning of {ERC20};\n     *\n     * Requirements:\n     * - `sender` and `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `amount`.\n     * - the caller must have allowance for `sender`'s tokens of at least\n     * `amount`.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) public returns (bool) {\n        _transfer(sender, recipient, amount);\n        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amount, \"ERC20: transfer amount exceeds allowance\"));\n        return true;\n    }\n\n    /**\n     * @dev Atomically increases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function increaseAllowance(address spender, uint256 addedValue) public returns (bool) {\n        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));\n        return true;\n    }\n\n    /**\n     * @dev Atomically decreases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `spender` must have allowance for the caller of at least\n     * `subtractedValue`.\n     */\n    function decreaseAllowance(address spender, uint256 subtractedValue) public returns (bool) {\n        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue, \"ERC20: decreased allowance below zero\"));\n        return true;\n    }\n\n    /**\n     * @dev Moves tokens `amount` from `sender` to `recipient`.\n     *\n     * This is internal function is equivalent to {transfer}, and can be used to\n     * e.g. implement automatic token fees, slashing mechanisms, etc.\n     *\n     * Emits a {Transfer} event.\n     *\n     * Requirements:\n     *\n     * - `sender` cannot be the zero address.\n     * - `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `amount`.\n     */\n    function _transfer(address sender, address recipient, uint256 amount) internal {\n        require(sender != address(0), \"ERC20: transfer from the zero address\");\n        require(recipient != address(0), \"ERC20: transfer to the zero address\");\n\n        _balances[sender] = _balances[sender].sub(amount, \"ERC20: transfer amount exceeds balance\");\n        _balances[recipient] = _balances[recipient].add(amount);\n        emit Transfer(sender, recipient, amount);\n    }\n\n    /** @dev Creates `amount` tokens and assigns them to `account`, increasing\n     * the total supply.\n     *\n     * Emits a {Transfer} event with `from` set to the zero address.\n     *\n     * Requirements\n     *\n     * - `to` cannot be the zero address.\n     */\n    function _mint(address account, uint256 amount) internal {\n        require(account != address(0), \"ERC20: mint to the zero address\");\n\n        _totalSupply = _totalSupply.add(amount);\n        _balances[account] = _balances[account].add(amount);\n        emit Transfer(address(0), account, amount);\n    }\n\n     /**\n     * @dev Destroys `amount` tokens from `account`, reducing the\n     * total supply.\n     *\n     * Emits a {Transfer} event with `to` set to the zero address.\n     *\n     * Requirements\n     *\n     * - `account` cannot be the zero address.\n     * - `account` must have at least `amount` tokens.\n     */\n    function _burn(address account, uint256 amount) internal {\n        require(account != address(0), \"ERC20: burn from the zero address\");\n\n        _balances[account] = _balances[account].sub(amount, \"ERC20: burn amount exceeds balance\");\n        _totalSupply = _totalSupply.sub(amount);\n        emit Transfer(account, address(0), amount);\n    }\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the `owner`s tokens.\n     *\n     * This is internal function is equivalent to `approve`, and can be used to\n     * e.g. set automatic allowances for certain subsystems, etc.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `owner` cannot be the zero address.\n     * - `spender` cannot be the zero address.\n     */\n    function _approve(address owner, address spender, uint256 amount) internal {\n        require(owner != address(0), \"ERC20: approve from the zero address\");\n        require(spender != address(0), \"ERC20: approve to the zero address\");\n\n        _allowances[owner][spender] = amount;\n        emit Approval(owner, spender, amount);\n    }\n\n    /**\n     * @dev Destroys `amount` tokens from `account`.`amount` is then deducted\n     * from the caller's allowance.\n     *\n     * See {_burn} and {_approve}.\n     */\n    function _burnFrom(address account, uint256 amount) internal {\n        _burn(account, amount);\n        _approve(account, _msgSender(), _allowances[account][_msgSender()].sub(amount, \"ERC20: burn amount exceeds allowance\"));\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/ERC20Detailed.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"./IERC20.sol\";\n\n/**\n * @dev Optional functions from the ERC20 standard.\n */\ncontract ERC20Detailed is IERC20 {\n    string private _name;\n    string private _symbol;\n    uint8 private _decimals;\n\n    /**\n     * @dev Sets the values for `name`, `symbol`, and `decimals`. All three of\n     * these values are immutable: they can only be set once during\n     * construction.\n     */\n    constructor (string memory name, string memory symbol, uint8 decimals) public {\n        _name = name;\n        _symbol = symbol;\n        _decimals = decimals;\n    }\n\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() public view returns (string memory) {\n        return _name;\n    }\n\n    /**\n     * @dev Returns the symbol of the token, usually a shorter version of the\n     * name.\n     */\n    function symbol() public view returns (string memory) {\n        return _symbol;\n    }\n\n    /**\n     * @dev Returns the number of decimals used to get its user representation.\n     * For example, if `decimals` equals `2`, a balance of `505` tokens should\n     * be displayed to a user as `5,05` (`505 / 10 ** 2`).\n     *\n     * Tokens usually opt for a value of 18, imitating the relationship between\n     * Ether and Wei.\n     *\n     * NOTE: This information is only used for _display_ purposes: it in\n     * no way affects any of the arithmetic of the contract, including\n     * {IERC20-balanceOf} and {IERC20-transfer}.\n     */\n    function decimals() public view returns (uint8) {\n        return _decimals;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/IERC20.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP. Does not include\n * the optional functions; to access them see {ERC20Detailed}.\n */\ninterface IERC20 {\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `recipient`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `sender` to `recipient` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"./IERC20.sol\";\nimport \"../../math/SafeMath.sol\";\nimport \"../../utils/Address.sol\";\n\n/**\n * @title SafeERC20\n * @dev Wrappers around ERC20 operations that throw on failure (when the token\n * contract returns false). Tokens that return no value (and instead revert or\n * throw on failure) are also supported, non-reverting calls are assumed to be\n * successful.\n * To use this library you can add a `using SafeERC20 for ERC20;` statement to your contract,\n * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.\n */\nlibrary SafeERC20 {\n    using SafeMath for uint256;\n    using Address for address;\n\n    function safeTransfer(IERC20 token, address to, uint256 value) internal {\n        callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));\n    }\n\n    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {\n        callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));\n    }\n\n    function safeApprove(IERC20 token, address spender, uint256 value) internal {\n        // safeApprove should only be called when setting an initial allowance,\n        // or when resetting it to zero. To increase and decrease it, use\n        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'\n        // solhint-disable-next-line max-line-length\n        require((value == 0) || (token.allowance(address(this), spender) == 0),\n            \"SafeERC20: approve from non-zero to non-zero allowance\"\n        );\n        callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));\n    }\n\n    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 newAllowance = token.allowance(address(this), spender).add(value);\n        callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));\n    }\n\n    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 newAllowance = token.allowance(address(this), spender).sub(value, \"SafeERC20: decreased allowance below zero\");\n        callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     */\n    function callOptionalReturn(IERC20 token, bytes memory data) private {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves.\n\n        // A Solidity high level call has three parts:\n        //  1. The target address is checked to verify it contains contract code\n        //  2. The call itself is made, and success asserted\n        //  3. The return value is decoded, which in turn checks the size of the returned data.\n        // solhint-disable-next-line max-line-length\n        require(address(token).isContract(), \"SafeERC20: call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = address(token).call(data);\n        require(success, \"SafeERC20: low-level call failed\");\n\n        if (returndata.length > 0) { // Return data is optional\n            // solhint-disable-next-line max-line-length\n            require(abi.decode(returndata, (bool)), \"SafeERC20: ERC20 operation did not succeed\");\n        }\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/utils/Address.sol": {
      "content": "pragma solidity ^0.5.5;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * This test is non-exhaustive, and there may be false-negatives: during the\n     * execution of a contract's constructor, its address will be reported as\n     * not containing a contract.\n     *\n     * IMPORTANT: It is unsafe to assume that an address for which this\n     * function returns false is an externally-owned account (EOA) and not a\n     * contract.\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies in extcodesize, which returns 0 for contracts in\n        // construction, since the code is only stored at the end of the\n        // constructor execution.\n\n        // According to EIP-1052, 0x0 is the value returned for not-yet created accounts\n        // and 0xc5d2460186f7233c927e7db2dcc703c0e500b653ca82273b7bfad8045d85a470 is returned\n        // for accounts without code, i.e. `keccak256('')`\n        bytes32 codehash;\n        bytes32 accountHash = 0xc5d2460186f7233c927e7db2dcc703c0e500b653ca82273b7bfad8045d85a470;\n        // solhint-disable-next-line no-inline-assembly\n        assembly { codehash := extcodehash(account) }\n        return (codehash != 0x0 && codehash != accountHash);\n    }\n\n    /**\n     * @dev Converts an `address` into `address payable`. Note that this is\n     * simply a type cast: the actual underlying value is not changed.\n     *\n     * _Available since v2.4.0._\n     */\n    function toPayable(address account) internal pure returns (address payable) {\n        return address(uint160(account));\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     *\n     * _Available since v2.4.0._\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        // solhint-disable-next-line avoid-call-value\n        (bool success, ) = recipient.call.value(amount)(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n}\n"
    },
    "solidity/contracts/StakeDelegatable.sol": {
      "content": "/**\n▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓\n  ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓▌        ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓\n  ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀\n  ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌\n▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓\n\n                           Trust math, not hardware.\n*/\n\npragma solidity 0.5.17;\n\nimport \"./utils/OperatorParams.sol\";\n\n/// @title Stake Delegatable\n/// @notice A base contract to allow stake delegation for staking contracts.\ncontract StakeDelegatable {\n    using OperatorParams for uint256;\n\n    mapping(address => Operator) internal operators;\n\n    struct Operator {\n        uint256 packedParams;\n        address owner;\n        address payable beneficiary;\n        address authorizer;\n    }\n\n    /// @notice Gets the stake balance of the specified address.\n    /// @param _address The address to query the balance of.\n    /// @return An uint256 representing the amount staked by the passed address.\n    function balanceOf(address _address) public view returns (uint256 balance) {\n        return operators[_address].packedParams.getAmount();\n    }\n\n    /// @notice Gets the stake owner for the specified operator address.\n    /// @return Stake owner address.\n    function ownerOf(address _operator) public view returns (address) {\n        return operators[_operator].owner;\n    }\n\n    /// @notice Gets the beneficiary for the specified operator address.\n    /// @return Beneficiary address.\n    function beneficiaryOf(address _operator) public view returns (address payable) {\n        return operators[_operator].beneficiary;\n    }\n\n    /// @notice Gets the authorizer for the specified operator address.\n    /// @return Authorizer address.\n    function authorizerOf(address _operator) public view returns (address) {\n        return operators[_operator].authorizer;\n    }\n}\n"
    },
    "solidity/contracts/utils/OperatorParams.sol": {
      "content": "pragma solidity 0.5.17;\n\nlibrary OperatorParams {\n    // OperatorParams packs values that are commonly used together\n    // into a single uint256 to reduce the cost functions\n    // like querying eligibility.\n    //\n    // An OperatorParams uint256 contains:\n    // - the operator's staked token amount (uint128)\n    // - the operator's creation timestamp (uint64)\n    // - the operator's undelegation timestamp (uint64)\n    //\n    // These are packed as [amount | createdAt | undelegatedAt]\n    //\n    // Staked KEEP is stored in an uint128,\n    // which is sufficient because KEEP tokens have 18 decimals (2^60)\n    // and there will be at most 10^9 KEEP in existence (2^30).\n    //\n    // Creation and undelegation times are stored in an uint64 each.\n    // Thus uint64s would be sufficient for around 3*10^11 years.\n    uint256 constant TIMESTAMP_WIDTH = 64;\n    uint256 constant AMOUNT_WIDTH = 128;\n\n    uint256 constant TIMESTAMP_MAX = (2**TIMESTAMP_WIDTH) - 1;\n    uint256 constant AMOUNT_MAX = (2**AMOUNT_WIDTH) - 1;\n\n    uint256 constant CREATION_SHIFT = TIMESTAMP_WIDTH;\n    uint256 constant AMOUNT_SHIFT = 2 * TIMESTAMP_WIDTH;\n\n    function pack(\n        uint256 amount,\n        uint256 createdAt,\n        uint256 undelegatedAt\n    ) internal pure returns (uint256) {\n        // Check for staked amount overflow.\n        // We shouldn't actually ever need this.\n        require(\n            amount <= AMOUNT_MAX,\n            \"uint128 overflow\"\n        );\n        // Bitwise OR the timestamps together.\n        // The resulting number is equal or greater than either,\n        // and tells if we have a bit set outside the 64 available bits.\n        require(\n            (createdAt | undelegatedAt) <= TIMESTAMP_MAX,\n            \"uint64 overflow\"\n        );\n\n        return (amount << AMOUNT_SHIFT | createdAt << CREATION_SHIFT | undelegatedAt);\n    }\n\n    function unpack(uint256 packedParams) internal pure returns (\n        uint256 amount,\n        uint256 createdAt,\n        uint256 undelegatedAt\n    ) {\n        amount = getAmount(packedParams);\n        createdAt = getCreationTimestamp(packedParams);\n        undelegatedAt = getUndelegationTimestamp(packedParams);\n    }\n\n    function getAmount(uint256 packedParams)\n        internal pure returns (uint256) {\n        return (packedParams >> AMOUNT_SHIFT) & AMOUNT_MAX;\n    }\n\n    function setAmount(\n        uint256 packedParams,\n        uint256 amount\n    ) internal pure returns (uint256) {\n        return pack(\n            amount,\n            getCreationTimestamp(packedParams),\n            getUndelegationTimestamp(packedParams)\n        );\n    }\n\n    function getCreationTimestamp(uint256 packedParams)\n        internal pure returns (uint256) {\n        return (packedParams >> CREATION_SHIFT) & TIMESTAMP_MAX;\n    }\n\n    function setCreationTimestamp(\n        uint256 packedParams,\n        uint256 creationTimestamp\n    ) internal pure returns (uint256) {\n        return pack(\n            getAmount(packedParams),\n            creationTimestamp,\n            getUndelegationTimestamp(packedParams)\n        );\n    }\n\n    function getUndelegationTimestamp(uint256 packedParams)\n        internal pure returns (uint256) {\n        return packedParams & TIMESTAMP_MAX;\n    }\n\n    function setUndelegationTimestamp(\n        uint256 packedParams,\n        uint256 undelegationTimestamp\n    ) internal pure returns (uint256) {\n        return pack(\n            getAmount(packedParams),\n            getCreationTimestamp(packedParams),\n            undelegationTimestamp\n        );\n    }\n\n    function setAmountAndCreationTimestamp(\n        uint256 packedParams,\n        uint256 amount,\n        uint256 creationTimestamp\n    ) internal pure returns (uint256) {\n        return pack(\n            amount,\n            creationTimestamp,\n            getUndelegationTimestamp(packedParams)\n        );\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/math/SafeMath.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Wrappers over Solidity's arithmetic operations with added overflow\n * checks.\n *\n * Arithmetic operations in Solidity wrap on overflow. This can easily result\n * in bugs, because programmers usually assume that an overflow raises an\n * error, which is the standard behavior in high level programming languages.\n * `SafeMath` restores this intuition by reverting the transaction when an\n * operation overflows.\n *\n * Using this library instead of the unchecked operations eliminates an entire\n * class of bugs, so it's recommended to use it always.\n */\nlibrary SafeMath {\n    /**\n     * @dev Returns the addition of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `+` operator.\n     *\n     * Requirements:\n     * - Addition cannot overflow.\n     */\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a + b;\n        require(c >= a, \"SafeMath: addition overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     * - Subtraction cannot overflow.\n     */\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\n        return sub(a, b, \"SafeMath: subtraction overflow\");\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     * - Subtraction cannot overflow.\n     *\n     * _Available since v2.4.0._\n     */\n    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b <= a, errorMessage);\n        uint256 c = a - b;\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the multiplication of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `*` operator.\n     *\n     * Requirements:\n     * - Multiplication cannot overflow.\n     */\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the\n        // benefit is lost if 'b' is also tested.\n        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522\n        if (a == 0) {\n            return 0;\n        }\n\n        uint256 c = a * b;\n        require(c / a == b, \"SafeMath: multiplication overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     * - The divisor cannot be zero.\n     */\n    function div(uint256 a, uint256 b) internal pure returns (uint256) {\n        return div(a, b, \"SafeMath: division by zero\");\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     * - The divisor cannot be zero.\n     *\n     * _Available since v2.4.0._\n     */\n    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        // Solidity only automatically asserts when dividing by 0\n        require(b > 0, errorMessage);\n        uint256 c = a / b;\n        // assert(a == b * c + a % b); // There is no case in which this doesn't hold\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     * - The divisor cannot be zero.\n     */\n    function mod(uint256 a, uint256 b) internal pure returns (uint256) {\n        return mod(a, b, \"SafeMath: modulo by zero\");\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts with custom message when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     * - The divisor cannot be zero.\n     *\n     * _Available since v2.4.0._\n     */\n    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b != 0, errorMessage);\n        return a % b;\n    }\n}\n"
    },
    "solidity/contracts/libraries/staking/MinimumStakeSchedule.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\n/// @notice MinimumStakeSchedule defines the minimum stake parametrization and\n/// schedule. It starts with a minimum stake of 100k KEEP. Over the following\n/// 2 years, the minimum stake is lowered periodically using a uniform stepwise\n/// function, eventually ending at 10k.\nlibrary MinimumStakeSchedule {\n    using SafeMath for uint256;\n\n    // 2 years in seconds (seconds per day * days in a year * years)\n    uint256 public constant schedule = 86400 * 365 * 2;\n    uint256 public constant steps = 10;\n    uint256 public constant base = 10000 * 1e18;\n\n    /// @notice Returns the current value of the minimum stake. The minimum\n    /// stake is lowered periodically over the course of 2 years since the time\n    /// of the shedule start and eventually ends at 10k KEEP.\n    function current(uint256 scheduleStart) internal view returns (uint256) {\n        if (now < scheduleStart.add(schedule)) {\n            uint256 currentStep = steps.mul(now.sub(scheduleStart)).div(schedule);\n            return base.mul(steps.sub(currentStep));\n        }\n        return base;\n    }\n}"
    },
    "solidity/contracts/libraries/staking/GrantStaking.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"../../TokenGrant.sol\";\nimport \"../../TokenStakingEscrow.sol\";\nimport \"../..//utils/BytesLib.sol\";\nimport \"../RolesLookup.sol\";\n\n/// @notice TokenStaking contract library allowing to capture the details of\n/// delegated grants and offering functions allowing to check grantee\n/// authentication for stake delegation management.\nlibrary GrantStaking {\n    using BytesLib for bytes;\n    using RolesLookup for address payable;\n\n    /// @dev Grant ID is flagged with the most significant bit set, to\n    /// distinguish the grant ID `0` from default (null) value. The flag is\n    /// toggled with bitwise XOR (`^`) which keeps all other bits intact but\n    /// flips the flag bit. The flag should be set before writing to\n    /// `operatorToGrant`, and unset after reading from `operatorToGrant`\n    /// before using the value.\n    uint256 constant GRANT_ID_FLAG = 1 << 255;\n\n    struct Storage {\n        /// @dev Do not read or write this mapping directly; please use\n        /// `hasGrantDelegated`, `setGrantForOperator`, and `getGrantForOperator`\n        /// instead.\n        mapping (address => uint256) _operatorToGrant;\n    }\n\n    /// @notice Tries to capture delegation data if the pending delegation has\n    /// been created from a grant. There are only two possibilities and they\n    /// need to be handled differently: delegation comes from the TokenGrant\n    /// contract or delegation comes from TokenStakingEscrow. In those two cases\n    /// grant ID has to be captured in a different way.\n    /// @dev In case of a delegation from the escrow, it is expected that grant\n    /// ID is passed in extraData bytes array. When the delegation comes from\n    /// the TokenGrant contract, delegation data are obtained directly from that\n    /// contract using `tryCapturingGrantId` function.\n    /// @param tokenGrant KEEP token grant contract reference.\n    /// @param escrow TokenStakingEscrow contract address.\n    /// @param from The owner of the tokens who approved them to transfer.\n    /// @param operator The operator tokens are delegated to.\n    /// @param extraData Data for stake delegation, as passed to\n    /// `receiveApproval` of `TokenStaking`.\n    function tryCapturingDelegationData(\n        Storage storage self,\n        TokenGrant tokenGrant,\n        address escrow,\n        address from,\n        address operator,\n        bytes memory extraData\n    ) public returns (bool, uint256) {\n        if (from == escrow) {\n            require(extraData.length == 92, \"Corrupted delegation data from escrow\");\n            uint256 grantId = extraData.toUint(60);\n            setGrantForOperator(self, operator, grantId);\n            return (true, grantId);\n        } else {\n            return tryCapturingGrantId(self, tokenGrant, operator);\n        }\n    }\n\n    /// @notice Checks if the delegation for the given operator has been created\n    /// from a grant defined in the passed token grant contract and if so,\n    /// captures the grant ID for that delegation.\n    /// Grant ID can be later retrieved based on the operator address and used\n    /// to authenticate grantee or to fetch the information about grant\n    /// unlocking schedule for escrow.\n    /// @param tokenGrant KEEP token grant contract reference.\n    /// @param operator The operator tokens are delegated to.\n    function tryCapturingGrantId(\n        Storage storage self,\n        TokenGrant tokenGrant,\n        address operator\n    ) internal returns (bool, uint256) {\n        (bool success, bytes memory data) = address(tokenGrant).call(\n            abi.encodeWithSignature(\"getGrantStakeDetails(address)\", operator)\n        );\n        if (success) {\n            (uint256 grantId,,address grantStakingContract) = abi.decode(\n                data, (uint256, uint256, address)\n            );\n            // Double-check if the delegation in TokenGrant has been defined\n            // for this staking contract. If not, it means it's an old\n            // delegation and the current one does not come from a grant.\n            // The scenario covered here is:\n            // - grantee delegated to operator A from a TokenGrant using another\n            //   staking contract,\n            // - someone delegates to operator A using liquid tokens and this\n            //   staking contract.\n            // Without this check, we'd consider the second delegation as coming\n            // from a grant.\n            if (address(this) != grantStakingContract) {\n                return (false, 0);\n            }\n\n            setGrantForOperator(self, operator, grantId);\n            return (true, grantId);\n        }\n\n        return (false, 0);\n    }\n\n    /// @notice Returns true if the given operator operates on stake delegated\n    /// from a grant. false is returned otherwise.\n    /// @param operator The operator to which tokens from a grant are\n    /// potentially delegated to.\n    function hasGrantDelegated(\n        Storage storage self,\n        address operator\n    ) public view returns (bool) {\n        return self._operatorToGrant[operator] != 0;\n    }\n\n    /// @notice Associates operator with the provided grant ID. It means that\n    /// the given operator delegates on stake from the grant with this ID.\n    /// @param operator The operator tokens are delegate to.\n    /// @param grantId Identifier of a grant from which the tokens are delegated\n    /// to.\n    function setGrantForOperator(\n        Storage storage self,\n        address operator,\n        uint256 grantId\n    ) public {\n        self._operatorToGrant[operator] = grantId ^ GRANT_ID_FLAG;\n    }\n\n    /// @notice Returns grant ID for the provided operator. If the operator\n    /// does not operate on stake delegated from a grant, function reverts.\n    /// @dev To avoid reverting in case the grant ID for the operator does not\n    /// exist, consider calling hasGrantDelegated before.\n    /// @param operator The operator tokens are delegate to.\n    function getGrantForOperator(\n        Storage storage self,\n        address operator\n    ) public view returns (uint256) {\n        uint256 grantId = self._operatorToGrant[operator];\n        require (grantId != 0, \"No grant for the operator\");\n        return grantId ^ GRANT_ID_FLAG;\n    }\n\n    /// @notice Returns true if msg.sender is grantee eligible to trigger stake\n    /// undelegation for this operator. Function checks both standard grantee\n    /// and managed grantee case.\n    /// @param operator The operator tokens are delegated to.\n    /// @param tokenGrant KEEP token grant contract reference.\n    function canUndelegate(\n        Storage storage self,\n        address operator,\n        TokenGrant tokenGrant\n    ) public returns (bool) {\n        // First of all, we need to see if the operator has grant delegated.\n        // If not, we don't need to bother about checking grantee or\n        // managed grantee and we just return false.\n        if (!hasGrantDelegated(self, operator)) {\n            return false;\n        }\n\n        uint256 grantId = getGrantForOperator(self, operator);\n        (,,,,uint256 revokedAt, address grantee) = tokenGrant.getGrant(grantId);\n\n        // Is msg.sender grantee of a standard grant?\n        if (msg.sender == grantee) {\n            return true;\n        }\n\n        // If not, we need to dig deeper and see if we are dealing with\n        // a grantee from a managed grant.\n        if ((msg.sender).isManagedGranteeForGrant(grantId, tokenGrant)) {\n            return true;\n        }\n\n        // There is only one possibility left - grant has been revoked and\n        // grant manager wants to take back delegated tokens.\n        if (revokedAt == 0) {\n            return false;\n        }\n        (address grantManager,,,,) = tokenGrant.getGrantUnlockingSchedule(grantId);\n        return msg.sender == grantManager;\n    }\n}"
    },
    "solidity/contracts/TokenGrant.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Burnable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport \"./libraries/grant/UnlockingSchedule.sol\";\nimport \"./utils/BytesLib.sol\";\nimport \"./utils/AddressArrayUtils.sol\";\nimport \"./TokenStaking.sol\";\nimport \"./TokenGrantStake.sol\";\nimport \"./GrantStakingPolicy.sol\";\n\n\n/// @title TokenGrant\n/// @notice A token grant contract for a specified standard ERC20Burnable token.\n/// Has additional functionality to stake delegate/undelegate token grants.\n/// Tokens are granted to the grantee via unlocking scheme and can be\n/// withdrawn gradually based on the unlocking schedule cliff and unlocking duration.\n/// Optionally grant can be revoked by the token grant manager.\ncontract TokenGrant {\n    using SafeMath for uint256;\n    using UnlockingSchedule for uint256;\n    using SafeERC20 for ERC20Burnable;\n    using BytesLib for bytes;\n    using AddressArrayUtils for address[];\n\n    event TokenGrantCreated(uint256 id);\n    event TokenGrantWithdrawn(uint256 indexed grantId, uint256 amount);\n    event TokenGrantStaked(uint256 indexed grantId, uint256 amount, address operator);\n    event TokenGrantRevoked(uint256 id);\n\n    event StakingContractAuthorized(address indexed grantManager, address stakingContract);\n\n    struct Grant {\n        address grantManager; // Token grant manager.\n        address grantee; // Address to which granted tokens are going to be withdrawn.\n        uint256 revokedAt; // Timestamp at which grant was revoked by the grant manager.\n        uint256 revokedAmount; // The number of tokens revoked from the grantee.\n        uint256 revokedWithdrawn; // The number of tokens returned to the grant creator.\n        bool revocable; // Whether grant manager can revoke the grant.\n        uint256 amount; // Amount of tokens to be granted.\n        uint256 duration; // Duration in seconds of the period in which the granted tokens will unlock.\n        uint256 start; // Timestamp at which the linear unlocking schedule will start.\n        uint256 cliff; // Timestamp before which no tokens will be unlocked.\n        uint256 withdrawn; // Amount that was withdrawn to the grantee.\n        uint256 staked; // Amount that was staked by the grantee.\n        GrantStakingPolicy stakingPolicy;\n    }\n\n    uint256 public numGrants;\n\n    ERC20Burnable public token;\n\n    // Staking contracts authorized by the given grant manager.\n    // grant manager -> staking contract -> authorized?\n    mapping(address => mapping (address => bool)) internal stakingContracts;\n\n    // Token grants.\n    mapping(uint256 => Grant) public grants;\n\n    // Token grants stakes.\n    mapping(address => TokenGrantStake) public grantStakes;\n\n    // Mapping of token grant IDs per particular address\n    // involved in a grant as a grantee or as a grant manager.\n    mapping(address => uint256[]) public grantIndices;\n\n    // Token grants balances. Sum of all granted tokens to a grantee.\n    // This includes granted tokens that are already unlocked and\n    // available to be withdrawn to the grantee\n    mapping(address => uint256) public balances;\n\n    // Mapping of operator addresses per particular grantee address.\n    mapping(address => address[]) public granteesToOperators;\n\n    /// @notice Creates a token grant contract for a provided Standard ERC20Burnable token.\n    /// @param _tokenAddress address of a token that will be linked to this contract.\n    constructor(address _tokenAddress) public {\n        require(_tokenAddress != address(0x0), \"Token address can't be zero.\");\n        token = ERC20Burnable(_tokenAddress);\n    }\n\n    /// @notice Used by grant manager to authorize staking contract with the given\n    /// address.\n    function authorizeStakingContract(address _stakingContract) public {\n        require(\n            _stakingContract != address(0x0),\n            \"Staking contract address can't be zero\"\n        );\n        stakingContracts[msg.sender][_stakingContract] = true;\n        emit StakingContractAuthorized(msg.sender, _stakingContract);\n    }\n\n    /// @notice Gets the amount of granted tokens to the specified address.\n    /// @param _owner The address to query the grants balance of.\n    /// @return An uint256 representing the grants balance owned by the passed address.\n    function balanceOf(address _owner) public view returns (uint256 balance) {\n        return balances[_owner];\n    }\n\n    /// @notice Gets the stake balance of the specified address.\n    /// @param _address The address to query the balance of.\n    /// @return An uint256 representing the amount staked by the passed address.\n    function stakeBalanceOf(address _address) public view returns (uint256 balance) {\n        for (uint i = 0; i < grantIndices[_address].length; i++) {\n            uint256 id = grantIndices[_address][i];\n            balance += grants[id].staked;\n        }\n        return balance;\n    }\n\n    /// @notice Gets grant by ID. Returns only basic grant data.\n    /// If you need unlocking schedule for the grant you must call `getGrantUnlockingSchedule()`\n    /// This is to avoid Ethereum `Stack too deep` issue described here:\n    /// https://forum.ethereum.org/discussion/2400/error-stack-too-deep-try-removing-local-variables\n    /// @param _id ID of the token grant.\n    /// @return amount The amount of tokens the grant provides.\n    /// @return withdrawn The amount of tokens that have already been withdrawn\n    ///                   from the grant.\n    /// @return staked The amount of tokens that have been staked from the grant.\n    /// @return revoked A boolean indicating whether the grant has been revoked,\n    ///                 which is to say that it is no longer unlocking.\n    /// @return grantee The grantee of grant.\n    function getGrant(uint256 _id) public view returns (\n        uint256 amount,\n        uint256 withdrawn,\n        uint256 staked,\n        uint256 revokedAmount,\n        uint256 revokedAt,\n        address grantee\n    ) {\n        return (\n            grants[_id].amount,\n            grants[_id].withdrawn,\n            grants[_id].staked,\n            grants[_id].revokedAmount,\n            grants[_id].revokedAt,\n            grants[_id].grantee\n        );\n    }\n\n    /// @notice Gets grant unlocking schedule by grant ID.\n    /// @param _id ID of the token grant.\n    /// @return grantManager The address designated as the manager of the grant,\n    ///                      which is the only address that can revoke this grant.\n    /// @return duration The duration, in seconds, during which the tokens will\n    ///                  unlocking linearly.\n    /// @return start The start time, as a timestamp comparing to `now`.\n    /// @return cliff The timestamp, before which none of the tokens in the grant\n    ///               will be unlocked, and after which a linear amount based on\n    ///               the time elapsed since the start will be unlocked.\n    /// @return policy The address of the grant's staking policy.\n    function getGrantUnlockingSchedule(\n        uint256 _id\n    ) public view returns (\n        address grantManager,\n        uint256 duration,\n        uint256 start,\n        uint256 cliff,\n        address policy\n    ) {\n        return (\n            grants[_id].grantManager,\n            grants[_id].duration,\n            grants[_id].start,\n            grants[_id].cliff,\n            address(grants[_id].stakingPolicy)\n        );\n    }\n\n    /// @notice Gets grant ids of the specified address.\n    /// @param _granteeOrGrantManager The address to query.\n    /// @return An uint256 array of grant IDs.\n    function getGrants(address _granteeOrGrantManager) public view returns (uint256[] memory) {\n        return grantIndices[_granteeOrGrantManager];\n    }\n\n    /// @notice Gets operator addresses of the specified grantee address.\n    /// @param grantee The grantee address.\n    /// @return An array of all operators for a given grantee.\n    function getGranteeOperators(address grantee) public view returns (address[] memory) {\n        return granteesToOperators[grantee];\n    }\n\n    /// @notice Gets grant stake details of the given operator.\n    /// @param operator The operator address.\n    /// @return grantId ID of the token grant.\n    /// @return amount The amount of tokens the given operator delegated.\n    /// @return stakingContract The address of staking contract.\n    function getGrantStakeDetails(address operator) public view returns (uint256 grantId, uint256 amount, address stakingContract) {\n        return grantStakes[operator].getDetails();\n    }\n\n\n    /// @notice Receives approval of token transfer and creates a token grant with a unlocking\n    /// schedule where balance withdrawn to the grantee gradually in a linear fashion until\n    /// start + duration. By then all of the balance will have unlocked.\n    /// @param _from The owner of the tokens who approved them to transfer.\n    /// @param _amount Approved amount for the transfer to create token grant.\n    /// @param _token Token contract address.\n    /// @param _extraData This byte array must have the following values ABI encoded:\n    /// grantManager (address) Address of the grant manager.\n    /// grantee (address) Address of the grantee.\n    /// duration (uint256) Duration in seconds of the unlocking period.\n    /// start (uint256) Timestamp at which unlocking will start.\n    /// cliffDuration (uint256) Duration in seconds of the cliff;\n    ///               no tokens will be unlocked until the time `start + cliff`.\n    /// revocable (bool) Whether the token grant is revocable or not (1 or 0).\n    /// stakingPolicy (address) Address of the staking policy for the grant.\n    function receiveApproval(address _from, uint256 _amount, address _token, bytes memory _extraData) public {\n        require(ERC20Burnable(_token) == token, \"Token contract must be the same one linked to this contract.\");\n        require(_amount <= token.balanceOf(_from), \"Sender must have enough amount.\");\n        (address _grantManager,\n         address _grantee,\n         uint256 _duration,\n         uint256 _start,\n         uint256 _cliffDuration,\n         bool _revocable,\n         address _stakingPolicy) = abi.decode(\n             _extraData,\n             (address, address, uint256, uint256, uint256, bool, address)\n        );\n\n        require(_grantee != address(0), \"Grantee address can't be zero.\");\n        require(\n            _cliffDuration <= _duration,\n            \"Unlocking cliff duration must be less or equal total unlocking duration.\"\n        );\n\n        require(_stakingPolicy != address(0), \"Staking policy can't be zero.\");\n\n        uint256 id = numGrants++;\n        grants[id] = Grant(\n            _grantManager,\n            _grantee,\n            0, 0, 0,\n            _revocable,\n            _amount,\n            _duration,\n            _start,\n            _start.add(_cliffDuration),\n            0, 0,\n            GrantStakingPolicy(_stakingPolicy)\n        );\n\n        // Maintain a record to make it easier to query grants by grant manager.\n        grantIndices[_from].push(id);\n\n        // Maintain a record to make it easier to query grants by grantee.\n        grantIndices[_grantee].push(id);\n\n        token.safeTransferFrom(_from, address(this), _amount);\n\n        // Maintain a record of the unlocked amount\n        balances[_grantee] = balances[_grantee].add(_amount);\n        emit TokenGrantCreated(id);\n    }\n\n    /// @notice Withdraws Token grant amount to grantee.\n    /// @dev Transfers unlocked tokens of the token grant to grantee.\n    /// @param _id Grant ID.\n    function withdraw(uint256 _id) public {\n        uint256 amount = withdrawable(_id);\n        require(amount > 0, \"Grant available to withdraw amount should be greater than zero.\");\n\n        // Update withdrawn amount.\n        grants[_id].withdrawn = grants[_id].withdrawn.add(amount);\n\n        // Update grantee grants balance.\n        balances[grants[_id].grantee] = balances[grants[_id].grantee].sub(amount);\n\n        // Transfer tokens from this contract balance to the grantee token balance.\n        token.safeTransfer(grants[_id].grantee, amount);\n\n        emit TokenGrantWithdrawn(_id, amount);\n    }\n\n    /// @notice Calculates and returns unlocked grant amount.\n    /// @dev Calculates token grant amount that has already unlocked,\n    /// including any tokens that have already been withdrawn by the grantee as well\n    /// as any tokens that are available to withdraw but have not yet been withdrawn.\n    /// @param _id Grant ID.\n    function unlockedAmount(uint256 _id) public view returns (uint256) {\n        Grant storage grant = grants[_id];\n        return (grant.revokedAt != 0)\n            // Grant revoked -> return what is remaining\n            ? grant.amount.sub(grant.revokedAmount)\n            // Not revoked -> calculate the unlocked amount normally\n            : now.getUnlockedAmount(\n                grant.amount,\n                grant.duration,\n                grant.start,\n                grant.cliff\n            );\n    }\n\n    /// @notice Calculates withdrawable granted amount.\n    /// @dev Calculates the amount that has already unlocked but hasn't been withdrawn yet.\n    /// @param _id Grant ID.\n    function withdrawable(uint256 _id) public view returns (uint256) {\n        uint256 unlocked = unlockedAmount(_id);\n        uint256 withdrawn = grants[_id].withdrawn;\n        uint256 staked = grants[_id].staked;\n\n        if (withdrawn.add(staked) >= unlocked) {\n            return 0;\n        } else {\n            return unlocked.sub(withdrawn).sub(staked);\n        }\n    }\n\n    /// @notice Allows the grant manager to revoke the grant.\n    /// @dev Granted tokens that are already unlocked (releasable amount)\n    /// remain in the grant so grantee can still withdraw them\n    /// the rest are revoked and withdrawable by token grant manager.\n    /// @param _id Grant ID.\n    function revoke(uint256 _id) public {\n        require(grants[_id].grantManager == msg.sender, \"Only grant manager can revoke.\");\n        require(grants[_id].revocable, \"Grant must be revocable in the first place.\");\n        require(grants[_id].revokedAt == 0, \"Grant must not be already revoked.\");\n\n        uint256 unlockedAmount = unlockedAmount(_id);\n        uint256 revokedAmount = grants[_id].amount.sub(unlockedAmount);\n        grants[_id].revokedAt = now;\n        grants[_id].revokedAmount = revokedAmount;\n\n        // Update grantee's grants balance.\n        balances[grants[_id].grantee] = balances[grants[_id].grantee].sub(revokedAmount);\n        emit TokenGrantRevoked(_id);\n    }\n\n    /// @notice Allows the grant manager to withdraw revoked tokens.\n    /// @dev Will withdraw as many of the revoked tokens as possible\n    /// without pushing the grant contract into a token deficit.\n    /// If the grantee has staked more tokens than the unlocked amount,\n    /// those tokens will remain in the grant until undelegated and returned,\n    /// after which they can be withdrawn by calling `withdrawRevoked` again.\n    /// @param _id Grant ID.\n    function withdrawRevoked(uint256 _id) public {\n        Grant storage grant = grants[_id];\n        require(\n            grant.grantManager == msg.sender,\n            \"Only grant manager can withdraw revoked tokens.\"\n        );\n        uint256 revoked = grant.revokedAmount;\n        uint256 revokedWithdrawn = grant.revokedWithdrawn;\n        require(revokedWithdrawn < revoked, \"All revoked tokens withdrawn.\");\n\n        uint256 revokedRemaining = revoked.sub(revokedWithdrawn);\n\n        uint256 totalAmount = grant.amount;\n        uint256 staked = grant.staked;\n        uint256 granteeWithdrawn = grant.withdrawn;\n        uint256 remainingPresentInGrant =\n            totalAmount.sub(staked).sub(revokedWithdrawn).sub(granteeWithdrawn);\n\n        require(remainingPresentInGrant > 0, \"No revoked tokens withdrawable.\");\n\n        uint256 amountToWithdraw = remainingPresentInGrant < revokedRemaining\n            ? remainingPresentInGrant\n            : revokedRemaining;\n        token.safeTransfer(msg.sender, amountToWithdraw);\n        grant.revokedWithdrawn += amountToWithdraw;\n    }\n\n    /// @notice Stake token grant.\n    /// @dev Stakable token grant amount is determined\n    /// by the grant's staking policy.\n    /// @param _id Grant Id.\n    /// @param _stakingContract Address of the staking contract.\n    /// @param _amount Amount to stake.\n    /// @param _extraData Data for stake delegation. This byte array must have\n    /// the following values concatenated:\n    /// - Beneficiary address (20 bytes)\n    /// - Operator address (20 bytes)\n    /// - Authorizer address (20 bytes)\n    function stake(uint256 _id, address _stakingContract, uint256 _amount, bytes memory _extraData) public {\n        require(grants[_id].grantee == msg.sender, \"Only grantee of the grant can stake it.\");\n        require(grants[_id].revokedAt == 0, \"Revoked grant can not be staked\");\n        require(\n            stakingContracts[grants[_id].grantManager][_stakingContract],\n            \"Provided staking contract is not authorized.\"\n        );\n\n        // Expecting 60 bytes _extraData for stake delegation.\n        require(_extraData.length == 60, \"Stake delegation data must be provided.\");\n        address operator = _extraData.toAddress(20);\n\n        // Calculate available amount. Amount of unlocked tokens minus what user already withdrawn and staked.\n        require(_amount <= availableToStake(_id), \"Must have available granted amount to stake.\");\n\n        // Keep staking record.\n        TokenGrantStake grantStake = new TokenGrantStake(\n            address(token),\n            _id,\n            _stakingContract\n        );\n        grantStakes[operator] = grantStake;\n        granteesToOperators[grants[_id].grantee].push(operator);\n        grants[_id].staked += _amount;\n\n        token.transfer(address(grantStake), _amount);\n\n        // Staking contract expects 40 bytes _extraData for stake delegation.\n        // 20 bytes beneficiary's address + 20 bytes operator's address.\n        grantStake.stake(_amount, _extraData);\n        emit TokenGrantStaked(_id, _amount, operator);\n    }\n\n    ///  @notice Returns the amount of tokens available for staking from the grant.\n    /// The stakeable amount is determined by the staking policy of the grant.\n    /// If the grantee has withdrawn some tokens\n    /// or the policy returns an erroneously high value,\n    /// the stakeable amount is limited to the number of tokens remaining.\n    /// @param _grantId Identifier of the grant\n    function availableToStake(uint256 _grantId) public view returns (uint256) {\n        Grant storage grant = grants[_grantId];\n        // Revoked grants cannot be staked.\n        // If the grant isn't revoked, the number of revoked tokens is 0.\n        if (grant.revokedAt != 0) { return 0; }\n        uint256 amount = grant.amount;\n        uint256 withdrawn = grant.withdrawn;\n        uint256 remaining = amount.sub(withdrawn);\n        uint256 stakeable = grant.stakingPolicy.getStakeableAmount(\n            now,\n            amount,\n            grant.duration,\n            grant.start,\n            grant.cliff,\n            withdrawn\n        );\n        // Clamp the stakeable amount to what is left in the grant\n        // in the case of a malfunctioning staking policy.\n        if (stakeable > remaining) {\n            stakeable = remaining;\n        }\n\n        return stakeable.sub(grant.staked);\n    }\n\n    /// @notice Cancels delegation within the operator initialization period\n    /// without being subjected to the stake lockup for the undelegation period.\n    /// This can be used to undo mistaken delegation to the wrong operator address.\n    /// @param _operator Address of the stake operator.\n    function cancelStake(address _operator) public {\n        TokenGrantStake grantStake = grantStakes[_operator];\n        uint256 grantId = grantStake.getGrantId();\n        require(\n            msg.sender == _operator || msg.sender == grants[grantId].grantee,\n            \"Only operator or grantee can cancel the delegation.\"\n        );\n\n        uint256 returned = grantStake.cancelStake();\n        grants[grantId].staked = grants[grantId].staked.sub(returned);\n    }\n\n    /// @notice Undelegate the token grant.\n    /// @param _operator Operator of the stake.\n    function undelegate(address _operator) public {\n        TokenGrantStake grantStake = grantStakes[_operator];\n        uint256 grantId = grantStake.getGrantId();\n        require(\n            msg.sender == _operator || msg.sender == grants[grantId].grantee,\n            \"Only operator or grantee can undelegate.\"\n        );\n\n        grantStake.undelegate();\n    }\n\n    /// @notice Force cancellation of a revoked grant's stake.\n    /// Can be used by the grant manager\n    /// to immediately withdraw tokens back into the grant,\n    /// from an operator still within the initialization period.\n    /// These tokens can then be withdrawn\n    /// if some revoked tokens haven't been withdrawn yet.\n    function cancelRevokedStake(address _operator) public {\n        TokenGrantStake grantStake = grantStakes[_operator];\n        uint256 grantId = grantStake.getGrantId();\n        require(\n            grants[grantId].revokedAt != 0,\n            \"Grant must be revoked\"\n        );\n        require(\n            msg.sender == grants[grantId].grantManager,\n            \"Only grant manager can force cancellation of revoked grant stake.\"\n        );\n\n        uint256 returned = grantStake.cancelStake();\n        grants[grantId].staked = grants[grantId].staked.sub(returned);\n    }\n\n    /// @notice Force undelegation of a revoked grant's stake.\n    /// @dev Can be called by the grant manager once the grant is revoked.\n    /// Has to be done this way, instead of undelegating all operators when the\n    /// grant is revoked, because the latter method is vulnerable to DoS via\n    /// out-of-gas.\n    function undelegateRevoked(address _operator) public {\n        TokenGrantStake grantStake = grantStakes[_operator];\n        uint256 grantId = grantStake.getGrantId();\n        require(\n            grants[grantId].revokedAt != 0,\n            \"Grant must be revoked\"\n        );\n        require(\n            msg.sender == grants[grantId].grantManager,\n            \"Only grant manager can force undelegation of revoked grant stake\"\n        );\n\n        grantStake.undelegate();\n    }\n\n    /// @notice Recover stake of the token grant.\n    /// Recovers the tokens correctly\n    /// even if they were earlier recovered directly in the staking contract.\n    /// @param _operator Operator of the stake.\n    function recoverStake(address _operator) public {\n        TokenGrantStake grantStake = grantStakes[_operator];\n        uint256 returned = grantStake.recoverStake();\n        uint256 grantId = grantStake.getGrantId();\n        grants[grantId].staked = grants[grantId].staked.sub(returned);\n\n        delete grantStakes[_operator];\n    }\n}\n"
    },
    "solidity/contracts/libraries/staking/Locks.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\nimport { AuthorityVerifier } from \"../../Authorizations.sol\";\nimport \"./LockUtils.sol\";\n\nlibrary Locks {\n    using SafeMath for uint256;\n    using LockUtils for LockUtils.LockSet;\n\n    event StakeLocked(address indexed operator, address lockCreator, uint256 until);\n    event LockReleased(address indexed operator, address lockCreator);\n    event ExpiredLockReleased(address indexed operator, address lockCreator);\n\n    uint256 public constant maximumLockDuration = 86400 * 200; // 200 days in seconds\n\n    struct Storage {\n        // Locks placed on the operator.\n        // `operatorLocks[operator]` returns all locks placed on the operator.\n        // Each authorized operator contract can place one lock on an operator.\n        mapping(address => LockUtils.LockSet) operatorLocks;\n    }\n\n    function lockStake(\n        Storage storage self,\n        address operator,\n        uint256 duration\n    ) public {\n        require(duration <= maximumLockDuration, \"Lock duration too long\");\n        self.operatorLocks[operator].setLock(\n            msg.sender,\n            uint96(block.timestamp.add(duration))\n        );\n        emit StakeLocked(operator, msg.sender, block.timestamp.add(duration));\n    }\n\n    function releaseLock(\n        Storage storage self,\n        address operator\n    ) public {\n        self.operatorLocks[operator].releaseLock(msg.sender);\n        emit LockReleased(operator, msg.sender);\n    }\n\n    function releaseExpiredLock(\n        Storage storage self,\n        address operator,\n        address operatorContract,\n        address authorityVerifier\n    ) public {\n        LockUtils.LockSet storage locks = self.operatorLocks[operator];\n\n        require(\n            locks.contains(operatorContract),\n            \"No matching lock present\"\n        );\n\n        bool expired = block.timestamp >= locks.getLockTime(operatorContract);\n        bool disabled = !AuthorityVerifier(authorityVerifier)\n            .isApprovedOperatorContract(operatorContract);\n\n        require(\n            expired || disabled,\n            \"Lock still active and valid\"\n        );\n\n        locks.releaseLock(operatorContract);\n\n        emit ExpiredLockReleased(operator, operatorContract);\n    }\n\n    /// @dev AuthorityVerifier is a trusted implementation and not a third-party,\n    /// external contract. AuthorityVerifier never reverts on the check and\n    /// has a reasonable gas consumption.\n    function isStakeLocked(\n        Storage storage self,\n        address operator,\n        address authorityVerifier\n    ) public view returns (bool) {\n        LockUtils.Lock[] storage _locks = self.operatorLocks[operator].locks;\n        LockUtils.Lock memory lock;\n        for (uint i = 0; i < _locks.length; i++) {\n            lock = _locks[i];\n            if (block.timestamp < lock.expiresAt) {\n                if (\n                    AuthorityVerifier(authorityVerifier)\n                        .isApprovedOperatorContract(lock.creator)\n                ) {\n                    return true;\n                }\n            }\n        }\n        return false;\n    }\n\n    function isStakeReleased(\n        Storage storage self,\n        address operator,\n        address operatorContract\n    ) public view returns (bool) {\n        LockUtils.LockSet storage locks = self.operatorLocks[operator];\n        // `getLockTime` returns 0 if the lock doesn't exist,\n        // thus we don't need to check for its presence separately.\n        return block.timestamp >= locks.getLockTime(operatorContract);\n    }\n\n    function getLocks(\n        Storage storage self,\n        address operator\n    ) public view returns (address[] memory creators, uint256[] memory expirations) {\n        uint256 lockCount = self.operatorLocks[operator].locks.length;\n        creators = new address[](lockCount);\n        expirations = new uint256[](lockCount);\n        LockUtils.Lock memory lock;\n        for (uint i = 0; i < lockCount; i++) {\n            lock = self.operatorLocks[operator].locks[i];\n            creators[i] = lock.creator;\n            expirations[i] = lock.expiresAt;\n        }\n    }\n} \n"
    },
    "solidity/contracts/libraries/staking/TopUps.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\nimport \"../../TokenStakingEscrow.sol\";\nimport \"../../utils/OperatorParams.sol\";\n\n\n/// @notice TokenStaking contract library allowing to perform two-step stake\n/// top-ups for existing delegations.\n/// Top-up is a two-step process: it is initiated with a declared top-up value\n/// and after waiting for at least the initialization period it can be\n/// committed.\nlibrary TopUps {\n    using SafeMath for uint256;\n    using OperatorParams for uint256;\n\n    event TopUpInitiated(address indexed operator, uint256 topUp);\n    event TopUpCompleted(address indexed operator, uint256 newAmount);\n\n    struct TopUp {\n        uint256 amount;\n        uint256 createdAt;\n    }\n\n    struct Storage {\n        // operator -> TopUp\n        mapping(address => TopUp) topUps;\n    }\n\n    /// @notice Performs top-up in one step when stake is not yet initialized by\n    /// adding the top-up amount to the stake and resetting stake initialization\n    /// time counter.\n    /// @dev This function should be called only for not yet initialized stake.\n    /// @param value Top-up value, the number of tokens added to the stake.\n    /// @param operator Operator The operator with existing delegation to which\n    /// the tokens should be added to.\n    /// @param operatorParams Parameters of that operator, as stored in the\n    /// staking contract.\n    /// @param escrow Reference to TokenStakingEscrow contract.\n    /// @return New value of parameters. It should be updated for the operator\n    /// in the staking contract.\n    function instantComplete(\n        Storage storage self,\n        uint256 value,\n        address operator,\n        uint256 operatorParams,\n        TokenStakingEscrow escrow\n    ) public returns (uint256 newParams) {\n        // Stake is not yet initialized so we don't need to check if the\n        // operator is not undelegating - initializing and undelegating at the\n        // same time is not possible. We do however, need to check whether the\n        // operator has not canceled its previous stake for that operator,\n        // depositing the stake it in the escrow. We do not want to allow\n        // resurrecting operators with cancelled stake by top-ups.\n        require(\n            !escrow.hasDeposit(operator),\n            \"Stake for the operator already deposited in the escrow\"\n        );\n        require(value > 0, \"Top-up value must be greater than zero\");\n\n        uint256 newAmount = operatorParams.getAmount().add(value);\n        newParams = operatorParams.setAmountAndCreationTimestamp(\n            newAmount,\n            block.timestamp\n        );\n\n        emit TopUpCompleted(operator, newAmount);\n    }\n\n    /// @notice Initiates top-up of the given value for tokens delegated to\n    /// the provided operator. If there is an existing top-up still\n    /// initializing, top-up values are summed up and initialization period\n    /// is set to the current block timestamp.\n    /// @dev This function should be called only for active operators with\n    /// initialized stake.\n    /// @param value Top-up value, the number of tokens added to the stake.\n    /// @param operator Operator The operator with existing delegation to which\n    /// the tokens should be added to.\n    /// @param operatorParams Parameters of that operator, as stored in the\n    /// staking contract.\n    /// @param escrow Reference to TokenStakingEscrow contract.\n    function initiate(\n        Storage storage self,\n        uint256 value,\n        address operator,\n        uint256 operatorParams,\n        TokenStakingEscrow escrow\n    ) public {\n        // Stake is initialized, the operator is still active so we need\n        // to check if it's not undelegating.\n        require(!isUndelegating(operatorParams), \"Stake undelegated\");\n        // We also need to check if the stake for the operator is not already\n        // in the escrow because it's been previously cancelled.\n        require(\n            !escrow.hasDeposit(operator),\n            \"Stake for the operator already deposited in the escrow\"\n        );\n        require(value > 0, \"Top-up value must be greater than zero\");\n\n        TopUp memory awaiting = self.topUps[operator];\n        self.topUps[operator] = TopUp(awaiting.amount.add(value), now);\n        emit TopUpInitiated(operator, value);\n    }\n\n    /// @notice Commits the top-up if it passed the initialization period.\n    /// Tokens are added to the stake once the top-up is committed.\n    /// @param operator Operator The operator with a pending stake top-up.\n    /// @param initializationPeriod Stake initialization period.\n    function commit(\n        Storage storage self,\n        address operator,\n        uint256 operatorParams,\n        uint256 initializationPeriod\n    ) public returns (uint256 newParams) {\n        TopUp memory topUp = self.topUps[operator];\n        require(topUp.amount > 0, \"No top up to commit\");\n        require(\n            now > topUp.createdAt.add(initializationPeriod),\n            \"Stake is initializing\"\n        );\n\n        uint256 newAmount = operatorParams.getAmount().add(topUp.amount);\n        newParams = operatorParams.setAmount(newAmount);\n\n        delete self.topUps[operator];\n        emit TopUpCompleted(operator, newAmount);\n    }\n\n    /// @notice Cancels pending, initiating top-up. If there is no initiating\n    /// top-up for the operator, function does nothing. This function should be\n    /// used when the stake is recovered to return tokens from a pending,\n    /// initiating top-up.\n    /// @param operator Operator The operator from which the stake is recovered.\n    function cancel(\n        Storage storage self,\n        address operator\n    ) public returns (uint256) {\n        TopUp memory topUp = self.topUps[operator];\n        if (topUp.amount == 0) {\n            return 0;\n        }\n\n        delete self.topUps[operator];\n        return topUp.amount;\n    }\n\n    /// @notice Returns true if the given operatorParams indicate that the\n    /// operator is undelegating its stake or that it completed stake\n    /// undelegation.\n    /// @param operatorParams Parameters of the operator, as stored in the\n    /// staking contract.\n    function isUndelegating(uint256 operatorParams)\n        internal view returns (bool) {\n        uint256 undelegatedAt = operatorParams.getUndelegationTimestamp();\n        return (undelegatedAt != 0) && (block.timestamp > undelegatedAt);\n    }\n}\n"
    },
    "solidity/contracts/utils/PercentUtils.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\nlibrary PercentUtils {\n    using SafeMath for uint256;\n\n    // Return `b`% of `a`\n    // 200.percent(40) == 80\n    // Commutative, works both ways\n    function percent(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a.mul(b).div(100);\n    }\n\n    // Return `a` as percentage of `b`:\n    // 80.asPercentOf(200) == 40\n    function asPercentOf(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a.mul(100).div(b);\n    }\n}\n"
    },
    "solidity/contracts/utils/BytesLib.sol": {
      "content": "pragma solidity 0.5.17;\n\n/*\nVerison pulled from https://github.com/summa-tx/bitcoin-spv/blob/2535e4edaeaac4b2b095903fce684ae1c05761bc/solidity/contracts/BytesLib.sol\n*/\n\n/*\nhttps://github.com/GNSPS/solidity-bytes-utils/\nThis is free and unencumbered software released into the public domain.\nAnyone is free to copy, modify, publish, use, compile, sell, or\ndistribute this software, either in source code form or as a compiled\nbinary, for any purpose, commercial or non-commercial, and by any\nmeans.\nIn jurisdictions that recognize copyright laws, the author or authors\nof this software dedicate any and all copyright interest in the\nsoftware to the public domain. We make this dedication for the benefit\nof the public at large and to the detriment of our heirs and\nsuccessors. We intend this dedication to be an overt act of\nrelinquishment in perpetuity of all present and future rights to this\nsoftware under copyright law.\nTHE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND,\nEXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF\nMERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.\nIN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR\nOTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,\nARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR\nOTHER DEALINGS IN THE SOFTWARE.\nFor more information, please refer to <https://unlicense.org>\n*/\n\n\n/** @title BytesLib **/\n/** @author https://github.com/GNSPS **/\n\nlibrary BytesLib {\n    function concat(bytes memory _preBytes, bytes memory _postBytes) internal pure returns (bytes memory) {\n        bytes memory tempBytes;\n\n        assembly {\n            // Get a location of some free memory and store it in tempBytes as\n            // Solidity does for memory variables.\n            tempBytes := mload(0x40)\n\n            // Store the length of the first bytes array at the beginning of\n            // the memory for tempBytes.\n            let length := mload(_preBytes)\n            mstore(tempBytes, length)\n\n            // Maintain a memory counter for the current write location in the\n            // temp bytes array by adding the 32 bytes for the array length to\n            // the starting location.\n            let mc := add(tempBytes, 0x20)\n            // Stop copying when the memory counter reaches the length of the\n            // first bytes array.\n            let end := add(mc, length)\n\n            for {\n                // Initialize a copy counter to the start of the _preBytes data,\n                // 32 bytes into its memory.\n                let cc := add(_preBytes, 0x20)\n            } lt(mc, end) {\n                // Increase both counters by 32 bytes each iteration.\n                mc := add(mc, 0x20)\n                cc := add(cc, 0x20)\n            } {\n                // Write the _preBytes data into the tempBytes memory 32 bytes\n                // at a time.\n                mstore(mc, mload(cc))\n            }\n\n            // Add the length of _postBytes to the current length of tempBytes\n            // and store it as the new length in the first 32 bytes of the\n            // tempBytes memory.\n            length := mload(_postBytes)\n            mstore(tempBytes, add(length, mload(tempBytes)))\n\n            // Move the memory counter back from a multiple of 0x20 to the\n            // actual end of the _preBytes data.\n            mc := end\n            // Stop copying when the memory counter reaches the new combined\n            // length of the arrays.\n            end := add(mc, length)\n\n            for {\n                let cc := add(_postBytes, 0x20)\n            } lt(mc, end) {\n                mc := add(mc, 0x20)\n                cc := add(cc, 0x20)\n            } {\n                mstore(mc, mload(cc))\n            }\n\n            // Update the free-memory pointer by padding our last write location\n            // to 32 bytes: add 31 bytes to the end of tempBytes to move to the\n            // next 32 byte block, then round down to the nearest multiple of\n            // 32. If the sum of the length of the two arrays is zero then add\n            // one before rounding down to leave a blank 32 bytes (the length block with 0).\n            mstore(0x40, and(\n                add(add(end, iszero(add(length, mload(_preBytes)))), 31),\n                not(31) // Round down to the nearest 32 bytes.\n            ))\n        }\n\n        return tempBytes;\n    }\n\n    function concatStorage(bytes storage _preBytes, bytes memory _postBytes) internal {\n        assembly {\n            // Read the first 32 bytes of _preBytes storage, which is the length\n            // of the array. (We don't need to use the offset into the slot\n            // because arrays use the entire slot.)\n            let fslot := sload(_preBytes_slot)\n            // Arrays of 31 bytes or less have an even value in their slot,\n            // while longer arrays have an odd value. The actual length is\n            // the slot divided by two for odd values, and the lowest order\n            // byte divided by two for even values.\n            // If the slot is even, bitwise and the slot with 255 and divide by\n            // two to get the length. If the slot is odd, bitwise and the slot\n            // with -1 and divide by two.\n            let slength := div(and(fslot, sub(mul(0x100, iszero(and(fslot, 1))), 1)), 2)\n            let mlength := mload(_postBytes)\n            let newlength := add(slength, mlength)\n            // slength can contain both the length and contents of the array\n            // if length < 32 bytes so let's prepare for that\n            // v. http://solidity.readthedocs.io/en/latest/miscellaneous.html#layout-of-state-variables-in-storage\n            switch add(lt(slength, 32), lt(newlength, 32))\n            case 2 {\n                // Since the new array still fits in the slot, we just need to\n                // update the contents of the slot.\n                // uint256(bytes_storage) = uint256(bytes_storage) + uint256(bytes_memory) + new_length\n                sstore(\n                    _preBytes_slot,\n                    // all the modifications to the slot are inside this\n                    // next block\n                    add(\n                        // we can just add to the slot contents because the\n                        // bytes we want to change are the LSBs\n                        fslot,\n                        add(\n                            mul(\n                                div(\n                                    // load the bytes from memory\n                                    mload(add(_postBytes, 0x20)),\n                                    // zero all bytes to the right\n                                    exp(0x100, sub(32, mlength))\n                        ),\n                        // and now shift left the number of bytes to\n                        // leave space for the length in the slot\n                        exp(0x100, sub(32, newlength))\n                        ),\n                        // increase length by the double of the memory\n                        // bytes length\n                        mul(mlength, 2)\n                        )\n                    )\n                )\n            }\n            case 1 {\n                // The stored value fits in the slot, but the combined value\n                // will exceed it.\n                // get the keccak hash to get the contents of the array\n                mstore(0x0, _preBytes_slot)\n                let sc := add(keccak256(0x0, 0x20), div(slength, 32))\n\n                // save new length\n                sstore(_preBytes_slot, add(mul(newlength, 2), 1))\n\n                // The contents of the _postBytes array start 32 bytes into\n                // the structure. Our first read should obtain the `submod`\n                // bytes that can fit into the unused space in the last word\n                // of the stored array. To get this, we read 32 bytes starting\n                // from `submod`, so the data we read overlaps with the array\n                // contents by `submod` bytes. Masking the lowest-order\n                // `submod` bytes allows us to add that value directly to the\n                // stored value.\n\n                let submod := sub(32, slength)\n                let mc := add(_postBytes, submod)\n                let end := add(_postBytes, mlength)\n                let mask := sub(exp(0x100, submod), 1)\n\n                sstore(\n                    sc,\n                    add(\n                        and(\n                            fslot,\n                            0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff00\n                    ),\n                    and(mload(mc), mask)\n                    )\n                )\n\n                for {\n                    mc := add(mc, 0x20)\n                    sc := add(sc, 1)\n                } lt(mc, end) {\n                    sc := add(sc, 1)\n                    mc := add(mc, 0x20)\n                } {\n                    sstore(sc, mload(mc))\n                }\n\n                mask := exp(0x100, sub(mc, end))\n\n                sstore(sc, mul(div(mload(mc), mask), mask))\n            }\n            default {\n                // get the keccak hash to get the contents of the array\n                mstore(0x0, _preBytes_slot)\n                // Start copying to the last used word of the stored array.\n                let sc := add(keccak256(0x0, 0x20), div(slength, 32))\n\n                // save new length\n                sstore(_preBytes_slot, add(mul(newlength, 2), 1))\n\n                // Copy over the first `submod` bytes of the new data as in\n                // case 1 above.\n                let slengthmod := mod(slength, 32)\n                let mlengthmod := mod(mlength, 32)\n                let submod := sub(32, slengthmod)\n                let mc := add(_postBytes, submod)\n                let end := add(_postBytes, mlength)\n                let mask := sub(exp(0x100, submod), 1)\n\n                sstore(sc, add(sload(sc), and(mload(mc), mask)))\n\n                for {\n                    sc := add(sc, 1)\n                    mc := add(mc, 0x20)\n                } lt(mc, end) {\n                    sc := add(sc, 1)\n                    mc := add(mc, 0x20)\n                } {\n                    sstore(sc, mload(mc))\n                }\n\n                mask := exp(0x100, sub(mc, end))\n\n                sstore(sc, mul(div(mload(mc), mask), mask))\n            }\n        }\n    }\n\n    function slice(bytes memory _bytes, uint _start, uint _length) internal  pure returns (bytes memory res) {\n        uint _end = _start + _length;\n        require(_end > _start && _bytes.length >= _end, \"Slice out of bounds\");\n\n        assembly {\n            // Alloc bytes array with additional 32 bytes afterspace and assign it's size\n            res := mload(0x40)\n            mstore(0x40, add(add(res, 64), _length))\n            mstore(res, _length)\n\n            // Compute distance between source and destination pointers\n            let diff := sub(res, add(_bytes, _start))\n\n            for {\n                let src := add(add(_bytes, 32), _start)\n                let end := add(src, _length)\n            } lt(src, end) {\n                src := add(src, 32)\n            } {\n                mstore(add(src, diff), mload(src))\n            }\n        }\n    }\n\n    function toAddress(bytes memory _bytes, uint _start) internal  pure returns (address) {\n        uint _totalLen = _start + 20;\n        require(_totalLen > _start && _bytes.length >= _totalLen, \"Address conversion out of bounds.\");\n        address tempAddress;\n\n        assembly {\n            tempAddress := div(mload(add(add(_bytes, 0x20), _start)), 0x1000000000000000000000000)\n        }\n\n        return tempAddress;\n    }\n\n    function toUint8(bytes memory _bytes, uint _start) internal  pure returns (uint8) {\n        require(_bytes.length >= (_start + 1), \"Uint8 conversion out of bounds.\");\n        uint8 tempUint;\n\n        assembly {\n            tempUint := mload(add(add(_bytes, 0x1), _start))\n        }\n\n        return tempUint;\n    }\n\n    function toUint(bytes memory _bytes, uint _start) internal  pure returns (uint256) {\n        uint _totalLen = _start + 32;\n        require(_totalLen > _start && _bytes.length >= _totalLen, \"Uint conversion out of bounds.\");\n        uint256 tempUint;\n\n        assembly {\n            tempUint := mload(add(add(_bytes, 0x20), _start))\n        }\n\n        return tempUint;\n    }\n\n    function equal(bytes memory _preBytes, bytes memory _postBytes) internal pure returns (bool) {\n        bool success = true;\n\n        assembly {\n            let length := mload(_preBytes)\n\n            // if lengths don't match the arrays are not equal\n            switch eq(length, mload(_postBytes))\n            case 1 {\n                // cb is a circuit breaker in the for loop since there's\n                //  no said feature for inline assembly loops\n                // cb = 1 - don't breaker\n                // cb = 0 - break\n                let cb := 1\n\n                let mc := add(_preBytes, 0x20)\n                let end := add(mc, length)\n\n                for {\n                    let cc := add(_postBytes, 0x20)\n                    // the next line is the loop condition:\n                    // while(uint(mc < end) + cb == 2)\n                } eq(add(lt(mc, end), cb), 2) {\n                    mc := add(mc, 0x20)\n                    cc := add(cc, 0x20)\n                } {\n                    // if any of these checks fails then arrays are not equal\n                    if iszero(eq(mload(mc), mload(cc))) {\n                        // unsuccess:\n                        success := 0\n                        cb := 0\n                    }\n                }\n            }\n            default {\n                // unsuccess:\n                success := 0\n            }\n        }\n\n        return success;\n    }\n\n    function equalStorage(bytes storage _preBytes, bytes memory _postBytes) internal view returns (bool) {\n        bool success = true;\n\n        assembly {\n            // we know _preBytes_offset is 0\n            let fslot := sload(_preBytes_slot)\n            // Decode the length of the stored array like in concatStorage().\n            let slength := div(and(fslot, sub(mul(0x100, iszero(and(fslot, 1))), 1)), 2)\n            let mlength := mload(_postBytes)\n\n            // if lengths don't match the arrays are not equal\n            switch eq(slength, mlength)\n            case 1 {\n                // slength can contain both the length and contents of the array\n                // if length < 32 bytes so let's prepare for that\n                // v. http://solidity.readthedocs.io/en/latest/miscellaneous.html#layout-of-state-variables-in-storage\n                if iszero(iszero(slength)) {\n                    switch lt(slength, 32)\n                    case 1 {\n                        // blank the last byte which is the length\n                        fslot := mul(div(fslot, 0x100), 0x100)\n\n                        if iszero(eq(fslot, mload(add(_postBytes, 0x20)))) {\n                            // unsuccess:\n                            success := 0\n                        }\n                    }\n                    default {\n                        // cb is a circuit breaker in the for loop since there's\n                        //  no said feature for inline assembly loops\n                        // cb = 1 - don't breaker\n                        // cb = 0 - break\n                        let cb := 1\n\n                        // get the keccak hash to get the contents of the array\n                        mstore(0x0, _preBytes_slot)\n                        let sc := keccak256(0x0, 0x20)\n\n                        let mc := add(_postBytes, 0x20)\n                        let end := add(mc, mlength)\n\n                        // the next line is the loop condition:\n                        // while(uint(mc < end) + cb == 2)\n                        for {} eq(add(lt(mc, end), cb), 2) {\n                            sc := add(sc, 1)\n                            mc := add(mc, 0x20)\n                        } {\n                            if iszero(eq(sload(sc), mload(mc))) {\n                                // unsuccess:\n                                success := 0\n                                cb := 0\n                            }\n                        }\n                    }\n                }\n            }\n            default {\n                // unsuccess:\n                success := 0\n            }\n        }\n\n        return success;\n    }\n\n    function toBytes32(bytes memory _source) pure internal returns (bytes32 result) {\n        if (_source.length == 0) {\n            return 0x0;\n        }\n\n        assembly {\n            result := mload(add(_source, 32))\n        }\n    }\n\n    function keccak256Slice(bytes memory _bytes, uint _start, uint _length) pure internal returns (bytes32 result) {\n        uint _end = _start + _length;\n        require(_end > _start && _bytes.length >= _end, \"Slice out of bounds\");\n\n        assembly {\n            result := keccak256(add(add(_bytes, 32), _start), _length)\n        }\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/ownership/Ownable.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"../GSN/Context.sol\";\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\ncontract Ownable is Context {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor () internal {\n        _owner = _msgSender();\n        emit OwnershipTransferred(address(0), _owner);\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        require(isOwner(), \"Ownable: caller is not the owner\");\n        _;\n    }\n\n    /**\n     * @dev Returns true if the caller is the current owner.\n     */\n    function isOwner() public view returns (bool) {\n        return _msgSender() == _owner;\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions anymore. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby removing any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public onlyOwner {\n        emit OwnershipTransferred(_owner, address(0));\n        _owner = address(0);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public onlyOwner {\n        _transferOwnership(newOwner);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     */\n    function _transferOwnership(address newOwner) internal {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        emit OwnershipTransferred(_owner, newOwner);\n        _owner = newOwner;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/math/Math.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Standard math utilities missing in the Solidity language.\n */\nlibrary Math {\n    /**\n     * @dev Returns the largest of two numbers.\n     */\n    function max(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a >= b ? a : b;\n    }\n\n    /**\n     * @dev Returns the smallest of two numbers.\n     */\n    function min(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a < b ? a : b;\n    }\n\n    /**\n     * @dev Returns the average of two numbers. The result is rounded towards\n     * zero.\n     */\n    function average(uint256 a, uint256 b) internal pure returns (uint256) {\n        // (a + b) / 2 can overflow, so we distribute\n        return (a / 2) + (b / 2) + ((a % 2 + b % 2) / 2);\n    }\n}\n"
    },
    "solidity/contracts/cryptography/AltBn128.sol": {
      "content": "pragma solidity 0.5.17;\n\nimport \"../utils/ModUtils.sol\";\n\n/**\n * @title Operations on alt_bn128\n * @dev Implementations of common elliptic curve operations on Ethereum's\n * (poorly named) alt_bn128 curve. Whenever possible, use post-Byzantium\n * pre-compiled contracts to offset gas costs. Note that these pre-compiles\n * might not be available on all (eg private) chains.\n */\nlibrary AltBn128 {\n\n    using ModUtils for uint256;\n\n    // G1Point implements a point in G1 group.\n    struct G1Point {\n        uint256 x;\n        uint256 y;\n    }\n\n    // gfP2 implements a field of size p² as a quadratic extension of the base field.\n    struct gfP2 {\n        uint256 x;\n        uint256 y;\n    }\n\n    // G2Point implements a point in G2 group.\n    struct G2Point {\n        gfP2 x;\n        gfP2 y;\n    }\n\n    // p is a prime over which we form a basic field\n    // Taken from go-ethereum/crypto/bn256/cloudflare/constants.go\n    uint256 constant p = 21888242871839275222246405745257275088696311157297823662689037894645226208583;\n\n    function getP() internal pure returns (uint256) {\n        return p;\n    }\n\n    /**\n     * @dev Gets generator of G1 group.\n     * Taken from go-ethereum/crypto/bn256/cloudflare/curve.go\n     */\n    uint256 constant g1x = 1;\n    uint256 constant g1y = 2;\n    function g1() internal pure returns (G1Point memory) {\n        return G1Point(g1x, g1y);\n    }\n\n    /**\n     * @dev Gets generator of G2 group.\n     * Taken from go-ethereum/crypto/bn256/cloudflare/twist.go\n     */\n    uint256 constant g2xx = 11559732032986387107991004021392285783925812861821192530917403151452391805634;\n    uint256 constant g2xy = 10857046999023057135944570762232829481370756359578518086990519993285655852781;\n    uint256 constant g2yx = 4082367875863433681332203403145435568316851327593401208105741076214120093531;\n    uint256 constant g2yy = 8495653923123431417604973247489272438418190587263600148770280649306958101930;\n    function g2() internal pure returns (G2Point memory) {\n        return G2Point(\n            gfP2(g2xx, g2xy),\n            gfP2(g2yx, g2yy)\n        );\n    }\n\n    /**\n     * @dev Gets twist curve B constant.\n     * Taken from go-ethereum/crypto/bn256/cloudflare/twist.go\n     */\n    uint256 constant twistBx = 266929791119991161246907387137283842545076965332900288569378510910307636690;\n    uint256 constant twistBy = 19485874751759354771024239261021720505790618469301721065564631296452457478373;\n    function twistB() private pure returns (gfP2 memory) {\n        return gfP2(twistBx, twistBy);\n    }\n\n    /**\n     * @dev Gets root of the point where x and y are equal.\n     */\n    uint256 constant hexRootX = 21573744529824266246521972077326577680729363968861965890554801909984373949499;\n    uint256 constant hexRootY = 16854739155576650954933913186877292401521110422362946064090026408937773542853;\n    function hexRoot() private pure returns (gfP2 memory) {\n        return gfP2(hexRootX, hexRootY);\n    }\n\n    /**\n     * @dev g1YFromX computes a Y value for a G1 point based on an X value.\n     * This computation is simply evaluating the curve equation for Y on a\n     * given X, and allows a point on the curve to be represented by just\n     * an X value + a sign bit.\n     */\n    function g1YFromX(uint256 x)\n        internal\n        view returns(uint256)\n    {\n        return ((x.modExp(3, p) + 3) % p).modSqrt(p);\n    }\n\n    /**\n     * @dev g2YFromX computes a Y value for a G2 point based on an X value.\n     * This computation is simply evaluating the curve equation for Y on a\n     * given X, and allows a point on the curve to be represented by just\n     * an X value + a sign bit.\n     */\n    function g2YFromX(gfP2 memory _x)\n        internal\n        pure returns(gfP2 memory y)\n    {\n        (uint256 xx, uint256 xy) = _gfP2CubeAddTwistB(_x.x, _x.y);\n\n        // Using formula y = x ^ (p^2 + 15) / 32 from\n        // https://github.com/ethereum/beacon_chain/blob/master/beacon_chain/utils/bls.py\n        // (p^2 + 15) / 32 results into a big 512bit value, so breaking it to two uint256 as (a * a + b)\n        uint256 a = 3869331240733915743250440106392954448556483137451914450067252501901456824595;\n        uint256 b = 146360017852723390495514512480590656176144969185739259173561346299185050597;\n\n        (uint256 xbx, uint256 xby) = _gfP2Pow(xx, xy, b);\n        (uint256 yax, uint256 yay) = _gfP2Pow(xx, xy, a);\n        (uint256 ya2x, uint256 ya2y) = _gfP2Pow(yax, yay, a);\n        (y.x, y.y) = _gfP2Multiply(ya2x, ya2y, xbx, xby);\n\n        // Multiply y by hexRoot constant to find correct y.\n        while (!_g2X2y(xx, xy, y.x, y.y)) {\n            (y.x, y.y) = _gfP2Multiply(y.x, y.y, hexRootX, hexRootY);\n        }\n    }\n\n    /**\n     * @dev Hash a byte array message, m, and map it deterministically to a\n     * point on G1. Note that this approach was chosen for its simplicity /\n     * lower gas cost on the EVM, rather than good distribution of points on\n     * G1.\n     */\n    function g1HashToPoint(bytes memory m)\n        internal\n        view returns(G1Point memory)\n    {\n        bytes32 h = sha256(m);\n        uint256 x = uint256(h) % p;\n        uint256 y;\n\n        while (true) {\n            y = g1YFromX(x);\n            if (y > 0) {\n                return G1Point(x, y);\n            }\n            x += 1;\n        }\n    }\n\n    /**\n     * @dev Calculates whether the provided number is even or odd.\n     * @return 0x01 if y is an even number and 0x00 if it's odd.\n     */\n    function parity(uint256 value) private pure returns (byte) {\n        return bytes32(value)[31] & 0x01;\n    }\n\n    /**\n     * @dev Compress a point on G1 to a single uint256 for serialization.\n     */\n    function g1Compress(G1Point memory point)\n        internal\n        pure returns(bytes32)\n    {\n        bytes32 m = bytes32(point.x);\n\n        byte leadM = m[0] | parity(point.y) << 7;\n        uint256 mask = 0xff << 31*8;\n        m = (m & ~bytes32(mask)) | (leadM >> 0);\n\n        return m;\n    }\n\n    /**\n     * @dev Compress a point on G2 to a pair of uint256 for serialization.\n     */\n    function g2Compress(G2Point memory point)\n        internal\n        pure returns(bytes memory)\n    {\n        bytes32 m = bytes32(point.x.x);\n\n        byte leadM = m[0] | parity(point.y.x) << 7;\n        uint256 mask = 0xff << 31*8;\n        m = (m & ~bytes32(mask)) | (leadM >> 0);\n\n        return abi.encodePacked(m, bytes32(point.x.y));\n    }\n\n    /**\n     * @dev Decompress a point on G1 from a single uint256.\n     */\n    function g1Decompress(bytes32 m)\n        internal\n        view returns(G1Point memory)\n    {\n        bytes32 mX = bytes32(0);\n        byte leadX = m[0] & 0x7f;\n        uint256 mask = 0xff << 31*8;\n        mX = (m & ~bytes32(mask)) | (leadX >> 0);\n\n        uint256 x = uint256(mX);\n        uint256 y = g1YFromX(x);\n\n        if (parity(y) != (m[0] & 0x80) >> 7) {\n            y = p - y;\n        }\n\n        require(isG1PointOnCurve(G1Point(x, y)), \"Malformed bn256.G1 point.\");\n\n        return G1Point(x, y);\n    }\n\n    /**\n     * @dev Unmarshals a point on G1 from bytes in an uncompressed form.\n     */\n    function g1Unmarshal(bytes memory m) internal pure returns(G1Point memory) {\n        require(\n            m.length == 64,\n            \"Invalid G1 bytes length\"\n        );\n\n        bytes32 x;\n        bytes32 y;\n\n        /* solium-disable-next-line */\n        assembly {\n            x := mload(add(m, 0x20))\n            y := mload(add(m, 0x40))\n        }\n\n        return G1Point(uint256(x), uint256(y));\n    }\n\n    /**\n     * @dev Marshals a point on G1 to bytes form.\n     */\n    function g1Marshal(G1Point memory point) internal pure returns(bytes memory) {\n        bytes memory m = new bytes(64);\n        bytes32 x = bytes32(point.x);\n        bytes32 y = bytes32(point.y);\n\n        /* solium-disable-next-line */\n        assembly {\n            mstore(add(m, 32), x)\n            mstore(add(m, 64), y)\n        }\n\n        return m;\n    }\n\n    /**\n     * @dev Unmarshals a point on G2 from bytes in an uncompressed form.\n     */\n    function g2Unmarshal(bytes memory m) internal pure returns(G2Point memory) {\n        require(\n            m.length == 128,\n            \"Invalid G2 bytes length\"\n        );\n\n        uint256 xx;\n        uint256 xy;\n        uint256 yx;\n        uint256 yy;\n\n        /* solium-disable-next-line */\n        assembly {\n            xx := mload(add(m, 0x20))\n            xy := mload(add(m, 0x40))\n            yx := mload(add(m, 0x60))\n            yy := mload(add(m, 0x80))\n        }\n\n        return G2Point(gfP2(xx, xy), gfP2(yx,yy));\n    }\n\n    /**\n     * @dev Decompress a point on G2 from a pair of uint256.\n     */\n    function g2Decompress(bytes memory m)\n        internal\n        pure returns(G2Point memory)\n    {\n        require(\n            m.length == 64,\n            \"Invalid G2 compressed bytes length\"\n        );\n\n        bytes32 x1;\n        bytes32 x2;\n        uint256 temp;\n\n        // Extract two bytes32 from bytes array\n        /* solium-disable-next-line */\n        assembly {\n            temp := add(m, 32)\n            x1 := mload(temp)\n            temp := add(m, 64)\n            x2 := mload(temp)\n        }\n\n        bytes32 mX = bytes32(0);\n        byte leadX = x1[0] & 0x7f;\n        uint256 mask = 0xff << 31*8;\n        mX = (x1 & ~bytes32(mask)) | (leadX >> 0);\n\n        gfP2 memory x = gfP2(uint256(mX), uint256(x2));\n        gfP2 memory y = g2YFromX(x);\n\n        if (parity(y.x) != (m[0] & 0x80) >> 7) {\n            y.x = p - y.x;\n            y.y = p - y.y;\n        }\n\n        return G2Point(x, y);\n    }\n\n    /**\n     * @dev Wrap the point addition pre-compile introduced in Byzantium. Return\n     * the sum of two points on G1. Revert if the provided points aren't on the\n     * curve.\n     */\n    function g1Add(G1Point memory a, G1Point memory b)\n        internal view returns (G1Point memory c) {\n        /* solium-disable-next-line */\n        assembly {\n            let arg := mload(0x40)\n            mstore(arg, mload(a))\n            mstore(add(arg, 0x20), mload(add(a, 0x20)))\n            mstore(add(arg, 0x40), mload(b))\n            mstore(add(arg, 0x60), mload(add(b, 0x20)))\n            // 0x60 is the ECADD precompile address\n            if iszero(staticcall(not(0), 0x06, arg, 0x80, c, 0x40)) {\n                revert(0, 0)\n            }\n        }\n    }\n\n    /**\n     * @dev Return the sum of two gfP2 field elements.\n     */\n    function gfP2Add(gfP2 memory a, gfP2 memory b) internal pure returns(gfP2 memory) {\n        return gfP2(\n            addmod(a.x, b.x, p),\n            addmod(a.y, b.y, p)\n        );\n    }\n\n    /**\n     * @dev Return multiplication of two gfP2 field elements.\n     */\n    function gfP2Multiply(gfP2 memory a, gfP2 memory b) internal pure returns(gfP2 memory) {\n        return gfP2(\n            addmod(mulmod(a.x, b.y, p), mulmod(b.x, a.y, p), p),\n            addmod(mulmod(a.y, b.y, p), p - mulmod(a.x, b.x, p), p)\n        );\n    }\n\n    /**\n     * @dev Return gfP2 element to the power of the provided exponent.\n     */\n    function gfP2Pow(gfP2 memory _a, uint256 _exp) internal pure returns(gfP2 memory result) {\n        (uint256 x, uint256 y) = _gfP2Pow(_a.x, _a.y, _exp);\n        return gfP2(x, y);\n    }\n\n    function gfP2Square(gfP2 memory a) internal pure returns (gfP2 memory) {\n        return gfP2Multiply(a, a);\n    }\n\n    function gfP2Cube(gfP2 memory a) internal pure returns (gfP2 memory) {\n        return gfP2Multiply(a, gfP2Square(a));\n    }\n\n    function gfP2CubeAddTwistB(gfP2 memory a) internal pure returns (gfP2 memory) {\n        (uint256 x, uint256 y) = _gfP2CubeAddTwistB(a.x, a.y);\n        return gfP2(x, y);\n    }\n\n    /**\n     * @dev Return true if G2 point's y^2 equals x.\n     */\n    function g2X2y(gfP2 memory x, gfP2 memory y) internal pure returns(bool) {\n        gfP2 memory y2;\n        y2 = gfP2Square(y);\n\n        return (y2.x == x.x && y2.y == x.y);\n    }\n\n    /**\n     * @dev Return true if G1 point is on the curve.\n     */\n    function isG1PointOnCurve(G1Point memory point) internal view returns (bool) {\n        return point.y.modExp(2, p) == (point.x.modExp(3, p) + 3) % p;\n    }\n\n    /**\n     * @dev Return true if G2 point is on the curve.\n     */\n    function isG2PointOnCurve(G2Point memory point) internal pure returns(bool) {\n        (uint256 y2x, uint256 y2y) = _gfP2Square(point.y.x, point.y.y);\n        (uint256 x3x, uint256 x3y) = _gfP2CubeAddTwistB(point.x.x, point.x.y);\n\n        return (y2x == x3x && y2y == x3y);\n    }\n\n    /**\n     * @dev Wrap the scalar point multiplication pre-compile introduced in\n     * Byzantium. The result of a point from G1 multiplied by a scalar should\n     * match the point added to itself the same number of times. Revert if the\n     * provided point isn't on the curve.\n     */\n    function scalarMultiply(G1Point memory p_1, uint256 scalar)\n        internal view returns (G1Point memory p_2) {\n        assembly {\n            let arg := mload(0x40)\n            mstore(arg, mload(p_1))\n            mstore(add(arg, 0x20), mload(add(p_1, 0x20)))\n            mstore(add(arg, 0x40), scalar)\n            // 0x07 is the ECMUL precompile address\n            if iszero(staticcall(not(0), 0x07, arg, 0x60, p_2, 0x40)) {\n                revert(0, 0)\n            }\n        }\n    }\n\n    /**\n     * @dev Wrap the pairing check pre-compile introduced in Byzantium. Return\n     * the result of a pairing check of 2 pairs (G1 p1, G2 p2) (G1 p3, G2 p4)\n     */\n    function pairing(\n        G1Point memory p1,\n        G2Point memory p2,\n        G1Point memory p3,\n        G2Point memory p4\n    ) internal view returns (bool result) {\n        uint256 _c;\n        /* solium-disable-next-line */\n        assembly {\n            let c := mload(0x40)\n            let arg := add(c, 0x20)\n\n            mstore(arg, mload(p1))\n            mstore(add(arg, 0x20), mload(add(p1, 0x20)))\n\n            let p2x := mload(p2)\n            mstore(add(arg, 0x40), mload(p2x))\n            mstore(add(arg, 0x60), mload(add(p2x, 0x20)))\n\n            let p2y := mload(add(p2, 0x20))\n            mstore(add(arg, 0x80), mload(p2y))\n            mstore(add(arg, 0xa0), mload(add(p2y, 0x20)))\n\n            mstore(add(arg, 0xc0), mload(p3))\n            mstore(add(arg, 0xe0), mload(add(p3, 0x20)))\n\n            let p4x := mload(p4)\n            mstore(add(arg, 0x100), mload(p4x))\n            mstore(add(arg, 0x120), mload(add(p4x, 0x20)))\n\n            let p4y := mload(add(p4, 0x20))\n            mstore(add(arg, 0x140), mload(p4y))\n            mstore(add(arg, 0x160), mload(add(p4y, 0x20)))\n\n            // call(gasLimit, to, value, inputOffset, inputSize, outputOffset, outputSize)\n            if iszero(staticcall(not(0), 0x08, arg, 0x180, c, 0x20)) {\n                revert(0, 0)\n            }\n            _c := mload(c)\n        }\n        return _c != 0;\n    }\n\n    function _gfP2Add(uint256 ax, uint256 ay, uint256 bx, uint256 by)\n        private pure returns(uint256 x, uint256 y) {\n        x = addmod(ax, bx, p);\n        y = addmod(ay, by, p);\n    }\n\n    function _gfP2Multiply(uint256 ax, uint256 ay, uint256 bx, uint256 by)\n        private pure returns(uint256 x, uint256 y) {\n        x = addmod(mulmod(ax, by, p), mulmod(bx, ay, p), p);\n        y = addmod(mulmod(ay, by, p), p - mulmod(ax, bx, p), p);\n    }\n\n    function _gfP2CubeAddTwistB(uint256 ax, uint256 ay)\n        private pure returns (uint256 x, uint256 y) {\n        (uint256 a3x, uint256 a3y) = _gfP2Cube(ax, ay);\n        return _gfP2Add(a3x, a3y, twistBx, twistBy);\n    }\n\n    function _gfP2Pow(uint256 _ax, uint256 _ay, uint256 _exp)\n        private pure returns (uint256 x, uint256 y) {\n        uint256 exp = _exp;\n        x = 0;\n        y = 1;\n        uint256 ax = _ax;\n        uint256 ay = _ay;\n\n        // Reduce exp dividing by 2 gradually to 0 while computing final\n        // result only when exp is an odd number.\n        while (exp > 0) {\n            if (parity(exp) == 0x01) {\n                (x, y) = _gfP2Multiply(x, y, ax, ay);\n            }\n\n            exp = exp / 2;\n            (ax, ay) = _gfP2Multiply(ax, ay, ax, ay);\n        }\n    }\n\n    function _gfP2Square(uint256 _ax, uint256 _ay)\n        private pure returns (uint256 x, uint256 y) {\n        return _gfP2Multiply(_ax, _ay, _ax, _ay);\n    }\n\n    function _gfP2Cube(uint256 _ax, uint256 _ay)\n        private pure returns (uint256 x, uint256 y) {\n        (uint256 _bx, uint256 _by) = _gfP2Square(_ax, _ay);\n        return _gfP2Multiply(_ax, _ay, _bx, _by);\n    }\n\n    function _g2X2y(uint256 xx, uint256 xy, uint256 yx, uint256 yy)\n        private pure returns (bool) {\n        (uint256 y2x, uint256 y2y) = _gfP2Square(yx, yy);\n\n        return (y2x == xx && y2y == xy);\n    }\n}\n"
    },
    "solidity/contracts/utils/ModUtils.sol": {
      "content": "pragma solidity 0.5.17;\n\n\nlibrary ModUtils {\n\n    /**\n     * @dev Wrap the modular exponent pre-compile introduced in Byzantium.\n     * Returns base^exponent mod p.\n     */\n    function modExp(uint256 base, uint256 exponent, uint256 p)\n        internal\n        view returns(uint256 o)\n    {\n        /* solium-disable-next-line */\n        assembly {\n            // Args for the precompile: [<length_of_BASE> <length_of_EXPONENT>\n            // <length_of_MODULUS> <BASE> <EXPONENT> <MODULUS>]\n            let output := mload(0x40)\n            let args := add(output, 0x20)\n            mstore(args, 0x20)\n            mstore(add(args, 0x20), 0x20)\n            mstore(add(args, 0x40), 0x20)\n            mstore(add(args, 0x60), base)\n            mstore(add(args, 0x80), exponent)\n            mstore(add(args, 0xa0), p)\n\n            // 0x05 is the modular exponent contract address\n            if iszero(staticcall(not(0), 0x05, args, 0xc0, output, 0x20)) {\n                revert(0, 0)\n            }\n            o := mload(output)\n        }\n    }\n\n    /**\n     * @dev Calculates and returns the square root of a mod p if such a square\n     * root exists. The modulus p must be an odd prime. If a square root does\n     * not exist, function returns 0.\n     */\n    function modSqrt(uint256 a, uint256 p)\n        internal\n        view returns(uint256)\n    {\n\n        if (legendre(a, p) != 1) {\n            return 0;\n        }\n\n        if (a == 0) {\n            return 0;\n        }\n\n        if (p % 4 == 3) {\n            return modExp(a, (p + 1) / 4, p);\n        }\n\n        uint256 s = p - 1;\n        uint256 e = 0;\n\n        while (s % 2 == 0) {\n            s = s / 2;\n            e = e + 1;\n        }\n\n        // Note the smaller int- finding n with Legendre symbol or -1\n        // should be quick\n        uint256 n = 2;\n        while (legendre(n, p) != -1) {\n            n = n + 1;\n        }\n\n        uint256 x = modExp(a, (s + 1) / 2, p);\n        uint256 b = modExp(a, s, p);\n        uint256 g = modExp(n, s, p);\n        uint256 r = e;\n        uint256 gs = 0;\n        uint256 m = 0;\n        uint256 t = b;\n\n        while (true) {\n            t = b;\n            m = 0;\n\n            for (m = 0; m < r; m++) {\n                if (t == 1) {\n                    break;\n                }\n                t = modExp(t, 2, p);\n            }\n\n            if (m == 0) {\n                return x;\n            }\n\n            gs = modExp(g, uint256(2) ** (r - m - 1), p);\n            g = (gs * gs) % p;\n            x = (x * gs) % p;\n            b = (b * g) % p;\n            r = m;\n        }\n    }\n\n    /**\n     * @dev Calculates the Legendre symbol of the given a mod p.\n     * @return Returns 1 if a is a quadratic residue mod p, -1 if it is\n     * a non-quadratic residue, and 0 if a is 0.\n     */\n    function legendre(uint256 a, uint256 p)\n        internal\n        view returns(int256)\n    {\n        uint256 raised = modExp(a, (p - 1) / uint256(2), p);\n\n        if (raised == 0 || raised == 1) {\n            return int256(raised);\n        } else if (raised == p - 1) {\n            return -1;\n        }\n\n        require(false, \"Failed to calculate legendre.\");\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/cryptography/ECDSA.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.\n *\n * These functions can be used to verify that a message was signed by the holder\n * of the private keys of a given address.\n */\nlibrary ECDSA {\n    /**\n     * @dev Returns the address that signed a hashed message (`hash`) with\n     * `signature`. This address can then be used for verification purposes.\n     *\n     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:\n     * this function rejects them by requiring the `s` value to be in the lower\n     * half order, and the `v` value to be either 27 or 28.\n     *\n     * NOTE: This call _does not revert_ if the signature is invalid, or\n     * if the signer is otherwise unable to be retrieved. In those scenarios,\n     * the zero address is returned.\n     *\n     * IMPORTANT: `hash` _must_ be the result of a hash operation for the\n     * verification to be secure: it is possible to craft signatures that\n     * recover to arbitrary addresses for non-hashed data. A safe way to ensure\n     * this is by receiving a hash of the original message (which may otherwise\n     * be too long), and then calling {toEthSignedMessageHash} on it.\n     */\n    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {\n        // Check the signature length\n        if (signature.length != 65) {\n            return (address(0));\n        }\n\n        // Divide the signature in r, s and v variables\n        bytes32 r;\n        bytes32 s;\n        uint8 v;\n\n        // ecrecover takes the signature parameters, and the only way to get them\n        // currently is to use assembly.\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            r := mload(add(signature, 0x20))\n            s := mload(add(signature, 0x40))\n            v := byte(0, mload(add(signature, 0x60)))\n        }\n\n        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature\n        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines\n        // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most\n        // signatures from current libraries generate a unique signature with an s-value in the lower half order.\n        //\n        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value\n        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or\n        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept\n        // these malleable signatures as well.\n        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {\n            return address(0);\n        }\n\n        if (v != 27 && v != 28) {\n            return address(0);\n        }\n\n        // If the signature is valid (and not malleable), return the signer address\n        return ecrecover(hash, v, r, s);\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Message, created from a `hash`. This\n     * replicates the behavior of the\n     * https://github.com/ethereum/wiki/wiki/JSON-RPC#eth_sign[`eth_sign`]\n     * JSON-RPC method.\n     *\n     * See {recover}.\n     */\n    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {\n        // 32 is the length in bytes of hash,\n        // enforced by the type signature above\n        return keccak256(abi.encodePacked(\"\\x19Ethereum Signed Message:\\n32\", hash));\n    }\n}\n"
    }
  },
  "settings": {
    "metadata": {
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "abi"
        ]
      }
    }
  }
}}
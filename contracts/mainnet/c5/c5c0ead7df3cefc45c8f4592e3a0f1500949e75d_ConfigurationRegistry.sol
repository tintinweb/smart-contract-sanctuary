/**
 *Submitted for verification at Etherscan.io on 2020-07-07
*/

pragma solidity 0.6.10;


interface ConfigurationRegistryInterface {
  event ConfigurationModified(bytes32 indexed key, bytes32 value);
  function set(bytes32 key, bytes32 value) external;
  function get(bytes32 key) external view returns (bytes32 value);
  function getKey(
    string calldata stringToHash
  ) external pure returns (bytes32 key);
}


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be aplied to your functions to restrict their use to
 * the owner.
 *
 * In order to transfer ownership, a recipient must be specified, at which point
 * the specified recipient can call `acceptOwnership` and take ownership.
 */
contract TwoStepOwnable {
  address private _owner;

  address private _newPotentialOwner;

  event OwnershipTransferred(
    address indexed previousOwner,
    address indexed newOwner
  );

  /**
   * @dev Initialize contract by setting transaction submitter as initial owner.
   */
  constructor() internal {
    _owner = tx.origin;
    emit OwnershipTransferred(address(0), _owner);
  }

  /**
   * @dev Returns the address of the current owner.
   */
  function owner() public view returns (address) {
    return _owner;
  }

  /**
   * @dev Throws if called by any account other than the owner.
   */
  modifier onlyOwner() {
    require(isOwner(), "TwoStepOwnable: caller is not the owner.");
    _;
  }

  /**
   * @dev Returns true if the caller is the current owner.
   */
  function isOwner() public view returns (bool) {
    return msg.sender == _owner;
  }

  /**
   * @dev Allows a new account (`newOwner`) to accept ownership.
   * Can only be called by the current owner.
   */
  function transferOwnership(address newOwner) public onlyOwner {
    require(
      newOwner != address(0),
      "TwoStepOwnable: new potential owner is the zero address."
    );

    _newPotentialOwner = newOwner;
  }

  /**
   * @dev Cancel a transfer of ownership to a new account.
   * Can only be called by the current owner.
   */
  function cancelOwnershipTransfer() public onlyOwner {
    delete _newPotentialOwner;
  }

  /**
   * @dev Transfers ownership of the contract to the caller.
   * Can only be called by a new potential owner set by the current owner.
   */
  function acceptOwnership() public {
    require(
      msg.sender == _newPotentialOwner,
      "TwoStepOwnable: current owner must set caller as new potential owner."
    );

    delete _newPotentialOwner;

    emit OwnershipTransferred(_owner, msg.sender);

    _owner = msg.sender;
  }
}


contract ConfigurationRegistry is
  ConfigurationRegistryInterface, TwoStepOwnable {
  mapping(bytes32 => bytes32) private _values;
    
  function set(bytes32 key, bytes32 value) external override onlyOwner {
    _values[key] = value;
    emit ConfigurationModified(key, value);
  }
    
  function get(
    bytes32 key
  ) external view override returns (bytes32 value) {
    value = _values[key];
  }
  
  function getKey(
    string calldata stringToHash
  ) external pure override returns (bytes32 key) {
    key = keccak256(bytes(stringToHash));
  }
}
//SPDX-License-Identifier: Unlicense

pragma solidity ^0.7.0;

import "./interfaces/IFlashLoanStrategy.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import "@openzeppelin/contracts/math/SafeMath.sol";
import "@openzeppelin/contracts/drafts/EIP712.sol";
import "@openzeppelin/contracts/cryptography/ECDSA.sol";

contract SubscriptionProtocol is EIP712 {

    using SafeERC20 for IERC20;
    using SafeMath for uint256;

    modifier onlyActiveReceivers(address _receiver) {
        require(isActiveReceiver(_receiver), "Receiver must be active");
        _;
    }

    event ReceiverAdded(address indexed receiver, address token, uint256 commissionPercentage);
    event ReceiverUpdated(address indexed receiver, address token, uint256 commissionPercentage);
    event ReceiverRemoved(address indexed receiver);
    event SubscriptionCreated(address indexed subscriber, address indexed receiver, uint256 amount);
    event SubscriptionUpdated(address indexed subscriber, address indexed receiver, uint256 amount);
    event SubscriptionRemoved(address indexed subscriber, address indexed receiver);
    event SubscriptionsExecuted(address indexed receiver);

    struct Subscription {
        address subscriber;
        uint256 amount;
    }

    struct Receiver {
        uint256 lastExecution;
        uint256 commissionPercentage;
        uint256 maxAmountPerExecution;
        address token;
        mapping (address => uint256) subscriberIndexes;
        Subscription[] subscriptions;
    }

    bytes32 immutable public SET_RECEIVER_TYPEHASH;
    uint256 immutable public TIME_BETWEEN_EXECUTIONS;

    mapping (address => Receiver) public receivers;
    mapping (address => uint256) public nonces;

    constructor(uint256 _timeBetweenExecutions) EIP712("SubscriptionProtocol", "0") {
        TIME_BETWEEN_EXECUTIONS = _timeBetweenExecutions;
        SET_RECEIVER_TYPEHASH = keccak256(
            "SetReceiver(address receiver,address token,uint256 commissionPercentage,uint256 nonce,uint256 deadline)"
        );
    }

    /**
    * @dev Tells wether the given receiver is active or not.
    * @param _receiver The address of the receiver.
    * @return True if given receiver is active, false if not.
    */
    function isActiveReceiver(address _receiver) public view returns (bool) {
        return receivers[_receiver].token != address(0);
    }

    /**
    * @dev Gets the subscription amount from a subscriber to an active receiver.
    * @param _subscriber The address of the subscriber.
    * @param _receiver The address of the receiver.
    * @return The amount of the subscription. If subscriber is not subscribed the amount will be zero.
    */
    function getSubscriptionAmount(
        address _subscriber, address _receiver
    ) external view onlyActiveReceivers(_receiver) returns (uint256) {
        if (receivers[_receiver].subscriptions.length > 0) {
            uint256 subscriberIndex = receivers[_receiver].subscriberIndexes[_subscriber];
            return receivers[_receiver].subscriptions[subscriberIndex].subscriber == _subscriber 
                ? receivers[_receiver].subscriptions[subscriberIndex].amount
                : 0;
        }
        return 0;
    }

    /**
    * @dev Sets a new configuration for the receiver through a message signed by him.
    * @param _receiver The address of the receiver.
    * @param _token The address of the ERC-20 token to receive transfers. Set as address(0) to remove the receiver.
    * @param _commissionPercentage A percentage of the total subscriptions to give to a third party that executes
    * the subscriptions incentivizing them. Set as 0 to avoid commissions.
    * @param _deadline The expiration time until which the signature will be considered valid.
    * @param _v The signature v value.
    * @param _r The signature r value.
    * @param _s The signature s value.
    */
    function setReceiver(
        address _receiver,
        address _token,
        uint256 _commissionPercentage,
        uint256 _deadline,
        uint8 _v,
        bytes32 _r,
        bytes32 _s
    ) external {
        require(block.timestamp <= _deadline, "Signature must not be expired");
        bytes32 hashStruct = keccak256(
            abi.encode(SET_RECEIVER_TYPEHASH, _receiver, _token, _commissionPercentage, nonces[_receiver], _deadline)
        );
        require(ECDSA.recover(_hashTypedDataV4(hashStruct), _v, _r, _s) == _receiver, "Signer must match receiver");
        nonces[_receiver]++;
        bool activeReceiver = isActiveReceiver(_receiver);
        if (_token == address(0)) {
            require(activeReceiver, "Must be active to be removed");
            uint256 lastExecution = receivers[_receiver].lastExecution;
            delete receivers[_receiver];
            // To avoid attack of removing and adding again to clear lastExecution restriction.
            receivers[_receiver].lastExecution = lastExecution;
            emit ReceiverRemoved(_receiver);
        } else {
            require(_commissionPercentage <= 100, "Commission percentage must be between 0 and 100");
            if (activeReceiver) {
                if (_token != receivers[_receiver].token) {
                    delete receivers[_receiver].subscriptions;
                    receivers[_receiver].maxAmountPerExecution = 0;
                }
                emit ReceiverUpdated(_receiver, _token, _commissionPercentage);
            } else {
                emit ReceiverAdded(_receiver, _token, _commissionPercentage);
            }
            Receiver storage receiver = receivers[_receiver];
            receiver.token = _token;
            receiver.commissionPercentage = _commissionPercentage;
        }

    }

    /**
    * @dev Subscribes sender to an active receiver. Sender must allow at least amount tokens previously.
    * @param _receiver The address of the receiver.
    * @param _token The address of the ERC-20 token used for the subscription.
    * @param _amount The amount of tokens that will be transferred on every subscription execution.
    */
    function subscribe(address _receiver, address _token, uint256 _amount) external onlyActiveReceivers(_receiver) {
        Receiver storage receiver = receivers[_receiver];
        require(_receiver != msg.sender, "Subscriber must not be the receiver");
        require(receiver.token == _token, "Token must match the defined by the given receiver");
        require(_amount > 0, "Amount must be greater than zero");
        require(IERC20(_token).allowance(msg.sender, address(this)) > _amount, "Allowance must exceed amount");
        uint256 subscriberIndex = receiver.subscriberIndexes[msg.sender];
        uint256 subscriptionsLength = receiver.subscriptions.length; 
        if (subscriptionsLength > 0 && receiver.subscriptions[subscriberIndex].subscriber == msg.sender) {
            receiver.subscriptions[subscriberIndex].amount = _amount;
            receiver.maxAmountPerExecution -= receiver.subscriptions[subscriberIndex].amount;
            receiver.maxAmountPerExecution.add(_amount); 
            emit SubscriptionUpdated(msg.sender, _receiver, _amount);
        } else {
            receivers[_receiver].subscriberIndexes[msg.sender] = subscriptionsLength;
            Subscription memory subscription = Subscription(msg.sender, _amount);
            receivers[_receiver].subscriptions.push(subscription);
            receivers[_receiver].maxAmountPerExecution = receivers[_receiver].maxAmountPerExecution.add(_amount);
            receiver.maxAmountPerExecution.add(_amount);
            emit SubscriptionCreated(msg.sender, _receiver, _amount);
        }
    }

    /**
    * @dev Unsubscribes sender from a receiver.
    * @param _receiver The address of the receiver.
    */
    function unsubscribe(address _receiver) external onlyActiveReceivers(_receiver) {
        Subscription[] storage subscriptions = receivers[_receiver].subscriptions;
        uint256 subscriptionsLength = subscriptions.length;
        require(subscriptionsLength > 0, "Receiver must have subscriptions");
        mapping (address => uint256) storage subscriberIndexes = receivers[_receiver].subscriberIndexes;
        uint256 subscriberIndex = subscriberIndexes[msg.sender];
        require(
            subscriptions[subscriberIndex].subscriber == msg.sender,
            "Sender must be subscribed to the given receiver"
        );
        receivers[_receiver].maxAmountPerExecution -= subscriptions[subscriberIndex].amount;
        subscriberIndexes[msg.sender] = 0;
        subscriberIndexes[subscriptions[subscriptionsLength - 1].subscriber] = subscriberIndex;
        subscriptions[subscriberIndex] = subscriptions[subscriptions.length - 1];
        subscriptions.pop();
        emit SubscriptionRemoved(msg.sender, _receiver);
    }

    /**
     * @dev Executes subscriptions for a given receiver. If flash loan strategy provided, could take the total amount
     * pulled for the subscription and use it as a flash loan for free. Also, a commission as a percentage of the total
     * amount is provided for the executor.
     * @param _receiver The address of the receiver for which the subscriptions will be executed.
     * @param _flashLoanStrategy The address of an IFlashLoanStrategy to manage the flash loan. If want to avoid taking
     * the loan, set as address(0).
     * @param _commissionReceiver The address where to send the commission. If set to address(0), commission wont be 
     * pulled from the loan strategy contract. If flash loan strategy is set to address(0) too, then the commission is
     * avoided and sent to the receiver.
     */
    function executeSubscriptions(
        address _receiver,
        address _flashLoanStrategy,
        address _commissionReceiver
    ) external onlyActiveReceivers(_receiver) {
        Receiver storage receiver = receivers[_receiver];
        require(
            block.timestamp > receiver.lastExecution + TIME_BETWEEN_EXECUTIONS,
            "Must pass more time since the last execution"
        );
        receiver.lastExecution = block.timestamp;
        IERC20 token = IERC20(receiver.token);
        uint256 amount = _pullTokensFromSubscribers(_receiver, token);
        uint256 commission = amount.mul(receiver.commissionPercentage).div(100);
        if (_flashLoanStrategy != address(0)) {
            token.safeTransfer(_flashLoanStrategy, amount);
            require(
                IFlashLoanStrategy(_flashLoanStrategy).executeStrategy(receiver.token, amount, commission),
                "Flash loan strategy must return successful state"
            );
            token.safeTransferFrom(
                _flashLoanStrategy, address(this), _commissionReceiver != address(0) ? amount : amount - commission
            );
        }
        if (_commissionReceiver != address(0)) {
            token.safeTransfer(_commissionReceiver, commission);
            token.safeTransfer(_receiver, amount - commission);
        } else {
            token.safeTransfer(_receiver, _flashLoanStrategy != address(0) ? amount - commission : amount);
        }
        emit SubscriptionsExecuted(_receiver);
    }

    /**
     * @dev Gets the total amount that would be pulled from subscribers if the subscriptions of the given receiver are
     * executed right now.
     * @param _receiver The address of the receiver.
     * @return The total amount available for the subscriptions of the given receiver.
     */
    function getAvailableAmountForExecution(
        address _receiver
    ) external view onlyActiveReceivers(_receiver) returns (uint256) {
        uint256 amount;
        IERC20 token = IERC20(receivers[_receiver].token);
        Subscription[] storage subscriptions = receivers[_receiver].subscriptions;
        for (uint256 i = 0; i < subscriptions.length; i++) {
            uint256 subscriptionAmount = subscriptions[i].amount;
            if (_isSubscriptionAvailable(subscriptions[i].subscriber, token, subscriptionAmount)) {
                amount += subscriptionAmount;
            }
        }
        return amount;
    }

    /**
     * @dev Tells whether a single subscription could be executed or not.
     * @param _subscriber The address of the subscriber from which the subscription will be checked.
     * @param _token The address of the token used for the subscription.
     * @param _subscriptionAmount The amount that needs to be pulled.
     * @return True if token pull can be performed, false if not.
     */
    function _isSubscriptionAvailable(
        address _subscriber,
        IERC20 _token,
        uint256 _subscriptionAmount
    ) internal view returns (bool) {
        return _token.balanceOf(_subscriber) >= _subscriptionAmount 
            && _token.allowance(_subscriber, address(this)) >= _subscriptionAmount;
    }

    /**
     * @dev Pulls tokens from the receiver subscribers to the contract.
     * @param _receiver The address of the receiver which subscribers tokens will be pulled.
     * @param _token The ERC20 token to pull from subscribers.
     * @return The amount of tokens actually pulled from subscribers.
     */
    function _pullTokensFromSubscribers(address _receiver, IERC20 _token) internal returns (uint256) {
        uint256 amount;
        Subscription[] storage subscriptions = receivers[_receiver].subscriptions;
        for (uint256 i = 0; i < subscriptions.length; i++) {
            uint256 currentAmount = subscriptions[i].amount;
            try _token.transferFrom(subscriptions[i].subscriber, address(this), currentAmount) {
                amount += currentAmount;
            } catch {
                // Just ignore the failing transfer. Here we could remove the subscriber if we want to clean the array.
            }
        }
        return amount;
    }
}

//SPDX-License-Identifier: Unlicense

pragma solidity ^0.7.0;

interface IFlashLoanStrategy {

    /**
     * @dev Receives a flash loan and manages it with its underlying strategy. Must approve the pull of loaned tokens.
     * @param _token The address of the ERC-20 token loaned.
     * @param _amount The amount of token loaned.
     * @param _commission The amount of token given as commission. Can keep it if no commission receiver set.
     * @return True when strategy was executed successfully, false otherwise.
     */
    function executeStrategy(address _token, uint256 _amount, uint256 _commission) external returns (bool);
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

import "./IERC20.sol";
import "../../math/SafeMath.sol";
import "../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using SafeMath for uint256;
    using Address for address;

    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(IERC20 token, address spender, uint256 value) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        // solhint-disable-next-line max-line-length
        require((value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 newAllowance = token.allowance(address(this), spender).add(value);
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 newAllowance = token.allowance(address(this), spender).sub(value, "SafeERC20: decreased allowance below zero");
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) { // Return data is optional
            // solhint-disable-next-line max-line-length
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        uint256 c = a + b;
        if (c < a) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b > a) return (false, 0);
        return (true, a - b);
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) return (true, 0);
        uint256 c = a * b;
        if (c / a != b) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a / b);
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a % b);
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");
        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) return 0;
        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: division by zero");
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: modulo by zero");
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        return a - b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryDiv}.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a % b;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev https://eips.ethereum.org/EIPS/eip-712[EIP 712] is a standard for hashing and signing of typed structured data.
 *
 * The encoding specified in the EIP is very generic, and such a generic implementation in Solidity is not feasible,
 * thus this contract does not implement the encoding itself. Protocols need to implement the type-specific encoding
 * they need in their contracts using a combination of `abi.encode` and `keccak256`.
 *
 * This contract implements the EIP 712 domain separator ({_domainSeparatorV4}) that is used as part of the encoding
 * scheme, and the final step of the encoding to obtain the message digest that is then signed via ECDSA
 * ({_hashTypedDataV4}).
 *
 * The implementation of the domain separator was designed to be as efficient as possible while still properly updating
 * the chain id to protect against replay attacks on an eventual fork of the chain.
 *
 * NOTE: This contract implements the version of the encoding known as "v4", as implemented by the JSON RPC method
 * https://docs.metamask.io/guide/signing-data.html[`eth_signTypedDataV4` in MetaMask].
 *
 * _Available since v3.4._
 */
abstract contract EIP712 {
    /* solhint-disable var-name-mixedcase */
    // Cache the domain separator as an immutable value, but also store the chain id that it corresponds to, in order to
    // invalidate the cached domain separator if the chain id changes.
    bytes32 private immutable _CACHED_DOMAIN_SEPARATOR;
    uint256 private immutable _CACHED_CHAIN_ID;

    bytes32 private immutable _HASHED_NAME;
    bytes32 private immutable _HASHED_VERSION;
    bytes32 private immutable _TYPE_HASH;
    /* solhint-enable var-name-mixedcase */

    /**
     * @dev Initializes the domain separator and parameter caches.
     *
     * The meaning of `name` and `version` is specified in
     * https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator[EIP 712]:
     *
     * - `name`: the user readable name of the signing domain, i.e. the name of the DApp or the protocol.
     * - `version`: the current major version of the signing domain.
     *
     * NOTE: These parameters cannot be changed except through a xref:learn::upgrading-smart-contracts.adoc[smart
     * contract upgrade].
     */
    constructor(string memory name, string memory version) internal {
        bytes32 hashedName = keccak256(bytes(name));
        bytes32 hashedVersion = keccak256(bytes(version));
        bytes32 typeHash = keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)");
        _HASHED_NAME = hashedName;
        _HASHED_VERSION = hashedVersion;
        _CACHED_CHAIN_ID = _getChainId();
        _CACHED_DOMAIN_SEPARATOR = _buildDomainSeparator(typeHash, hashedName, hashedVersion);
        _TYPE_HASH = typeHash;
    }

    /**
     * @dev Returns the domain separator for the current chain.
     */
    function _domainSeparatorV4() internal view virtual returns (bytes32) {
        if (_getChainId() == _CACHED_CHAIN_ID) {
            return _CACHED_DOMAIN_SEPARATOR;
        } else {
            return _buildDomainSeparator(_TYPE_HASH, _HASHED_NAME, _HASHED_VERSION);
        }
    }

    function _buildDomainSeparator(bytes32 typeHash, bytes32 name, bytes32 version) private view returns (bytes32) {
        return keccak256(
            abi.encode(
                typeHash,
                name,
                version,
                _getChainId(),
                address(this)
            )
        );
    }

    /**
     * @dev Given an already https://eips.ethereum.org/EIPS/eip-712#definition-of-hashstruct[hashed struct], this
     * function returns the hash of the fully encoded EIP712 message for this domain.
     *
     * This hash can be used together with {ECDSA-recover} to obtain the signer of a message. For example:
     *
     * ```solidity
     * bytes32 digest = _hashTypedDataV4(keccak256(abi.encode(
     *     keccak256("Mail(address to,string contents)"),
     *     mailTo,
     *     keccak256(bytes(mailContents))
     * )));
     * address signer = ECDSA.recover(digest, signature);
     * ```
     */
    function _hashTypedDataV4(bytes32 structHash) internal view virtual returns (bytes32) {
        return keccak256(abi.encodePacked("\x19\x01", _domainSeparatorV4(), structHash));
    }

    function _getChainId() private view returns (uint256 chainId) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        // solhint-disable-next-line no-inline-assembly
        assembly {
            chainId := chainid()
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.
 *
 * These functions can be used to verify that a message was signed by the holder
 * of the private keys of a given address.
 */
library ECDSA {
    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature`. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     */
    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {
        // Check the signature length
        if (signature.length != 65) {
            revert("ECDSA: invalid signature length");
        }

        // Divide the signature in r, s and v variables
        bytes32 r;
        bytes32 s;
        uint8 v;

        // ecrecover takes the signature parameters, and the only way to get them
        // currently is to use assembly.
        // solhint-disable-next-line no-inline-assembly
        assembly {
            r := mload(add(signature, 0x20))
            s := mload(add(signature, 0x40))
            v := byte(0, mload(add(signature, 0x60)))
        }

        return recover(hash, v, r, s);
    }

    /**
     * @dev Overload of {ECDSA-recover-bytes32-bytes-} that receives the `v`,
     * `r` and `s` signature fields separately.
     */
    function recover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) internal pure returns (address) {
        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
        // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most
        // signatures from current libraries generate a unique signature with an s-value in the lower half order.
        //
        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
        // these malleable signatures as well.
        require(uint256(s) <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0, "ECDSA: invalid signature 's' value");
        require(v == 27 || v == 28, "ECDSA: invalid signature 'v' value");

        // If the signature is valid (and not malleable), return the signer address
        address signer = ecrecover(hash, v, r, s);
        require(signer != address(0), "ECDSA: invalid signature");

        return signer;
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from a `hash`. This
     * replicates the behavior of the
     * https://github.com/ethereum/wiki/wiki/JSON-RPC#eth_sign[`eth_sign`]
     * JSON-RPC method.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {
        // 32 is the length in bytes of hash,
        // enforced by the type signature above
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.2 <0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

{
  "optimizer": {
    "enabled": true,
    "runs": 200
  },
  "outputSelection": {
    "*": {
      "*": [
        "evm.bytecode",
        "evm.deployedBytecode",
        "abi"
      ]
    }
  },
  "libraries": {}
}
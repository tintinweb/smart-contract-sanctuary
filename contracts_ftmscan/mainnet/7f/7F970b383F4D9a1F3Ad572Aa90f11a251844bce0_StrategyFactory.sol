/**
 *Submitted for verification at FtmScan.com on 2021-11-29
*/

// SPDX-License-Identifier: MIXED

// File @openzeppelin/contracts-upgradeable/proxy/utils/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        require(_initializing || !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }
}

// File @openzeppelin/contracts-upgradeable/utils/structs/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSetUpgradeable {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}

// File @openzeppelin/contracts-upgradeable/access/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControlUpgradeable {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// File @openzeppelin/contracts-upgradeable/access/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControlEnumerable declared to support ERC165 detection.
 */
interface IAccessControlEnumerableUpgradeable is IAccessControlUpgradeable {
    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) external view returns (address);

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) external view returns (uint256);
}

// File @openzeppelin/contracts-upgradeable/utils/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal initializer {
        __Context_init_unchained();
    }

    function __Context_init_unchained() internal initializer {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
    uint256[50] private __gap;
}

// File @openzeppelin/contracts-upgradeable/utils/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library StringsUpgradeable {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// File @openzeppelin/contracts-upgradeable/utils/introspection/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165Upgradeable {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// File @openzeppelin/contracts-upgradeable/utils/introspection/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;


/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165Upgradeable is Initializable, IERC165Upgradeable {
    function __ERC165_init() internal initializer {
        __ERC165_init_unchained();
    }

    function __ERC165_init_unchained() internal initializer {
    }
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165Upgradeable).interfaceId;
    }
    uint256[50] private __gap;
}

// File @openzeppelin/contracts-upgradeable/access/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;





/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControlUpgradeable is Initializable, ContextUpgradeable, IAccessControlUpgradeable, ERC165Upgradeable {
    function __AccessControl_init() internal initializer {
        __Context_init_unchained();
        __ERC165_init_unchained();
        __AccessControl_init_unchained();
    }

    function __AccessControl_init_unchained() internal initializer {
    }
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControlUpgradeable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        StringsUpgradeable.toHexString(uint160(account), 20),
                        " is missing role ",
                        StringsUpgradeable.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    function _grantRole(bytes32 role, address account) private {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    function _revokeRole(bytes32 role, address account) private {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
    uint256[49] private __gap;
}

// File @openzeppelin/contracts-upgradeable/access/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;




/**
 * @dev Extension of {AccessControl} that allows enumerating the members of each role.
 */
abstract contract AccessControlEnumerableUpgradeable is Initializable, IAccessControlEnumerableUpgradeable, AccessControlUpgradeable {
    function __AccessControlEnumerable_init() internal initializer {
        __Context_init_unchained();
        __ERC165_init_unchained();
        __AccessControl_init_unchained();
        __AccessControlEnumerable_init_unchained();
    }

    function __AccessControlEnumerable_init_unchained() internal initializer {
    }
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.AddressSet;

    mapping(bytes32 => EnumerableSetUpgradeable.AddressSet) private _roleMembers;

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControlEnumerableUpgradeable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) public view override returns (address) {
        return _roleMembers[role].at(index);
    }

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) public view override returns (uint256) {
        return _roleMembers[role].length();
    }

    /**
     * @dev Overload {grantRole} to track enumerable memberships
     */
    function grantRole(bytes32 role, address account) public virtual override(AccessControlUpgradeable, IAccessControlUpgradeable) {
        super.grantRole(role, account);
        _roleMembers[role].add(account);
    }

    /**
     * @dev Overload {revokeRole} to track enumerable memberships
     */
    function revokeRole(bytes32 role, address account) public virtual override(AccessControlUpgradeable, IAccessControlUpgradeable) {
        super.revokeRole(role, account);
        _roleMembers[role].remove(account);
    }

    /**
     * @dev Overload {renounceRole} to track enumerable memberships
     */
    function renounceRole(bytes32 role, address account) public virtual override(AccessControlUpgradeable, IAccessControlUpgradeable) {
        super.renounceRole(role, account);
        _roleMembers[role].remove(account);
    }

    /**
     * @dev Overload {_setupRole} to track enumerable memberships
     */
    function _setupRole(bytes32 role, address account) internal virtual override {
        super._setupRole(role, account);
        _roleMembers[role].add(account);
    }
    uint256[49] private __gap;
}

// File @openzeppelin/contracts/token/ERC20/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// File @openzeppelin/contracts/utils/introspection/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// File @openzeppelin/contracts/token/ERC1155/[email protected]
// License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155 is IERC165 {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// File contracts/interfaces/IStrategy.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;

interface IStrategy {
  /**
     * @notice Gets the token this strategy compounds.
     * @dev This token might have a transfer-tax.
     * @dev Invariant: This variable may never change.
     */
    function underlyingToken() external view returns (IERC20);

    /**
     * @notice Gets the total amount of tokens either idle in this strategy or staked in an underlying strategy.
     */
    function totalUnderlying() external view returns (uint256 totalUnderlying);
    /**
     * @notice Gets the total amount of tokens either idle in this strategy or staked in an underlying strategy and only the tokens actually staked.
     */
    function totalUnderlyingAndStaked() external view returns (uint256 totalUnderlying, uint256 totalUnderlyingStaked);

    /**
     * @notice The panic function unstakes all staked funds from the strategy and leaves them idle in the strategy for withdrawal
     * @dev Authority: This function must only be callable by the VaultChef.
     */
    function panic() external;

    /**
     * @notice Executes a harvest on the underlying vaultchef.
     * @dev Authority: This function must only be callable by the vaultchef.
     */
    function harvest() external;
    /**
     * @notice Deposits `amount` amount of underlying tokens in the underlying strategy
     * @dev Authority: This function must only be callable by the VaultChef.
     */
    function deposit(uint256 amount) external;

    /**
     * @notice Withdraws `amount` amount of underlying tokens to `to`.
     * @dev Authority: This function must only be callable by the VaultChef.
     */
    function withdraw(address to, uint256 amount) external;

    /**
     * @notice Withdraws `amount` amount of `token` to `to`.
     * @notice This function is used to withdraw non-staking and non-native tokens accidentally sent to the strategy.
     * @notice It will also be used to withdraw tokens airdropped to the strategies.
     * @notice The underlying token can never be withdrawn through this method because VaultChef prevents it.
     * @dev Requirement: This function should in no way allow withdrawal of staking tokens
     * @dev Requirement: This function should in no way allow for the decline in shares or share value (this is also checked in the VaultChef);
     * @dev Validation is already done in the VaultChef that the staking token cannot be withdrawn.
     * @dev Authority: This function must only be callable by the VaultChef.
     */
    function inCaseTokensGetStuck(
        IERC20 token,
        uint256 amount,
        address to
    ) external;
}

// File contracts/interfaces/IVaultChefCore.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;



/**
 * @notice The VaultChef is a vault management contract that manages vaults, their strategies and the share positions of investors in these vaults.
 * @notice Positions are not hardcoded into the contract like traditional staking contracts, instead they are managed as ERC-1155 receipt tokens.
 * @notice This receipt-token mechanism is supposed to simplify zapping and other derivative protocols.
 * @dev The VaultChef contract has the following design principles.
 * @dev 1. Simplicity of Strategies: Strategies should be as simple as possible.
 * @dev 2. Control of Governance: Governance should never be able to steal underlying funds.
 * @dev 3. Auditability: It should be easy for third-party reviewers to assess the safety of the VaultChef.
 */
interface IVaultChefCore is IERC1155 {
    /// @notice A vault is a strategy users can stake underlying tokens in to receive a share of the vault value.
    struct Vault {
        /// @notice The token this strategy will compound.
        IERC20 underlyingToken;
        /// @notice The timestamp of the last harvest, set to zero while no harvests have happened.
        uint96 lastHarvestTimestamp;
        /// @notice The strategy contract.
        IStrategy strategy;
        /// @notice The performance fee portion of the harvests that is sent to the feeAddress, denominated by 10,000.
        uint16 performanceFeeBP;
        /// @notice Whether deposits are currently paused.
        bool paused;
        /// @notice Whether the vault has panicked which means the funds are pulled from the strategy and it is paused forever.
        bool panicked;
    }

    /**
     * @notice Deposit `underlyingAmount` amount of underlying tokens into the vault and receive `sharesReceived` proportional to the actually staked amount.
     * @notice Deposits mint `sharesReceived` receipt tokens as ERC-1155 tokens to msg.sender with the tokenId equal to the vaultId.
     * @notice The tokens are transferred from `msg.sender` which requires approval if pulled is set to false, otherwise `msg.sender` needs to implement IPullDepositor.
     * @param vaultId The id of the vault.
     * @param underlyingAmount The intended amount of tokens to deposit (this might not equal the actual deposited amount due to tx/stake fees or the pull mechanism).
     * @param pulled Uses a pull-based deposit hook if set to true, otherwise traditional safeTransferFrom. The pull-based mechanism allows the depositor to send tokens using a hook.
     * @param minSharesReceived The minimum amount of shares that must be received, or the transaction reverts.
     * @dev This pull-based methodology is extremely valuable for zapping transfer-tax tokens more economically.
     * @dev `msg.sender` must be a smart contract implementing the `IPullDepositor` interface.
     * @return sharesReceived The number of shares minted to the msg.sender.
     */
    function depositUnderlying(
        uint256 vaultId,
        uint256 underlyingAmount,
        bool pulled,
        uint256 minSharesReceived
    ) external returns (uint256 sharesReceived);

    /**
     * @notice Withdraws `shares` from the vault into underlying tokens to the `msg.sender`.
     * @notice Burns `shares` receipt tokens from the `msg.sender`.
     * @param vaultId The id of the vault.
     * @param shares The amount of shares to burn, underlying tokens will be sent to msg.sender proportionally.
     * @param minUnderlyingReceived The minimum amount of underlying tokens that must be received, or the transaction reverts.
     */
    function withdrawShares(
        uint256 vaultId,
        uint256 shares,
        uint256 minUnderlyingReceived
    ) external returns (uint256 underlyingReceived);

    /**
     * @notice Withdraws `shares` from the vault into underlying tokens to the `to` address.
     * @notice To prevent phishing, we require msg.sender to be a contract as this is intended for more economical zapping of transfer-tax token withdrawals.
     * @notice Burns `shares` receipt tokens from the `msg.sender`.
     * @param vaultId The id of the vault.
     * @param shares The amount of shares to burn, underlying tokens will be sent to msg.sender proportionally.
     * @param minUnderlyingReceived The minimum amount of underlying tokens that must be received, or the transaction reverts.
     */
    function withdrawSharesTo(
        uint256 vaultId,
        uint256 shares,
        uint256 minUnderlyingReceived,
        address to
    ) external returns (uint256 underlyingReceived);

    /**
     * @notice Total amount of shares in circulation for a given vaultId.
     * @param vaultId The id of the vault.
     * @return The total number of shares currently in circulation.
     */
    function totalSupply(uint256 vaultId) external view returns (uint256);

    /**
     * @notice Calls harvest on the underlying strategy to compound pending rewards to underlying tokens.
     * @notice The performance fee is minted to the owner as shares, it can never be greater than 5% of the underlyingIncrease.
     * @return underlyingIncrease The amount of underlying tokens generated.
     * @dev Can only be called by owner.
     */
    function harvest(uint256 vaultId)
        external
        returns (uint256 underlyingIncrease);

    /**
     * @notice Adds a new vault to the vaultchef.
     * @param strategy The strategy contract that manages the allocation of the funds for this vault, also defines the underlying token
     * @param performanceFeeBP The percentage of the harvest rewards that are given to the governance, denominated by 10,000 and maximum 5%.
     * @dev Can only be called by owner.
     */
    function addVault(IStrategy strategy, uint16 performanceFeeBP) external;

    /**
     * @notice Updates the performanceFee of the vault.
     * @param vaultId The id of the vault.
     * @param performanceFeeBP The percentage of the harvest rewards that are given to the governance, denominated by 10,000 and maximum 5%.
     * @dev Can only be called by owner.
     */
    function setVault(uint256 vaultId, uint16 performanceFeeBP) external;
    /**
     * @notice Allows the `pullDepositor` to create pull-based deposits (useful for zapping contract).
     * @notice Having a whitelist is not necessary for this functionality as it is safe but upon defensive code recommendations one was added in.
     * @dev Can only be called by owner.
     */
    function setPullDepositor(address pullDepositor, bool isAllowed) external;
    
    /**
     * @notice Withdraws funds from the underlying staking contract to the strategy and irreversibly pauses the vault.
     * @param vaultId The id of the vault.
     * @dev Can only be called by owner.
     */
    function panicVault(uint256 vaultId) external;

    /**
     * @notice Returns true if there is a vault associated with the `vaultId`.
     * @param vaultId The id of the vault.
     */
    function isValidVault(uint256 vaultId) external returns (bool);

    /**
     * @notice Returns the Vault information of the vault at `vaultId`, returns if non-existent.
     * @param vaultId The id of the vault.
     */
    function vaultInfo(uint256 vaultId) external returns (IERC20 underlyingToken, uint96 lastHarvestTimestamp, IStrategy strategy, uint16 performanceFeeBP, bool paused, bool panicked);

    /**
     * @notice Pauses the vault which means deposits and harvests are no longer permitted, reverts if already set to the desired value.
     * @param vaultId The id of the vault.
     * @param paused True to pause, false to unpause.
     * @dev Can only be called by owner.
     */
    function pauseVault(uint256 vaultId, bool paused) external;

    /**
     * @notice Transfers tokens from the VaultChef to the `to` address.
     * @notice Cannot be abused by governance since the protocol never ever transfers tokens to the VaultChef. Any tokens stored there are accidentally sent there.
     * @param token The token to withdraw from the VaultChef.
     * @param to The address to send the token to.
     * @dev Can only be called by owner.
     */
    function inCaseTokensGetStuck(IERC20 token, address to) external;

    /**
     * @notice Transfers tokens from the underlying strategy to the `to` address.
     * @notice Cannot be abused by governance since VaultChef prevents token to be equal to the underlying token.
     * @param token The token to withdraw from the strategy.
     * @param to The address to send the token to.
     * @param amount The amount of tokens to withdraw.
     * @dev Can only be called by owner.
     */
    function inCaseVaultTokensGetStuck(
        uint256 vaultId,
        IERC20 token,
        address to,
        uint256 amount
    ) external;
}

// File contracts/interfaces/IMasterChef.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;

/// @dev The VaultChef implements the masterchef interface for compatibility with third-party tools.
interface IMasterChef {
    /// @dev An active vault has a dummy allocPoint of 1 while an inactive one has an allocPoint of zero.
    /// @dev This is done for better compatibility with third-party tools.
    function poolInfo(uint256 pid)
        external
        view
        returns (
            IERC20 lpToken,
            uint256 allocPoint,
            uint256 lastRewardBlock,
            uint256 accTokenPerShare
        );

    function userInfo(uint256 pid, address user)
        external
        view
        returns (uint256 amount, uint256 rewardDebt);

    function startBlock() external view returns (uint256);

    function poolLength() external view returns (uint256);

    /// @dev Returns the total number of active vaults.
    function totalAllocPoint() external view returns (uint256);

    function deposit(uint256 _pid, uint256 _amount) external;

    function withdraw(uint256 _pid, uint256 _amount) external;

    function emergencyWithdraw(uint256 _pid) external;
}

// File contracts/interfaces/IERC20Metadata.sol
// License-Identifier: MIT
// Based on: https://github.com/OpenZeppelin/openzeppelin-contracts/blob/1b27c13096d6e4389d62e7b0766a1db53fbb3f1b/contracts/token/ERC20/extensions/IERC20Metadata.sol

pragma solidity ^0.8.6;
/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// File contracts/interfaces/IVaultChefWrapper.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;



interface IVaultChefWrapper is IMasterChef, IERC20Metadata{
     /**
     * @notice Interface function to fetch the total underlying tokens inside a vault.
     * @notice Calls the totalUnderlying function on the vault strategy.
     * @param vaultId The id of the vault.
     */
    function totalUnderlying(uint256 vaultId) external view returns (uint256);

     /**
     * @notice Changes the ERC-20 metadata for etherscan listing.
     * @param newName The new ERC-20-like token name.
     * @param newSymbol The new ERC-20-like token symbol.
     * @param newDecimals The new ERC-20-like token decimals.
     */
    function changeMetadata(
        string memory newName,
        string memory newSymbol,
        uint8 newDecimals
    ) external;

     /**
     * @notice Sets the ERC-1155 metadata URI.
     * @param newURI The new ERC-1155 metadata URI.
     */
    function setURI(string memory newURI) external;

    /// @notice mapping that returns true if the strategy is set as a vault.
    function strategyExists(IStrategy strategy) external view returns(bool);


    /// @notice Utility mapping for UI to figure out the vault id of a strategy.
    function strategyVaultId(IStrategy strategy) external view returns(uint256);

}

// File contracts/interfaces/IVaultChef.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;


/// @notice Interface for derivative protocols.
interface IVaultChef is IVaultChefWrapper, IVaultChefCore {
    function owner() external view returns (address);
}

// File contracts/interfaces/ISubFactory.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;


interface ISubFactory {
    function deployStrategy(
        IVaultChef vaultChef,
        IERC20 underlyingToken,
        bytes calldata projectData,
        bytes calldata strategyData
    ) external returns (IStrategy);
}

// File contracts/interfaces/IZapHandler.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;

/// @notice The IZap interface allows contracts to swap a token for another token without having to directly interact with verbose AMMs directly.
/// @notice It furthermore allows to zap to and from an LP pair within a single transaction.
interface IZapHandler {
    struct Factory {
        /// @dev The address of the factory.
        address factory;
        /// @dev The fee nominator of the AMM, usually set to 997 for a 0.3% fee.
        uint32 amountsOutNominator;
        /// @dev The fee denominator of the AMM, usually set to 1000.
        uint32 amountsOutDenominator;
    }

    function setFactory(
        address factory,
        uint32 amountsOutNominator,
        uint32 amountsOutDenominator
    ) external;

    function setRoute(
        IERC20 from,
        IERC20 to,
        address[] memory inputRoute
    ) external;
    function factories(address factoryAddress) external view returns (Factory memory);

    function routeLength(IERC20 token0, IERC20 token1) external view returns (uint256);

    function owner() external view returns (address);
}

// File contracts/interfaces/IZap.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;


/// @notice The IZap interface allows contracts to swap a token for another token without having to directly interact with verbose AMMs directly.
/// @notice It furthermore allows to zap to and from an LP pair within a single transaction.
interface IZap {
    /**
    * @notice Swap `amount` of `fromToken` to `toToken` and send them to the `recipient`.
    * @notice The `fromToken` and `toToken` arguments can be AMM pairs.
    * @notice Reverts if the `recipient` received less tokens than `minReceived`.
    * @notice Requires approval.
    * @param fromToken The token to take from `msg.sender` and exchange for `toToken`.
    * @param toToken The token that will be bought and sent to the `recipient`.
    * @param recipient The destination address to receive the `toToken`.
    * @param amount The amount that the zapper should take from the `msg.sender` and swap.
    * @param minReceived The minimum amount of `toToken` the `recipient` should receive. Otherwise the transaction reverts.
    */
    function swapERC20(IERC20 fromToken, IERC20 toToken, address recipient, uint256 amount, uint256 minReceived) external returns (uint256 received);


    /**
    * @notice Swap `amount` of `fromToken` to `toToken` and send them to the `msg.sender`.
    * @notice The `fromToken` and `toToken` arguments can be AMM pairs.
    * @notice Requires approval.
    * @param fromToken The token to take from `msg.sender` and exchange for `toToken`.
    * @param toToken The token that will be bought and sent to the `msg.sender`.
    * @param amount The amount that the zapper should take from the `msg.sender` and swap.
    */
    function swapERC20Fast(IERC20 fromToken, IERC20 toToken, uint256 amount) external;

    function implementation() external view returns (IZapHandler);
}

// File contracts/factories/StrategyFactory.sol
// License-Identifier: MIT

pragma solidity ^0.8.6;







/// @notice The strategy factory is a utility contracts used by Violin to deploy new strategies more swiftly and securely.
/// @notice The admin can register strategy types together with a SubFactory that creates instances of the relevant strategy type.
/// @notice Examples of strategy types are MC_PCS_V1, MC_GOOSE_V1, MC_PANTHER_V1...
/// @notice Once a few types are registered, new strategies can be easily deployed by registering the relevant project and then instantiating strategies on that project.
/// @dev All strategy types, projects and individual strategies are stored as keccak256 hashes. The deployed strategies are identified by keccak256(keccak256(projectId), keccak256(strategyId)).
/// @dev VAULTCHEF AUTH: The StrategyFactory must have the ADD_VAULT_ROLE on the governor.
/// @dev ZAPGOVERNANCE AUTH: The StrategyFactory must have the

/// TODO: There is no createVault function!
contract StrategyFactory is AccessControlEnumerableUpgradeable {
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.Bytes32Set;
    /// @notice The instance of the vaultChef to deploy strategies to.
    IVaultChef public vaultChef;

    /// @notice The zapper
    IZap public zap;

    //** STRATEGY TYPES **/
    /// @notice strategyTypes contains all registered hashed strategy types.
    EnumerableSetUpgradeable.Bytes32Set private strategyTypes;
    /// @notice Returns the registered subfactory of the strategy type. The subfactory is responsible for instantiating factories.
    mapping(bytes32 => ISubFactory) public subfactoryByType;

    mapping(address => bool) public isSubfactory;

    //** PROJECTS **/
    /// @notice All registered projects.
    EnumerableSetUpgradeable.Bytes32Set private projects;
    /// @notice The associated strategy type hash of the project. All strategies under the project will thus be deployed using the subfactory of this strategy type.
    mapping(bytes32 => bytes32) public projectStrategyType;
    /// @notice Generic parameters that will always be forwarded to the subfactory. This could for example be the native token.
    mapping(bytes32 => bytes) public projectParams;
    /// @notice Metadata associated with the project that can be used on the frontend, expected to be encoded in UTF-8 JSON.
    /// @notice Even though not ideomatic, this is a cheap solution to avoid infrastructure downtime within the first months after launch.
    mapping(bytes32 => bytes) public projectMetadata;

    /// @notice List of strategies registered for the project.
    mapping(bytes32 => EnumerableSetUpgradeable.Bytes32Set)
        private projectStrategies;

    //** STRATEGIES **/

    /// @notice All registered strategies.
    /// @dev These are identified as keccak256(abi.encodePacked(keccak256(projectId), keccak256(strategyId))).
    EnumerableSetUpgradeable.Bytes32Set private strategies;
    /// @notice Metadata associated with the strategy that can be used on the frontend, expected to be encoded in UTF-8 JSON.
    /// @notice Even though not ideomatic, this is a cheap solution to avoid infrastructure downtime within the first months after launch.
    mapping(bytes32 => bytes) public strategyMetadata;

    /// @notice Gets the vaultId associated with the strategyId.
    mapping(bytes32 => uint256) private strategyToVaultId;
    /// @notice Gets the strategy id associated with the vaultId.
    mapping(uint256 => bytes32) public vaultIdToStrategy;

    /// @notice gets all strategy ids associated with an underlying token.
    mapping(IERC20 => EnumerableSetUpgradeable.Bytes32Set)
        private underlyingToStrategies;

    bytes32 public constant REGISTER_STRATEGY_ROLE =
        keccak256("REGISTER_STRATEGY_ROLE");
    bytes32 public constant REGISTER_PROJECT_ROLE =
        keccak256("REGISTER_PROJECT_ROLE");
    bytes32 public constant CREATE_VAULT_ROLE = keccak256("CREATE_VAULT_ROLE");

    event StrategyTypeAdded(
        bytes32 indexed strategyType,
        ISubFactory indexed subfactory
    );
    event ProjectRegistered(
        bytes32 indexed projectId,
        bytes32 indexed strategyType,
        bool indexed isUpdate
    );
    event VaultRegistered(
        uint256 indexed vaultId,
        bytes32 indexed projectId,
        bytes32 indexed strategyId,
        bool isUpdate
    );

    function initialize(IVaultChef _vaultChef, IZap _zap, address owner) external initializer {
        __AccessControlEnumerable_init();

        vaultChef = _vaultChef;
        zap = _zap;
        vaultChef.poolLength(); // validate vaultChef

        _setupRole(DEFAULT_ADMIN_ROLE, owner);
        _setupRole(REGISTER_STRATEGY_ROLE, owner);
        _setupRole(REGISTER_PROJECT_ROLE, owner);
        _setupRole(CREATE_VAULT_ROLE, owner);
    }

    function registerStrategyType(
        string calldata strategyType,
        ISubFactory subfactory
    ) external onlyRole(REGISTER_STRATEGY_ROLE) {
        registerStrategyTypeRaw(
            keccak256(abi.encodePacked(strategyType)),
            subfactory
        );
    }

    function registerStrategyTypeRaw(
        bytes32 strategyType,
        ISubFactory subfactory
    ) public onlyRole(REGISTER_STRATEGY_ROLE) {
        require(!strategyTypes.contains(strategyType), "!exists");
        strategyTypes.add(strategyType);
        subfactoryByType[strategyType] = subfactory;
        isSubfactory[address(subfactory)] = true;

        emit StrategyTypeAdded(strategyType, subfactory);
    }

    function registerProject(
        string calldata projectId,
        string calldata strategyType,
        bytes calldata params,
        bytes calldata metadata
    ) external onlyRole(REGISTER_PROJECT_ROLE) {
        registerProjectRaw(
            keccak256(abi.encodePacked(projectId)),
            keccak256(abi.encodePacked(strategyType)),
            params,
            metadata
        );
    }

    function registerProjectRaw(
        bytes32 projectId,
        bytes32 strategyType,
        bytes calldata params,
        bytes calldata metadata
    ) public onlyRole(REGISTER_PROJECT_ROLE) {
        require(
            strategyTypes.contains(strategyType),
            "!strategyType not found"
        );
        bool exists = projects.contains(projectId);
        projectStrategyType[projectId] = strategyType;
        projectParams[projectId] = params;
        projectMetadata[projectId] = metadata;

        emit ProjectRegistered(projectId, strategyType, exists);
    }

    struct CreateVaultVars {
        bytes32 strategyUID;
        bool exists;
        IStrategy strategy;
        uint256 vaultId;
        bytes projectParams;
    }

    function createVault(
        string calldata projectId,
        string calldata strategyId,
        IERC20 underlyingToken,
        bytes calldata params,
        bytes calldata metadata,
        uint16 performanceFee
    ) external onlyRole(CREATE_VAULT_ROLE) returns (uint256, IStrategy) {
        return
            createVaultRaw(
                keccak256(abi.encodePacked(projectId)),
                keccak256(abi.encodePacked(strategyId)),
                underlyingToken,
                params,
                metadata,
                performanceFee
            );
    }

    function createVaultRaw(
        bytes32 projectId,
        bytes32 strategyId,
        IERC20 underlyingToken,
        bytes calldata params,
        bytes calldata metadata,
        uint16 performanceFee
    ) public onlyRole(CREATE_VAULT_ROLE) returns (uint256, IStrategy) {
        CreateVaultVars memory vars;

        vars.strategyUID = getStrategyUID(projectId, strategyId);
        vars.exists = strategies.contains(vars.strategyUID);
        vars.projectParams = projectParams[projectId];

        vars.strategy = subfactoryByType[projectStrategyType[projectId]]
            .deployStrategy(
                vaultChef,
                underlyingToken,
                vars.projectParams,
                params
            );
        vars.vaultId = vaultChef.poolLength();
        IVaultChef(vaultChef.owner()).addVault(vars.strategy, performanceFee); // .owner to get the governor which inherits the vaultchef interface

        strategyMetadata[vars.strategyUID] = metadata;

        // Indexing
        projectStrategies[projectId].add(vars.strategyUID);
        vaultIdToStrategy[vars.vaultId] = vars.strategyUID;
        strategyToVaultId[vars.strategyUID] = vars.vaultId;
        underlyingToStrategies[vars.strategy.underlyingToken()].add(
            vars.strategyUID
        );

        emit VaultRegistered(vars.vaultId, projectId, strategyId, vars.exists);
        return (vars.vaultId, vars.strategy);
    }

    function setRoute(address[] calldata route) external {
        require(isSubfactory[msg.sender], "!subfactory");
        // Only set the route if it actually exists.
        if (route.length > 0) {
            IERC20 from = IERC20(route[0]);
            IERC20 to = IERC20(route[route.length - 1]);
            IZapHandler zapHandler = IZapHandler(
                IZapHandler(zap.implementation()).owner()
            ); // go to the governance contract which mimics the IZapHandler interface
            if (zapHandler.routeLength(from, to) == 0) {
                zapHandler.setRoute(from, to, route);
            }
        }
    }

    function getStrategyUID(bytes32 projectId, bytes32 strategyId)
        internal
        pure
        returns (bytes32)
    {
        return keccak256(abi.encodePacked(projectId, strategyId));
    }

    //** VIEW FUNCTIONS **//

    //** strategy types **/

    /// @notice Returns whether the unhashed `strategyType` is registered.
    function isStrategyType(string calldata strategyType)
        external
        view
        returns (bool)
    {
        return isStrategyTypeRaw(keccak256(abi.encodePacked(strategyType)));
    }

    /// @notice Returns whether the hashed `strategyType` is registered.
    function isStrategyTypeRaw(bytes32 strategyType)
        public
        view
        returns (bool)
    {
        return strategyTypes.contains(strategyType);
    }

    /// @notice Gets the length of the strategyType listing.
    function getStrategyTypeLength() public view returns (uint256) {
        return strategyTypes.length();
    }

    /// @notice Gets the strategyType hash at a specific index in the listing.
    function getStrategyTypeAt(uint256 index) public view returns (bytes32) {
        return strategyTypes.at(index);
    }

    /// @notice Lists the strategyType hashes within a specific range in the listing.
    function getStrategyTypes(uint256 from, uint256 amount)
        public
        view
        returns (bytes32[] memory)
    {
        return getPaginated(strategyTypes, from, amount);
    }

    //** projects **/

    /// @notice Returns whether the unhashed `projectId` is registered.
    function isProject(string calldata projectId) external view returns (bool) {
        return isStrategyTypeRaw(keccak256(abi.encodePacked(projectId)));
    }

    /// @notice Returns whether the hashed `projectId` is registered.
    function isProjectRaw(bytes32 projectId) public view returns (bool) {
        return strategyTypes.contains(projectId);
    }

    /// @notice Gets the length of the projects listing.
    function getProjectsLength() public view returns (uint256) {
        return projects.length();
    }

    /// @notice Gets the project hash at a specific index in the listing.
    function getProjectAt(uint256 index) public view returns (bytes32) {
        return projects.at(index);
    }

    /// @notice Lists the project hashes within a specific range in the listing.
    function getProjects(uint256 from, uint256 amount)
        public
        view
        returns (bytes32[] memory)
    {
        return getPaginated(projects, from, amount);
    }

    /// @notice Gets the length (number) of strategies of a project listing.
    function getProjectStrategiesLength(string calldata projectId)
        external
        view
        returns (uint256)
    {
        return
            getProjectStrategiesLengthRaw(
                keccak256(abi.encodePacked(projectId))
            );
    }

    /// @notice Gets the length (number) of strategies of a project listing.
    function getProjectStrategiesLengthRaw(bytes32 projectId)
        public
        view
        returns (uint256)
    {
        return projectStrategies[projectId].length();
    }

    /// @notice Gets the project's strategy hash at a specific index in the listing.
    function getProjectStrategyAt(string calldata projectId, uint256 index)
        external
        view
        returns (bytes32)
    {
        return
            getProjectStrategyAtRaw(
                keccak256(abi.encodePacked(projectId)),
                index
            );
    }

    /// @notice Gets the project's strategy hash at a specific index in the listing.
    function getProjectStrategyAtRaw(bytes32 projectId, uint256 index)
        public
        view
        returns (bytes32)
    {
        return projectStrategies[projectId].at(index);
    }

    /// @notice Lists the project's strategy hashes within a specific range in the listing.
    function getProjectStrategies(
        string calldata projectId,
        uint256 from,
        uint256 amount
    ) external view returns (bytes32[] memory) {
        return
            getProjectStrategiesRaw(
                keccak256(abi.encodePacked(projectId)),
                from,
                amount
            );
    }

    /// @notice Lists the project's strategy hashes within a specific range in the listing.
    function getProjectStrategiesRaw(
        bytes32 projectId,
        uint256 from,
        uint256 amount
    ) public view returns (bytes32[] memory) {
        return getPaginated(projectStrategies[projectId], from, amount);
    }

    //** strategies **/

    /// @notice Gets the length (number) of strategies of a project listing.
    function getStrategiesLength() external view returns (uint256) {
        return strategies.length();
    }

    /// @notice Gets the strategy hash at a specific index in the listing.
    function getStrategyAt(uint256 index) external view returns (bytes32) {
        return strategies.at(index);
    }

    /// @notice Lists the strategy hashes within a specific range in the listing.
    function getStrategies(uint256 from, uint256 amount)
        external
        view
        returns (bytes32[] memory)
    {
        return getPaginated(strategies, from, amount);
    }

    //** underlying */

    /// @notice Gets the length (number) of strategies of a project listing.
    function getUnderlyingStrategiesLength(IERC20 token)
        external
        view
        returns (uint256)
    {
        return underlyingToStrategies[token].length();
    }

    /// @notice Gets the underlying tokens's strategy hash at a specific index in the listing.
    function getUnderlyingStrategyAt(IERC20 token, uint256 index)
        external
        view
        returns (bytes32)
    {
        return underlyingToStrategies[token].at(index);
    }

    /// @notice Lists the underlying tokens's strategy hashes within a specific range in the listing.
    function getUnderlyingStrategies(
        IERC20 token,
        uint256 from,
        uint256 amount
    ) external view returns (bytes32[] memory) {
        return getPaginated(underlyingToStrategies[token], from, amount);
    }

    function getPaginated(
        EnumerableSetUpgradeable.Bytes32Set storage set,
        uint256 from,
        uint256 amount
    ) private view returns (bytes32[] memory) {
        uint256 length = set.length();
        if (from >= length) {
            return new bytes32[](0);
        }

        if (from + amount > length) {
            amount = length - from;
        }

        bytes32[] memory types = new bytes32[](amount);
        for (uint256 i = 0; i < amount; i++) {
            types[i] == strategyTypes.at(from + i);
        }
        return types;
    }
}
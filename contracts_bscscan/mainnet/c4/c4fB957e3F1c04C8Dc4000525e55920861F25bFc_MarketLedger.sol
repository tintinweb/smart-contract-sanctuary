//	                                                                                                                                                 
//                                                               .....'''',,,,,,,'''.....                                                               
//                                                       ...',;:clllooooooooooooooooooollc::;,'..                                                       
//                                                  ..,;:llooooooooooooooooooooooooooooooooooooolc:,'.                                                  
//                                              .';cloooooooooollc::;;,,,'''''''',,,,;;:clloooooooooolc:,..                                             
//                                          .';cloooooooolc;,'...                        ...',;:clooooooool:,.                                          
//                                       .,:loooooool:;'..                                      ..',:cooooooolc;..                                      
//                                    .,:looooool:,..                                                ..,;coooooooc;.                                    
//                                  ':looooooc;'.                                                        ..,cloooool:'.                                 
//                               .,cooooool;..                                                               .,cooooooc;.                               
//                             .,loooool:'.                                                                     .;loooool;.                             
//                           .,loooool;.                                                                          .,coooool;.                           
//                          'coooool,.                                                                              .'coooool;.                         
//                        .:oooool,.    .d000000000000000k;                                       ':::::::::::::'     .'coooooc'                        
//                      .,looool;.      '0MMMMMMMMMMMMMMMMX:                                    .;loooooooooooo;.       .,looool:.                      
//                     .:oooooc.        '0MMMMMMMMMMMMMMMMMXl                                  .:ooooooooooool,           .:oooooc'                     
//                    'cooool;.         '0MMMMMMMMMMMMMMMMMMNo.                               .:ooooooooooool'              ,looool,                    
//                   ,looool'           '0MMMMMMMMMMMMMMMMMMMWd.                             .cooooooooooool'                .cooooo;.                  
//                  ,looooc.            '0MMMMMMMMMMMMMMMMMMMMWx.                           .cooooooooooooc.                  .:ooooo:.                 
//                 ;ooooo:.             '0MMMMMMMMMMMMMMMMMMMMMWk'                         'loooooooooooo:.     ..             .;ooooo:.                
//                ,loooo:.              '0MMMMMMMMMMMMMMMMMMMMMMWO,                       ,loooooooooooo:.     ;O:               ;ooooo:.               
//               'loooo:.               '0MMMMMMMMMMMMMMMMMMMMMMMM0;                    .;loooooooooool;.     :KN:                ;ooooo;               
//              .cooooc.                '0MMMMMMMMMMMMMMMMMMMMMMMMMK:                  .:ooooooooooool,     .lNMN:                .:ooool'              
//             .:ooool'                 '0MMMMMMMMMMMMMMMMMMMMMMMMMMXc                .:ooooooooooool,     .dNMMX:                 .cooooc.             
//             'loooo;                  '0MMMMMMMMMMMMMMMMMMMMMMMMMMMXl.             .cooooooooooool'     .xWMMMN:                  'loooo;             
//            .cooooc.                  '0MMMMMMMMMMMMWXNMMMMMMMMMMMMMNo.           'cooooooooooooc.     'OWMMMMN:                  .:ooool.            
//            'loooo,                   '0MMMMMMMMMMMMWd:KMMMMMMMMMMMMMWd.         'looooooooooooc.     ;0MMMMMMN:                   .loooo;            
//           .:ooooc.                   '0MMMMMMMMMMMMWo ,OWMMMMMMMMMMMMWk.       ,loooooooooooo:.     cXMMMMMMMN:                   .:ooooc.           
//           .loooo:                    '0MMMMMMMMMMMMWo  .xWMMMMMMMMMMMMWO'     ;loooooooooooo;.    .lNMMMMMMMMN:                    ,loooo'           
//           'loooo,                    '0MMMMMMMMMMMMWo   .dNMMMMMMMMMMMMW0,  .:ooooooooooool;     .dNMMMMMMMMMN:                    .loooo;           
//           ;ooool.                    '0MMMMMMMMMMMMWo     lXMMMMMMMMMMMMM0oc:ooooooooooool,      :NMMMMMMMMMMN:                    .cooooc.          
//           :ooooc.                    '0MMMMMMMMMMMMWo      ;KMMMMMMMMMMMW0xdooooooooooool'       cWMMMMMMMMMMN:                    .:ooooc.          
//          .:ooooc.                    '0MMMMMMMMMMMMWo       ,OWMMMMMMMWXkooooooooooooooc'        cWMMMMMMMMMMN:                     ;ooool.          
//          .:ooooc.                    '0MMMMMMMMMMMMWo        .xWMMMMMWKxooooooooooooooc.         cWMMMMMMMMMMN:                     ;ooool.          
//          .:ooooc.                    '0MMMMMMMMMMMMWo         .dNMMMNOdoooooooooooooo:.          cWMMMMMMMMMMN:                     ;ooooc.          
//           ;ooool.                    '0MMMMMMMMMMMMWo          .lXWKxooooooooooooooo:.           cWMMMMMMMMMMN:                    .:ooooc.          
//           ,ooool'                    '0MMMMMMMMMMMMWo            :xdooooooooooooooo;.            cWMMMMMMMMMMN:                    .coooo:.          
//           'loooo,                    '0MMMMMMMMMMMMWo            ,looooooooooooool,              cWMMMMMMMMMMN:                    .loooo,           
//           .coooo:.                   '0MMMMMMMMMMMMWo          .;oooooooooooooool,               cWMMMMMMMMMMN:                    ;ooool.           
//            ;ooool'                   '0MMMMMMMMMMMMWo         .:looooooooooooooc'                cWMMMMMMMMMMN:                   .coooo:.           
//            .loooo:.                  '0MMMMMMMMMMMMWo        .:oooooooooooooooc.                 cWMMMMMMMMMMN:                   ,ooooo,            
//             ;ooool'                  '0MMMMMMMMMMMMWo       .coooooooooooooooc.                  cWMMMMMMMMMMN:                  .cooooc.            
//             .cooooc.                 '0WWWWWWWWWWWWWl      .cooooooooooooooo:.                   cNWWWWWWWWWWX:                  ;ooool'             
//              ,loooo;                  ';;;;;;;;;;;;;.     'loooooooooooooll;.                    .,;;;;;;;;;;,.                 ,loooo:.             
//              .:ooool,                                    ,loooooooooooool,.                                                    .looooc.              
//               .:ooool'                                  ,loooooooooooool,                                                     .cooool.               
//                .cooool'                               .;looooooooooooool:;,;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;,;;;,.              .cooool'                
//                 .cooool,                             .:ooooooooooooooooooooooooooooooooooooooooooooooooooooool.             .cooool'                 
//                  .cooool;.                          .:oooooooooooooooooooooooooooooooooooooooooooooooooooooool.            'looool'                  
//                   .:ooooo:.                        .cooooooooooooooooooooooooooooooooooooooooooooooooooooooool.          .;looooc.                   
//                    .;looool,                      .coooooooooooooooooooooooooooooooooooooooooooooooooooooooool.         .cooooo:.                    
//                      'cooool:.                   'looooooooooooooooooooooooooooooooooooooooooooooooooooooooool.       .;looool;.                     
//                       .:oooool;.                'loooooooooooooooooooooooooooooooooooooooooooooooooooooooooool.      'coooooc.                       
//                         'coooooc,.              ..............................................................     'coooool,.                        
//                          .,loooooc,.                                                                            .'coooool;.                          
//                            .;loooool;.                                                                        .,coooool:.                            
//                              .,cooooolc,.                                                                  .':loooool;.                              
//                                .'cloooool:'.                                                            .';looooooc,.                                
//                                   .;cooooool:,..                                                     .,:looooool:'.                                  
//                                     .':loooooool:,..                                            ..';clooooool:,.                                     
//                                        .';clooooooolc;,...                                 ..';:looooooool:'.                                        
//                                            .,:cloooooooollc:;,'......          ......',,;:cloooooooool:,..                                           
//                                               ..';:looooooooooooolllccccc:::cccclllooooooooooooolc;,..                                               
//                                                    ..',;clloooooooooooooooooooooooooooooollc:;'..                                                    
//                                                          ...'',;;::cccllllllllccc::;;,,'...                                                          
//            
//
//                                                            
// https://t.me/MarketLedgerOfficial
// https://marketledger.co.uk
//
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;
import "./Libraries.sol";



////////////////////////////////////////////////////////////////////////////////////////////////////////
//ML Contract /////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////
contract MarketLedger is IBEP20, Ownable
{
    using EnumerableSet for EnumerableSet.AddressSet;
    
    mapping (address => uint256) private _balances;
    mapping (address => mapping (address => uint256)) private _allowances;
    mapping (address=>uint) customTransferTaxes;

    mapping(address=>bool) private _excluded;
    mapping(address=>bool) private _excludedFromStaking;

    mapping(address=>bool) private _automatedMarketMakers;
    
    //Token Info
    string private constant _name = 'MarketLedger';
    string private constant _symbol = 'ML';
    uint8 private constant _decimals = 18;
    uint256 public constant InitialSupply= 5 * 10**6 * 10**_decimals;//equals 5,000,000 token

    //The time Liquidity gets locked at start and prolonged once it gets released
    uint256 private constant DefaultLiquidityLockTime=7 days;

    //variables that track balanceLimit and sellLimit,
    //can be updated based on circulating supply and Sell- and BalanceLimitDividers
    uint256 private _circulatingSupply;

    //Limits max tax, only gets applied for tax changes, doesn't affect inital Tax
    uint public constant MaxTax=250;
    //Tracks the current Taxes, different Taxes can be applied for buy/sell/transfer
    //Taxes can never exceed MaxTax
    uint private _buyTax=15;
    uint private _sellTax=300;
    uint private _transferTax=0;
    //The shares of the specific Taxes, always needs to equal 100%
    uint private _liquidityTax=200;
    uint private _stakingTax=400;
    uint private _burnTax=200;
    uint private _marketingTax=200;
    uint private constant TaxDenominator=1000;
    //determines the permille of the pancake pair needed to trigger Liquify
    uint8 public LiquifyTreshold=2;
    
    //_pancakePairAddress is also equal to the liquidity token address
    //LP token are locked in the contract
    address private _pancakePairAddress; 
    IDexRouter private  _pancakeRouter; 
    //TestNet
    //address private constant PancakeRouter=0x9Ac64Cc6e4415144C455BD8E4837Fea55603e5c3;
    //MainNet
    address private constant PancakeRouter=0x10ED43C718714eb63d5aA57B78B54704E256024E;
    address public MarketingWallet;
    //modifier for functions only the team can call
    modifier onlyTeam() {
        require(_isTeam(msg.sender), "Caller not in Team");
        _;
    }
    bool _isInFunction;
    modifier isInFunction(){
        require(!_isInFunction);
        _isInFunction=true;
        _;
        _isInFunction=false;

    }

    function _isTeam(address addr) private view returns (bool){
        return addr==owner()||addr==MarketingWallet;
    }

    constructor () {
        //Creates a Pancake Pair
        _pancakeRouter = IDexRouter(PancakeRouter);
        _pancakePairAddress = IPancakeFactory(_pancakeRouter.factory()).createPair(address(this), _pancakeRouter.WETH());
        _automatedMarketMakers[_pancakePairAddress]=true;
        //excludes Pancake Pair and contract from staking
        _excludedFromStaking[_pancakePairAddress]=true;
        _excludedFromStaking[address(this)]=true;
        //deployer gets 100% of the supply to create LP
        _addToken(msg.sender,InitialSupply);
        emit Transfer(address(0), msg.sender, InitialSupply);
        //Team wallet deployer and contract are excluded from Taxes
        //contract can't be included to taxes
        MarketingWallet=msg.sender;

        _excluded[MarketingWallet]=true;
        _excluded[msg.sender]=true;
        _excluded[address(this)]=true;
        _approve(address(this), address(_pancakeRouter), type(uint256).max);
    }
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //Transfer functionality////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////

    //picks the transfer function
    function _transfer(address sender, address recipient, uint256 amount) private{
        require(sender != address(0), "from zero");
        require(recipient != address(0), "to zero");

        //excluded adresses are transfering tax and lock free
        if(_excluded[sender] || _excluded[recipient]){
            _feelessTransfer(sender, recipient, amount);
            return;
        }
        //once trading is enabled, it can't be turned off again
        require(tradingEnabled,"trading not yet enabled"); 
        _regularTransfer(sender,recipient,amount);
        //AutoPayout

    }
    //applies taxes, checks for limits, locks generates autoLP and stakingBNB, and autostakes
    function _regularTransfer(address sender, address recipient, uint256 amount) private{
        require(_balances[sender] >= amount, "exceeds balance");
        //checks all registered AMM if it's a buy or sell.
        bool isBuy=_automatedMarketMakers[sender];
        bool isSell=_automatedMarketMakers[recipient];
        uint tax;
        if(isSell)tax=_sellTax;
        else if(isBuy)tax=_buyTax;
        else {
            tax=customTransferTaxes[recipient]>customTransferTaxes[sender]
                ?customTransferTaxes[recipient]:customTransferTaxes[sender];
            tax=tax==0?_transferTax:tax;
        }     
        
        //Swapping AutoLP and MarketingBNB is only possible if sender is not pancake pair, 
        //if its not manually disabled, if its not already swapping
        if((sender!=_pancakePairAddress)&&(!swapAndLiquifyDisabled)&&(!_isSwappingContractModifier))
            _swapContractToken(LiquifyTreshold,false);
            
        _transferTaxed(sender,recipient,amount,tax);
    }
    function _transferTaxed(address sender, address recipient, uint256 amount, uint tax) private{
        uint totalTaxedToken=_calculateFee(amount, tax, TaxDenominator);
        uint burnedToken=_calculateFee(amount,tax,_burnTax);
        uint256 taxedAmount=amount-totalTaxedToken;
        //Removes token and handles staking
        _removeToken(sender,amount);
        //Adds the taxed tokens -burnedToken to the contract
        _addToken(address(this), totalTaxedToken-burnedToken);
        //Burns token
        _circulatingSupply-=burnedToken;
        //Adds token and handles staking
        _addToken(recipient, taxedAmount);
        emit Transfer(sender,recipient,taxedAmount);
        if(!autoPayoutDisabled) _autoPayout();

    }
    //Feeless transfer only transfers and autostakes
    function _feelessTransfer(address sender, address recipient, uint256 amount) private{
        require(_balances[sender] >= amount, ">balance");
        //Removes token and handles staking
        _removeToken(sender,amount);
        //Adds token and handles staking
        _addToken(recipient, amount);
        
        emit Transfer(sender,recipient,amount);

    }

    //Calculates the token that should be taxed
    function _calculateFee(uint256 amount, uint tax, uint taxPercent) private pure returns (uint256) {
        return (amount*tax*taxPercent) / (TaxDenominator*TaxDenominator);
    }
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //BNB Autostake/////////////////////////////////////////////////////////////////////////////////////////
    //////////////////////////////////////////////////////////////////////////////////////////////////////// 
    //Autostake uses the balances of each holder to redistribute auto generated BNB.
    //Each transaction _addToken and _removeToken gets called for the transaction amount
    EnumerableSet.AddressSet private _autoPayoutList;
    function isAutoPayout(address account) public view returns(bool){
        return _autoPayoutList.contains(account);
    }
    uint AutoPayoutCount=15;
    uint MinPayout=10**17/2;//0,05 BNB
    uint256 currentPayoutIndex;

    bool public autoPayoutDisabled;

    event OnDisableAutoPayout(bool disabled);
    function TeamDisableAutoPayout(bool disabled) public onlyTeam{
        autoPayoutDisabled=disabled;
        emit  OnDisableAutoPayout(disabled);
    }
    event OnChangeAutoPayoutCount(uint count); 
    function TeamChangeAutoPayoutCount(uint count) public onlyTeam{
        require(count<=50);
        AutoPayoutCount=count;
        emit OnChangeAutoPayoutCount(count);
    }
    event OnChangeMinPayout(uint treshold); 
    function TeamChangeMinPayout(uint minPayout) public onlyTeam{
        MinPayout=minPayout;
        emit OnChangeAutoPayoutCount(minPayout);
    }
    function TeamSetAutoPayoutAccount(address account, bool enable) public onlyTeam{
        if(enable)_autoPayoutList.add(account);
        else _autoPayoutList.remove(account);
    }



    
    function _autoPayout() private{
        //resets payout counter and moves to next payout token if last holder is reached
        if(currentPayoutIndex>=_autoPayoutList.length()) currentPayoutIndex=0;
        for(uint i=0;i<AutoPayoutCount;i++){
            address current=_autoPayoutList.at(currentPayoutIndex);
            currentPayoutIndex++; 
            if(getDividents(current)>=MinPayout){
                _claimBNB(current);
                i+=3;//if payout happens, increase the counter faster  
            }
            if(currentPayoutIndex>=_autoPayoutList.length()){
                currentPayoutIndex=0;
                return;
            }


        }
    }

    //Multiplier to add some accuracy to profitPerShare
    uint256 private constant DistributionMultiplier = 2**64;
    //profit for each share a holder holds, a share equals a decimal.
    uint256 public profitPerShare;
    //totalShares in circulation +InitialSupply to avoid underflow 
    //getTotalShares returns the correct amount
    uint256 private _totalShares=InitialSupply;
    //the total reward distributed through staking, for tracking purposes
    uint256 public totalStakingReward;
    //the total payout through staking, for tracking purposes
    uint256 public totalPayouts;
    //Mapping of the already paid out(or missed) shares of each staker
    mapping(address => uint256) private alreadyPaidShares;
    //Mapping of shares that are reserved for payout
    mapping(address => uint256) private toBePaid;
    mapping(address => uint256) public totalPayout;

    //adds Token to balances, adds new BNB to the toBePaid mapping and resets staking
    function _addToken(address addr, uint256 amount) private {
        //the amount of token after transfer
        uint256 newAmount=_balances[addr]+amount;
        _circulatingSupply+=amount;
        //if excluded, don't change staking amount
        if(_excludedFromStaking[addr]){
           _balances[addr]=newAmount;
           return;
        }
        _totalShares+=amount;
        //gets the payout before the change
        uint256 payment=_newDividentsOf(addr);
        //resets dividents to 0 for newAmount
        alreadyPaidShares[addr] = profitPerShare * newAmount;
        //adds dividents to the toBePaid mapping
        toBePaid[addr]+=payment; 
        //sets newBalance
        _balances[addr]=newAmount;
        _autoPayoutList.add(addr);

    }
    
    //removes Token, adds BNB to the toBePaid mapping and resets staking
    function _removeToken(address addr, uint256 amount) private {
        //the amount of token after transfer
        uint256 newAmount=_balances[addr]-amount;
        _circulatingSupply-=amount;
        if(_excludedFromStaking[addr]){
           _balances[addr]=newAmount;
           return;
        }

        //gets the payout before the change
        uint256 payment=_newDividentsOf(addr);
        //sets newBalance
        _balances[addr]=newAmount;
        //resets dividents to 0 for newAmount
        alreadyPaidShares[addr] = profitPerShare * getShares(addr);
        //adds dividents to the toBePaid mapping
        toBePaid[addr]+=payment; 
        _totalShares-=amount;
        if(newAmount==0)
        _autoPayoutList.remove(addr);
    }
    
    
    //gets the dividents of a staker that aren't in the toBePaid mapping 
    function _newDividentsOf(address staker) private view returns (uint256) {
        uint256 fullPayout = profitPerShare * getShares(staker);
        //if excluded from staking or some error return 0
        if(fullPayout<=alreadyPaidShares[staker]) return 0;
        return (fullPayout - alreadyPaidShares[staker]) / DistributionMultiplier;
    }
    
    //distributes bnb between marketing share and dividents 
    function _distributeStake(uint256 AmountWei) private {
        // Deduct marketing Tax
        if(AmountWei==0) return;      

        uint256 totalShares=getTotalShares();
        //when there are 0 shares, add everything to marketing budget
        if (totalShares == 0) {
            (bool sent,)=MarketingWallet.call{value:AmountWei}("");
            sent=true;
        }else{
            totalStakingReward += AmountWei;
            //Increases profit per share based on current total shares
            profitPerShare += ((AmountWei * DistributionMultiplier) / totalShares);
        }
    }
    function AddFunds() public payable isInFunction{
        _distributeStake(msg.value);
    }
    function AddFundsTo(address Account) public payable isInFunction{
        toBePaid[Account]+=msg.value;
        totalStakingReward += msg.value;
    } 
    //Sets dividents to 0 returns dividents
    function _substractDividents(address addr) private returns (uint256){
        uint256 amount=getDividents(addr);
        if(amount==0) return 0;
        if(!_excludedFromStaking[addr]){
            alreadyPaidShares[addr] = profitPerShare * getShares(addr);
        }
        toBePaid[addr]=0;
        totalPayout[addr]+=amount;
        return amount;
    }
    //Manually claimRewards
    function ClaimRewards() public isInFunction{
        _claimBNB(msg.sender);
    }
    function _claimBNB(address account) private{
        uint256 amount=_substractDividents(account);
        if(amount==0) return;
        //Substracts the amount from the dividents
        totalPayouts+=amount;  
        (bool sent,)=account.call{value:amount,gas:30000}("");
        if(!sent){
            //if payout fails, revert payment
            toBePaid[account]+=amount;
            totalPayouts-=amount;
         }
        
 
    }
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //Swap Contract Tokens//////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    
    //tracks auto generated BNB, useful for ticker etc
    uint256 public totalLPBNB;
    //Locks the swap if already swapping
    bool private _isSwappingContractModifier;
    modifier lockTheSwap {
        _isSwappingContractModifier = true;
        _;
        _isSwappingContractModifier = false;
    }
    uint public overLiquifyTreshold=100; 
    function isOverLiquified() public view returns(bool){
        return _balances[_pancakePairAddress]>_circulatingSupply*overLiquifyTreshold/TaxDenominator;
    }
    function _swapContractToken(uint16 PancakeTreshold,bool ignoreLimits) private lockTheSwap{
        uint contractBalance=_balances[address(this)];
        uint totalTax=_liquidityTax+_stakingTax+_marketingTax;
        if(totalTax==0) return;

            
        uint256 tokenToSwap=_balances[_pancakePairAddress]*PancakeTreshold/TaxDenominator;
        
        //only swap if contractBalance is larger than tokenToSwap or ignore limits
        bool NotEnoughToken=contractBalance<tokenToSwap;
        if(NotEnoughToken){
            if(ignoreLimits)
                tokenToSwap=contractBalance;
            else return;
        }
        uint tokenForLiquidity;
        //if over Liquified, then use 100% of the token for LP
        if(isOverLiquified())tokenForLiquidity=0;
        else tokenForLiquidity=(tokenToSwap*_liquidityTax)/totalTax;

        uint tokenForBNB=tokenToSwap-tokenForLiquidity;


        //splits tokenForLiquidity in 2 halves
        uint liqToken=tokenForLiquidity/2;
        uint liqBNBToken=tokenForLiquidity-liqToken;

        //swaps marktetingToken and the liquidity token half for BNB
        uint swapToken=liqBNBToken+tokenForBNB;
        //Gets the initial BNB balance, so swap won't touch any staked BNB
        uint initialBNBBalance = address(this).balance;
        _swapTokenForBNB(swapToken);
        uint newBNB=(address(this).balance - initialBNBBalance);
        //calculates the amount of BNB belonging to the LP-Pair and converts them to LP
        uint liqBNB = (newBNB*liqBNBToken)/swapToken;
        if(liqBNB>0) _addLiquidity(liqToken, liqBNB);
        //Get the BNB balance after LP generation to get the
        //exact amount of token left for Staking, as LP generation leaves some BNB untouched
        uint distributeBNB=(address(this).balance - initialBNBBalance);

        uint marketingBNB=distributeBNB*_marketingTax/(_marketingTax+_stakingTax);
        uint stakingBNB=distributeBNB-marketingBNB;
        //distributes BNB between stakers
        _distributeStake(stakingBNB);
    }
    //swaps tokens on the contract for BNB
    function _swapTokenForBNB(uint256 amount) private {
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = _pancakeRouter.WETH();

        _pancakeRouter.swapExactTokensForETHSupportingFeeOnTransferTokens(
            amount,
            0,
            path,
            address(this),
            block.timestamp
        );
    }
    //Adds Liquidity directly to the contract where LP are locked(unlike safemoon forks, that transfer it to the owner)
    function _addLiquidity(uint256 tokenamount, uint256 bnbamount) private {
        totalLPBNB+=bnbamount;
        try _pancakeRouter.addLiquidityETH{value: bnbamount}(
            address(this),
            tokenamount,
            0,
            0,
            address(this),
            block.timestamp
        ){}
        catch{}
    }

    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //public functions /////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
        //gets shares of an address, returns 0 if excluded
    function getShares(address addr) public view returns(uint256){
        if(_excludedFromStaking[addr]) return 0;
        return _balances[addr];
    }

    //Total shares equals circulating supply minus excluded Balances
    function getTotalShares() public view returns (uint256){
        return _totalShares-InitialSupply;
    }

    function getLiquidityLockSeconds() public view returns (uint256 LockedSeconds){
        if(block.timestamp<_liquidityUnlockTime)
            return _liquidityUnlockTime-block.timestamp;
        return 0;
    }

    function getTaxes() public view returns(
    uint256 buyTax, 
    uint256 sellTax, 
    uint256 transferTax, 
    uint256 liquidityTax,
    uint256 stakingTax,
    uint256 marketingTax,
    uint burnTax){
            buyTax=_buyTax;
            sellTax=_sellTax;
            transferTax=_transferTax;
            liquidityTax=_liquidityTax;
            stakingTax=_stakingTax;
            marketingTax=_marketingTax;
            burnTax=_burnTax;


    }
    
    function getStatus(address account) public view returns(
        bool Excluded, 
        bool ExcludedFromStaking
        ){
        return(
            _excluded[account],
            _excludedFromStaking[account]
            );
    }
    
    //Returns the not paid out dividents of an address in wei
    function getDividents(address addr) public view returns (uint256){
        return _newDividentsOf(addr)+toBePaid[addr];
    }
    
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //Settings//////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////

    bool public swapAndLiquifyDisabled;
    event  OnAddAMM(address AMM,bool Add);
    function TeamAddOrRemoveAMM(address AMMPairAddress, bool Add) public onlyTeam{
        require(AMMPairAddress!=_pancakePairAddress,"can't change Pancake");
        if(Add){
            if(!_excludedFromStaking[AMMPairAddress])
                TeamSetStakingExcluded(AMMPairAddress, true);
            _automatedMarketMakers[AMMPairAddress]=true;
        } 
        else{
            _automatedMarketMakers[AMMPairAddress]=false;
        }
        emit OnAddAMM(AMMPairAddress, Add);
    }

    function SetCustomTransferTaxes(uint taxes, address account) public onlyTeam{
        require(taxes<MaxTax);
        customTransferTaxes[account]=taxes;
    }
    function TeamChangeTeamWallet(address newTeamWallet) public{
        require(msg.sender==MarketingWallet);
        MarketingWallet=newTeamWallet;
    }
    event  OnChangeLiquifyTreshold(uint8 TresholdPermille);
    function TeamSetLiquifyTreshold(uint8 TresholdPermille) public onlyTeam{
        require(TresholdPermille<=50);
        require(TresholdPermille>0);
        LiquifyTreshold=TresholdPermille;
        emit OnChangeLiquifyTreshold(TresholdPermille);
    }

    event  OnChangeOverLiquifyTreshold(uint8 TresholdPermille);
    function TeamSetOverLiquifyTreshold(uint8 TresholdPermille) public onlyTeam{
        
        require(TresholdPermille<=TaxDenominator);
        overLiquifyTreshold=TresholdPermille;
        emit OnChangeOverLiquifyTreshold(TresholdPermille);
    }

    
    
    event  OnSwitchSwapAndLiquify(bool Disabled);
    //switches autoLiquidity and marketing BNB generation during transfers
    function TeamSwitchSwapAndLiquify(bool disabled) public onlyTeam{
        swapAndLiquifyDisabled=disabled;
        emit OnSwitchSwapAndLiquify(disabled);
    }
    event OnChangeTaxes(uint liquidityTaxes, uint stakingTaxes,uint buyTaxes,uint marketingTaxes,uint burn, uint sellTaxes, uint transferTaxes);
    //Sets Taxes, is limited by MaxTax(25%) to make it impossible to create honeypot
    function TeamSetTaxes(uint liquidityTaxes, uint stakingTaxes,uint marketingTax,uint burnTax, uint buyTax, uint sellTax, uint transferTax) public onlyTeam{
        uint totalTax=liquidityTaxes+stakingTaxes+marketingTax+burnTax;
        require(totalTax==TaxDenominator);
        require(buyTax<=MaxTax&&sellTax<=MaxTax&&transferTax<=MaxTax);
    
        _marketingTax=marketingTax;
        _burnTax=burnTax;
        _liquidityTax=liquidityTaxes;
        _stakingTax=stakingTaxes;
        
        _buyTax=buyTax;
        _sellTax=sellTax;
        _transferTax=transferTax;
        emit OnChangeTaxes(liquidityTaxes, stakingTaxes, marketingTax, burnTax, buyTax, sellTax, transferTax);
    }

    //manually converts contract token to LP and staking BNB
    function TeamTriggerLiquify(uint16 pancakePermille, bool ignoreLimits) public onlyTeam{
        _swapContractToken(pancakePermille,ignoreLimits);
    }
    
    event OnExcludeFromStaking(address addr, bool exclude);
    //Excludes account from Staking
    function TeamSetStakingExcluded(address addr, bool exclude) public onlyTeam{
        uint256 shares;
        if(exclude){
            require(!_excludedFromStaking[addr]);
            uint256 newDividents=_newDividentsOf(addr);
            shares=getShares(addr);
            _excludedFromStaking[addr]=true; 
            _totalShares-=shares;
            alreadyPaidShares[addr]=shares*profitPerShare;
            toBePaid[addr]+=newDividents;
            _autoPayoutList.remove(addr);

        } else _includeToStaking(addr);
        emit OnExcludeFromStaking(addr, exclude);
    }    

    //function to Include own account to staking, should it be excluded
    function IncludeMeToStaking() public{
        _includeToStaking(msg.sender);
    }
    function _includeToStaking(address addr) private{
        require(_excludedFromStaking[addr]);
        _excludedFromStaking[addr]=false;
        uint256 shares=getShares(addr);
        _totalShares+=shares;
        //sets alreadyPaidShares to the current amount
        alreadyPaidShares[addr]=shares*profitPerShare;
        _autoPayoutList.add(addr);
    }
    event OnExclude(address addr, bool exclude);
    //Exclude/Include account from fees and locks (eg. CEX)
    function TeamSetExcludedStatus(address account,bool excluded) public onlyTeam {
        require(account!=address(this),"can't Include the contract");   
        _excluded[account]=excluded;
        emit OnExclude(account, excluded);
    }
    event ContractBurn(uint256 amount);
    //Burns token on the contract, like when there is a very large backlog of token
    //or for scheudled BurnEvents
    function TeamBurnContractToken(uint8 percent) public onlyTeam{
        require(percent<=100);
        uint256 burnAmount=_balances[address(this)]*percent/100;
        _removeToken(address(this),burnAmount);
        emit Transfer(address(this), address(0), burnAmount);
        emit ContractBurn(burnAmount);
    }
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //Setup Functions///////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////

    //Creates LP using Payable Amount, LP automatically land on the contract where they get locked
    //once Trading gets enabled
    bool public tradingEnabled;    
    event OnTradingOpen();
    //Enables trading. Turns on bot protection and Locks LP for default Lock time
    function SetupEnableTrading() public onlyTeam{
        require(!tradingEnabled);
        tradingEnabled=true;
        _liquidityUnlockTime=block.timestamp+DefaultLiquidityLockTime; 
        emit OnTradingOpen();
    }
    
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //Liquidity Lock////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //the timestamp when Liquidity unlocks
    uint256 private _liquidityUnlockTime;
    bool public liquidityRelease20Percent;
    event  LimitReleaseTo20Percent();
    //Sets Liquidity Release to 20% at a time and prolongs liquidity Lock for a Week after Release. 
    //Should be called once start was successful.
    function TeamlimitLiquidityReleaseTo20Percent() public onlyTeam{
        liquidityRelease20Percent=true;
        emit LimitReleaseTo20Percent();
    }
    
    //Prolongs the Liquidity Lock. Lock can't be reduced
    event ProlongLiquidityLock(uint256 secondsUntilUnlock);
    function TeamLockLiquidityForSeconds(uint256 secondsUntilUnlock) public onlyTeam{
        _prolongLiquidityLock(secondsUntilUnlock+block.timestamp);
        emit ProlongLiquidityLock(secondsUntilUnlock);
    }
    function _prolongLiquidityLock(uint256 newUnlockTime) private{
        // require new unlock time to be longer than old one
        require(newUnlockTime>_liquidityUnlockTime);
        _liquidityUnlockTime=newUnlockTime;
    }
    event OnRemoveRemainingBNB();
    function TeamRemoveRemainingBNB() public onlyTeam{
        require(block.timestamp >= _liquidityUnlockTime+30 days, "Locked");
        _liquidityUnlockTime=block.timestamp+DefaultLiquidityLockTime;
        (bool sent,)=MarketingWallet.call{value:address(this).balance}("");
        sent=true;
        emit OnRemoveRemainingBNB();
    }

    event OnReleaseLP();
    //Release Liquidity Tokens once unlock time is over
    function LiquidityRelease() public onlyTeam {
        //Only callable if liquidity Unlock time is over
        require(block.timestamp >= _liquidityUnlockTime, "Not yet unlocked");

        IBEP20 liquidityToken = IBEP20(_pancakePairAddress);
        uint amount = liquidityToken.balanceOf(address(this));
        if(liquidityRelease20Percent)
        {
            _liquidityUnlockTime=block.timestamp+DefaultLiquidityLockTime;
            //regular liquidity release, only releases 20% at a time and locks liquidity for another week
            amount=amount*2/10;
        }
        liquidityToken.transfer(msg.sender, amount);
        emit OnReleaseLP();
    }



    //Allows the team to withdraw token that get's accidentally sent to the contract(happens way too often)
    //Can't withdraw the LP token, this token or the promotion token
    function TeamWithdrawStrandedToken(address strandedToken) public onlyTeam{
        require((strandedToken!=_pancakePairAddress)&&strandedToken!=address(this));
        IBEP20 token=IBEP20(strandedToken);
        token.transfer(MarketingWallet,token.balanceOf(address(this)));
    }

    
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //external//////////////////////////////////////////////////////////////////////////////////////////////
    ////////////////////////////////////////////////////////////////////////////////////////////////////////

    receive() external payable {
        //only allow pancakeRouter to send BNB
        require(msg.sender==address(PancakeRouter));
    }
    // IBEP20

    function getOwner() external view override returns (address) {
        return owner();
    }
    function name() external pure override returns (string memory) {
        return _name;
    }
    function symbol() external pure override returns (string memory) {
        return _symbol;
    }
    function decimals() external pure override returns (uint8) {
        return _decimals;
    }
    function totalSupply() external view override returns (uint256) {
        return _circulatingSupply;
    }
    function balanceOf(address account) external view override returns (uint256) {
        return _balances[account];
    }
    function transfer(address recipient, uint256 amount) external override returns (bool) {
        _transfer(msg.sender, recipient, amount);
        return true;
    }
    function allowance(address _owner, address spender) external view override returns (uint256) {
        return _allowances[_owner][spender];
    }
    function approve(address spender, uint256 amount) external override returns (bool) {
        _approve(msg.sender, spender, amount);
        return true;
    }
    function _approve(address owner, address spender, uint256 amount) private {
        require(owner != address(0));
        require(spender != address(0));

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }
    function transferFrom(address sender, address recipient, uint256 amount) external override returns (bool) {
        _transfer(sender, recipient, amount);
        uint256 currentAllowance = _allowances[sender][msg.sender];
        require(currentAllowance >= amount);

        _approve(sender, msg.sender, currentAllowance - amount);
        return true;
    }
    // IBEP20 - Helpers
    function increaseAllowance(address spender, uint256 addedValue) external returns (bool) {
        _approve(msg.sender, spender, _allowances[msg.sender][spender] + addedValue);
        return true;
    }
    function decreaseAllowance(address spender, uint256 subtractedValue) external returns (bool) {
        uint256 currentAllowance = _allowances[msg.sender][spender];
        require(currentAllowance >= subtractedValue);

        _approve(msg.sender, spender, currentAllowance - subtractedValue);
        return true;
    }
}